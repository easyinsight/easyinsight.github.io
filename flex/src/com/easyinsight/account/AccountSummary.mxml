<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:customupload="com.easyinsight.customupload.*" width="100%"
           height="100%" xmlns:account="com.easyinsight.account.*">
    <mx:Script>
		<![CDATA[
        import com.easyinsight.framework.User;
        import mx.collections.ArrayCollection;
        import mx.events.ItemClickEvent;
        import mx.managers.PopUpManager;

        private var _account:Account;
        [Bindable]
        private var accountName:String;
        [Bindable]
        private var showToggle:Boolean = false;
        [Bindable]
        private var toggleOptions:ArrayCollection;

        public function set account(account:Account):void {
            this._account = account;
            if (account == null) {
            } else {
                this.accountName = User.getInstance().userName;
            }
        }

        override protected function commitProperties():void {
            super.commitProperties();
            coreBox.removeAllChildren();
            if (_account.accountType == Account.FREE) {
                // user profile information, option to upgrade, option to cancel
                toggleOptions = new ArrayCollection([ new AccountOption("Account Settings", new FreeAccountSettings()) ]);
            } else if (_account.accountType == Account.INDIVIDUAL) {
                // user profile information, option to ugprade, option to cancel, billing link, general account settings?
                toggleOptions = new ArrayCollection([ new AccountOption("Account Settings", new IndividualAccountSettings()),
                    new AccountOption("API Settings", new IndividualAPIAdmin())]);
            } else {
                if (User.getInstance() != null && User.getInstance().accountAdmin) {
                    // user profile
                    // space allocation, user licensing?, ssl configuration, ip range configuration
                    // core user administration...add user, remove user, change user privileges
                    toggleOptions = new ArrayCollection([ new AccountOption("Account Administration", new AccountAdminPage()),
                        new AccountOption("User Administration", new UserAdministration()),                            
                        new AccountOption("My Profile", new ProfessionalMyProfile()),
                            new AccountOption("My API Settings", new APIUserAdmin()),
                        new AccountOption("API Administration", new APIAccountAdmin())]);
                    showToggle = true;
                } else {
                    toggleOptions = new ArrayCollection([ new AccountOption("My Profile", new ProfessionalMyProfile()),
                    new AccountOption("My API Settings", new APIUserAdmin())]);
                    showToggle = true;
                }
            }
            coreBox.addChild(toggleOptions.getItemAt(0).container);
        }

        private function choseButton(event:ItemClickEvent):void {
            var accountOption:AccountOption = event.item as AccountOption;
            if (coreBox.getChildren().length > 0) {
                coreBox.removeAllChildren();
            }
            coreBox.addChild(accountOption.container);            
        }

        private function showSecurityStatement():void {
            var window:SecurityStatement = new SecurityStatement();
            PopUpManager.addPopUp(window, this, true);
            PopUpManager.centerPopUp(window);
        }

        private function showTermsOfService():void {
            var window:TermsOfService = new TermsOfService();
            PopUpManager.addPopUp(window, this, true);
            PopUpManager.centerPopUp(window);
        }
		]]>
    </mx:Script>
    <mx:VBox width="100%" height="100%" paddingTop="15">
        <mx:HBox width="100%" horizontalAlign="center">
            <mx:ToggleButtonBar itemClick="choseButton(event)" dataProvider="{toggleOptions}" visible="{showToggle}"/>
        </mx:HBox>
        <mx:HBox id="coreBox" width="100%">
            
        </mx:HBox>
        <mx:Spacer height="30"/>
        <mx:HBox width="100%" horizontalAlign="center">
            <mx:LinkButton textDecoration="underline" label="Security Statement" click="showSecurityStatement()" fontSize="16"/>
            <mx:Spacer width="20"/>
            <mx:LinkButton textDecoration="underline" label="Terms of Service" click="showTermsOfService()" fontSize="16"/>
        </mx:HBox>
    </mx:VBox>
</mx:Canvas>
