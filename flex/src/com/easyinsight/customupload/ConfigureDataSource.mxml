<?xml version="1.0" ?>
<mx:TitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="setupListeners()">
    <mx:Script><![CDATA[
        import com.easyinsight.administration.feed.CredentialsResponse;
        import com.easyinsight.administration.feed.FeedDefinitionData;

        import com.easyinsight.datasources.IServerDataSourceDefinition;
        import com.easyinsight.listing.DataFeedDescriptor;
        import com.easyinsight.framework.CredentialsCache;

        import mx.controls.Alert;
        import mx.managers.PopUpManager;

        private function onKey(event:KeyboardEvent):void {
            if (event.keyCode == Keyboard.ENTER) {
                validate();
            }
        }

        private function setupListeners():void {
            stage.addEventListener(KeyboardEvent.KEY_UP, onKey);
        }

        private var _dataSourceClass:Class;
        private var dataSourceCreation:IServerDataSourceCreation;
        private var _dataSourceID:int;
        private var _dataSourceDefinition:FeedDefinitionData;

        private var _onlyDataSource:Boolean;

        [Bindable]
        private var ready:Boolean;


        private var _credentialsSaved:Boolean = false;

        [Bindable(event="credentialsSavedChanged")]
        public function get credentialsSaved():Boolean {
            return _credentialsSaved;
        }

        public function set credentialsSaved(value:Boolean):void {
            if (_credentialsSaved == value) return;
            _credentialsSaved = value;
            dispatchEvent(new Event("credentialsSavedChanged"));
        }

        public function set onlyDataSource(value:Boolean):void {
            _onlyDataSource = value;
        }

        override protected function createChildren():void {
            super.createChildren();
            if (_dataSourceDefinition == null) {
                feedService.getFeedDefinition.send(_dataSourceID);
            } else {
                ready = true;
                dataSourceCreation = new _dataSourceClass();
                coreContent.addChild(dataSourceCreation as DisplayObject);
            }
        }

        private function gotDataSource():void {
            dataSourceDefinition = feedService.getFeedDefinition.lastResult as FeedDefinitionData;
            ready = true;
            var configClass:Class = (dataSourceDefinition as IServerDataSourceDefinition).configClass();
            dataSourceCreation = new configClass();
            coreContent.addChild(dataSourceCreation as DisplayObject);
        }

        public function set dataSourceID(value:int):void {
            _dataSourceID = value;
        }

        public function get dataSourceDefinition():FeedDefinitionData {
            return _dataSourceDefinition;
        }

        public function set dataSourceDefinition(value:FeedDefinitionData):void {
            _dataSourceDefinition = value;
        }

        private function validate():void {
            progressBar.visible = true;
            dataSourceCreation.updateDataSource(dataSourceDefinition);
            uploadService.validateCredentials.send(dataSourceDefinition, dataSourceCreation.credentials);
        }

        private function validated():void {
            var validation:String = uploadService.validateCredentials.lastResult as String;
            if (validation == null) {
                dataSourceDefinition.refreshDataInterval = refreshStepper.value * 1000 * 60 * 60;
                dataSourceCreation.updateDataSource(dataSourceDefinition);
                feedService.updateFeedDefinition.send(dataSourceDefinition, "", null);
            } else {
                progressBar.visible = false;
                Alert.show(validation);
            }
        }

        private function created():void {
            CredentialsCache.getCache().addCredentials(dataSourceDefinition.dataFeedID, dataSourceCreation.credentials);
            uploadService.refreshData.send(dataSourceDefinition.dataFeedID, dataSourceCreation.credentials, credentialsSaved, true);
        }

        private function refreshed():void {
            progressBar.visible = true;
            var response:CredentialsResponse = uploadService.refreshData.lastResult as CredentialsResponse;
            if (response.successful) {
                var descriptor:DataFeedDescriptor = new DataFeedDescriptor();
                descriptor.dataFeedID = dataSourceDefinition.dataFeedID;
                descriptor.name = dataSourceDefinition.feedName;
                dispatchEvent(new DataSourceConfiguredEvent(descriptor));
                stage.removeEventListener(KeyboardEvent.KEY_UP, onKey);
                PopUpManager.removePopUp(this);
            } else {
                Alert.show(response.failureMessage);
            }
        }

        public function set dataSourceClass(value:Class):void {
            _dataSourceClass = value;
        }

        private function credentialClick():void {
            credentialsSaved = !credentialsSaved;
            if (!credentialsSaved) {
                refreshStepper.value = 0;
            }
        }
        ]]></mx:Script>
    <mx:RemoteObject destination="userUpload" id="uploadService">
        <mx:method name="validateCredentials" result="validated()"/>
        <mx:method name="refreshData" result="refreshed()"/>
    </mx:RemoteObject>
    <mx:RemoteObject destination="feeds" id="feedService">
        <mx:method name="updateFeedDefinition" result="created()"/>
        <mx:method name="getFeedDefinition" result="gotDataSource()"/>
    </mx:RemoteObject>
    <mx:Form>
        <mx:FormItem label="">
            <mx:Label text="Now that you've installed this solution, you need to configure it:" fontSize="14"/>
        </mx:FormItem>
        <mx:Box id="coreContent"/>
        <mx:FormItem label="" direction="vertical">
            <mx:ProgressBar id="progressBar" indeterminate="true" visible="false"/>
        </mx:FormItem>
        <mx:FormItem label="Save Credentials:" enabled="{ready}" direction="horizontal">
            <mx:CheckBox id="saveCredentialsCheckbox" click="credentialClick()"/>
            <mx:Label textDecoration="underline" text=""/>
        </mx:FormItem>
        <mx:FormItem label="Hourly Refresh:" enabled="{credentialsSaved}">
            <mx:NumericStepper id="refreshStepper"/>
        </mx:FormItem>
        <mx:FormItem label="" direction="horizontal">
            <mx:Button label="Create" click="validate()" enabled="{ready}"/>
            <mx:Button label="Cancel" click="stage.removeEventListener(KeyboardEvent.KEY_UP, onKey); PopUpManager.removePopUp(this)"/>
        </mx:FormItem>
	</mx:Form>
</mx:TitleWindow>