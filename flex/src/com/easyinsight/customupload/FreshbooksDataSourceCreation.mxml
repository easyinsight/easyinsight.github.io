<?xml version="1.0" ?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" implements="com.easyinsight.customupload.IServerDataSourceCreation">
    <mx:Metadata>
		[Event(name="userConfigComplete", type="com.easyinsight.customupload.UploadConfigEvent")]
		[Event(name="userConfigCancel", type="com.easyinsight.customupload.UploadConfigEvent")]
	</mx:Metadata>
    <mx:Script><![CDATA[
        import com.easyinsight.administration.feed.FeedDefinitionData;
        import com.easyinsight.datasources.FreshbooksDataSource;
        import com.easyinsight.util.ProgressAlert;

        import mx.controls.Alert;
        import mx.validators.Validator;

        [Bindable]
        private var url:String;

        public function get dataSourceDefinition():FeedDefinitionData {
            var obj:FreshbooksDataSource = new FreshbooksDataSource();
            obj.url = urlInput.text;
            return obj;
        }

        public function set dataSourceDefinition(feedDefinition:FeedDefinitionData):void {
            var ds:FreshbooksDataSource = feedDefinition as FreshbooksDataSource;
            url = ds.url;
        }

        public function get savesCredentials():Boolean {
            return false;
        }

        public function updateDataSource(feedDefinition:FeedDefinitionData):void {
            var ds:FreshbooksDataSource = feedDefinition as FreshbooksDataSource;
            ds.url = urlInput.text;
            ds.pin = verifierInput.text;
        }

        private function install():void {
            var results:Array = Validator.validateAll([nameValidator]);
            if (results.length > 0) {
                urlInput.setFocus();
                urlInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
            } else {
                ProgressAlert.alert(this, "Retrieving access information...", null, tokenService.getOAuthURL);
                tokenService.getOAuthURL.send(49, 0, urlInput.text);
            }
        }

        private function gotURL():void {
            var response:OAuthResponse = tokenService.getOAuthURL.lastResult as OAuthResponse;
            if (response.successful) {
                stackIndex = 1;
                var requestToken:String = response.requestToken;
                navigateToURL(new URLRequest(requestToken));
            } else {
                if (response.error == OAuthResponse.BAD_HOST) {
                    Alert.show("We had a problem with the account name you specified. Please double check the value you entered. For example, if you access Freshbooks by easyinsight.freshbooks.com, your account name would be easyinsight.");
                } else {
                    Alert.show("Freshbooks rejected the specified verifier token. Please try authorizing again.");
                }
            }
        }

        [Bindable]
        private var stackIndex:int = 0;

        public function validate():Boolean {
            urlInput.errorString = null;
            installButton.errorString = null;
            if (verifierInput != null) {
                verifierInput.errorString = null;
            }
            if (urlInput.text.length == 0) {
                urlInput.errorString = "You need to specify the location of your Freshbooks account.";
                urlInput.setFocus();
                urlInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }

            if (stackIndex == 0) {
                installButton.errorString = "You need to authorize Easy Insight to access your Freshbooks data.";
                installButton.setFocus();
                installButton.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }

            if (verifierInput.text.length == 0) {
                verifierInput.errorString = "You need to enter your Verifier from Freshbooks here.";
                verifierInput.setFocus();
                verifierInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return false;
            }
            return true;
        }]]></mx:Script>
    <mx:RemoteObject destination="tokenService" id="tokenService">
        <mx:method name="getOAuthURL" result="gotURL()"/>
    </mx:RemoteObject>
    <mx:HBox>
        <mx:Label text="Freshbooks Account Name:"/>
        <mx:TextInput id="urlInput" fontWeight="normal" fontFamily="Lucida Grande" text="{url}" width="300"/>
    </mx:HBox>
    <mx:Label text="For example, if you access Freshbooks by https://easyinsight.freshbooks.com, your account name would be easyinsight."/>
    <mx:Button label="Authorize Easy Insight Access" click="install()" id="installButton"/>
    <mx:TextArea text="Clicking Authorize will open a new browser tab or window to your Freshbooks account. Upon granting access, you will receive a verification token. Copy that value, return to this tab or window, and paste that value below." borderStyle="none"
            backgroundAlpha="0" editable="false" selectable="false" width="100%" height="50"/>
    <mx:ViewStack resizeToContent="true" selectedIndex="{stackIndex}">
        <mx:Box/>
        <mx:Box>
            <mx:Label text="Verifier from Freshbooks:"/>
        <mx:TextInput id="verifierInput" fontWeight="normal" fontFamily="Lucida Grande" width="500"/>
        </mx:Box>
    </mx:ViewStack>
    <mx:StringValidator minLength="1" source="{urlInput}" property="text" id="nameValidator"/>
</mx:VBox>