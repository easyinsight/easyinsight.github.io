<?xml version="1.0" ?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" implements="com.easyinsight.customupload.IServerDataSourceCreation">
    <mx:Metadata>
		[Event(name="userConfigComplete", type="com.easyinsight.customupload.UploadConfigEvent")]
		[Event(name="userConfigCancel", type="com.easyinsight.customupload.UploadConfigEvent")]
	</mx:Metadata>
    <mx:Script><![CDATA[
        import com.easyinsight.administration.feed.FeedDefinitionData;
        import com.easyinsight.datasources.HarvestDataSource;
        import com.easyinsight.util.ProgressAlert;

        import mx.controls.Alert;
        import mx.controls.Button;
        import mx.validators.Validator;

        [Bindable]
        private var url:String;

        private var def:HarvestDataSource;

        private var installButton:Button;

        public function get dataSourceDefinition():FeedDefinitionData {
            var obj:HarvestDataSource = new HarvestDataSource();
            obj.url = urlInput.text;
            return obj;
        }

        public function set dataSourceDefinition(feedDefinition:FeedDefinitionData):void {
            var ds:HarvestDataSource = feedDefinition as HarvestDataSource;
            url = ds.url;
            this.def = ds;
        }

        public function setupButton(button:Button):Function {
            installButton = button;
            button.label = "Authorize Easy Insight Access";
            button.addEventListener(MouseEvent.CLICK, install);
            return install;
        }

        public function updateDataSource(feedDefinition:FeedDefinitionData):void {
            var ds:HarvestDataSource = feedDefinition as HarvestDataSource;
            ds.url = urlInput.text;
        }

        private function install(event:MouseEvent = null):void {
            var results:Array = Validator.validateAll([userNameValidator]);
            if (results.length > 0) {
                urlInput.setFocus();
                urlInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
            } else {
                updateDataSource(def);
                ProgressAlert.alert(this, "Retrieving access information...", null, tokenService.getOAuthURL);
                tokenService.getOAuthURL.send(93, true, null, def, def.visible ? 2 : 1);
            }
        }

        private function gotURL():void {
            var response:OAuthResponse = tokenService.getOAuthURL.lastResult as OAuthResponse;
            if (response.successful) {
                var requestToken:String = response.requestToken;
                navigateToURL(new URLRequest(requestToken), "_self");
            } else {
                Alert.show("We had a problem with authorizing your Harvest account. Please contact support@easy-insight.com.");
            }
        }

        public function validate():Boolean {
            installButton.errorString = "You need to authorize Easy Insight to access your Harvest data.";
            installButton.setFocus();
            installButton.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
            return false;
        }]]></mx:Script>
    <mx:RemoteObject destination="tokenService" id="tokenService">
        <mx:method name="getOAuthURL" result="gotURL()"/>
    </mx:RemoteObject>
    <mx:StringValidator id="userNameValidator" source="{urlInput}" property="text" minLength="1"/>
    <mx:Form>
        <mx:FormItem label="Harvest Path: " direction="horizontal" fontWeight="bold" fontFamily="Tahoma">
			<mx:TextInput id="urlInput" fontWeight="normal" fontFamily="Lucida Grande" text="{url}" width="350"/>
		</mx:FormItem>
        <mx:FormItem label="Example: &quot;easyinsight&quot; for &quot;easyinsight.harvestapp.com&quot;" fontSize="10"/>
	</mx:Form>
    <mx:TextArea text="Clicking Authorize will open a new browser tab or window to your Harvest account. Upon granting access, you will be returned to Easy Insight to finish setting up the connection." borderStyle="none"
            backgroundAlpha="0" editable="false" selectable="false" width="450" height="50"/>
</mx:VBox>