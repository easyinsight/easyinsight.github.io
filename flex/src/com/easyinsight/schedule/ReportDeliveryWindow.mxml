<?xml version="1.0"?>
<util:EITitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*"
                    xmlns:schedule="com.easyinsight.schedule.*" width="600" height="500">
    <mx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.controls.Alert;
        import mx.managers.PopUpManager;

        private var _reportID:int;

        private var _dataSourceID:int;

        [Bindable]
        private var formatValue:String;

        private var _reports:ArrayCollection;

        private var _customFilters:ArrayCollection;

        private var _deliveryInfo:DeliveryInfo;

        public function set deliveryInfo(value:DeliveryInfo):void {
            _deliveryInfo = value;
        }

        override protected function commitProperties():void {
            super.commitProperties();
            if (reportID != _deliveryInfo.id) {
                reportID = _deliveryInfo.id;
                dataSourceID = _deliveryInfo.dataSourceID;
                formatValue = String(_deliveryInfo.format);
                customFilters = _deliveryInfo.filters;
                deliveryLabel = _deliveryInfo.label;
                analysisService.getInsightDescriptors.send();
            }
        }

        private function save():void {
            _deliveryInfo.id = reportID;
            deliveryConfiguration.save(_deliveryInfo);
            filterConfiguration.save(_deliveryInfo);
            PopUpManager.removePopUp(this);
        }

        [Bindable(event="customFiltersChanged")]
        public function get customFilters():ArrayCollection {
            return _customFilters;
        }

        public function set customFilters(value:ArrayCollection):void {
            if (_customFilters == value) return;
            _customFilters = value;
            dispatchEvent(new Event("customFiltersChanged"));
        }

        [Bindable(event="reportIDChanged")]
        public function get reportID():int {
            return _reportID;
        }

        public function set reportID(value:int):void {
            if (_reportID == value) return;
            _reportID = value;
            dispatchEvent(new Event("reportIDChanged"));
        }

        [Bindable(event="dataSourceIDChanged")]
        public function get dataSourceID():int {
            return _dataSourceID;
        }

        public function set dataSourceID(value:int):void {
            if (_dataSourceID == value) return;
            _dataSourceID = value;
            dispatchEvent(new Event("dataSourceIDChanged"));
        }

        [Bindable(event="reportsChanged")]
        public function get reports():ArrayCollection {
            return _reports;
        }

        public function set reports(value:ArrayCollection):void {
            if (_reports == value) return;
            _reports = value;
            dispatchEvent(new Event("reportsChanged"));
        }

        private function gotReports():void {
            var descriptors:ArrayCollection = analysisService.getInsightDescriptors.lastResult as ArrayCollection;
            var sort:Sort = new Sort();
            sort.fields = [ new SortField("name", true) ];
            descriptors.sort = sort;
            descriptors.refresh();
            deliveryConfiguration.reports = descriptors;
        }

        [Bindable]
        private var deliveryLabel:String;
        ]]></mx:Script>
    <mx:RemoteObject destination="analysisDefinition" id="analysisService">
        <mx:method name="getInsightDescriptors" result="gotReports()"/>
    </mx:RemoteObject>
    <mx:VBox backgroundColor="#FFFFFF" width="100%" height="100%" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
        <mx:TabNavigator width="100%" height="100%" creationPolicy="all">
            <mx:VBox label="Report" paddingLeft="10" paddingTop="10" width="100%" height="100%">
                <schedule:ReportDeliveryConfiguration id="deliveryConfiguration" reportChangeable="true"
                                                      reportID="{reportID}" formatValue="{formatValue}" deliveryLabel="{deliveryLabel}"/>
            </mx:VBox>
                <schedule:FilterDeliveryConfiguration label="Filters" paddingLeft="10" paddingRight="10" id="filterConfiguration"
                                                  customFilters="{customFilters}" reportID="{reportID}" dataSourceID="{dataSourceID}"
                                                  width="100%" height="100%"/>
        </mx:TabNavigator>
        <mx:HBox width="100%" horizontalAlign="center">
            <util:SaveButton label="Save" click="save()"/>
            <util:CancelButton label="Cancel" click="PopUpManager.removePopUp(this)"/>
        </mx:HBox>
    </mx:VBox>
</util:EITitleWindow>
