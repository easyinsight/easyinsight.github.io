<?xml version="1.0" ?>
<util:EITitleWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*"
                    xmlns:schedule="com.easyinsight.schedule.*"
                    creationComplete="setup()" title="Report Email Delivery">
    <mx:Script><![CDATA[
        import com.easyinsight.administration.sharing.UserStub;
        import com.easyinsight.analysis.PromptEvent;
        import com.easyinsight.analysis.SavePromptWindow;
        import com.easyinsight.framework.User;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.binding.utils.BindingUtils;

        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.managers.PopUpManager;
        import mx.validators.Validator;

        [Bindable]
        private var reports:ArrayCollection;

        private var _activity:ReportDelivery;


        private var _reportID:int;

        [Bindable(event="reportIDChanged")]
        public function get reportID():int {
            return _reportID;
        }

        public function set reportID(value:int):void {
            if (_reportID == value) return;
            _reportID = value;
            dispatchEvent(new Event("reportIDChanged"));
        }

        private function setup():void {
            addEventListener(RecipientEvent.DELETE_RECIPIENT, deleteRecipient);
            if (_fixedReportName == null) {
                analysisService.getInsightDescriptors.send();
            }
            shareService.getUserStubs.send(false);
            BindingUtils.bindProperty(this, "reportID", deliveryConfiguration, "reportID");
            BindingUtils.bindProperty(this, "dataSourceID", deliveryConfiguration, "dataSourceID");
        }

        private function deleteRecipient(event:RecipientEvent):void {
            if (event.user != null) recipients.removeItemAt(recipients.getItemIndex(event.user));
            else recipients.removeItemAt(recipients.getItemIndex(event.email));
        }


        [Bindable(event="activityChanged")]
        public function get activity():ScheduledActivity {
            return _activity;
        }

        public function set activity(value:ScheduledActivity):void {
            if (_activity == value) return;
            _activity = value as ReportDelivery;
            dispatchEvent(new Event("activityChanged"));
        }

        protected override function commitProperties():void {
            super.commitProperties();
            if (_activity != null) {
                recipients = new ArrayCollection();
                htmlEmail = _activity.htmlEmail;
                for each (var user:UserStub in _activity.users) {
                    recipients.addItem(user);
                }
                for each (var email:String in _activity.emails) {
                    recipients.addItem(email);
                }
                deliveryLabel = _activity.deliveryLabel;
                reportID = _activity.reportID;
                body = _activity.body;
                subjectLine = _activity.subject;
                fromUserID = _activity.senderID;
                formatValue = String(_activity.reportFormat);
                dataSourceID = _activity.dataSourceID;
                customFilters = _activity.customFilters;
            } else {
                if (_fixedReportID != 0) {
                    dataSourceID = _fixedReportDataSourceID;
                    analysisService.getFilters.send(_fixedReportID);
                }
                fromUserID = User.getInstance().userID;
            }
        }

        [Bindable]
        private var customFilters:ArrayCollection;

        private var newActivity:Boolean;

        private function save():void {
            var subjectResults:Array = Validator.validateAll([subjectValidator]);
            if (subjectResults.length > 0) {
                tabNavigator.selectedIndex = 3;
                subjectInput.setFocus();
                subjectInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return;
            }
            var users:ArrayCollection = new ArrayCollection();
            var emails:ArrayCollection = new ArrayCollection();
            for each (var obj:Object in recipients) {
                if (obj is UserStub) {
                    users.addItem(obj);
                } else {
                    emails.addItem(obj);
                }
            }
            if (users.length == 0 && emails.length == 0) {
                var window:SavePromptWindow = new SavePromptWindow();
                window.prompt = "You must define at least one recipient.";
                window.defineOption("Add Recipient", PromptEvent.PROMPT_SAVE);
                window.defineOption("Cancel", PromptEvent.PROMPT_CANCEL);
                window.addEventListener(PromptEvent.PROMPT_SAVE, function (event:PromptEvent):void {
                    addRecipient();
                }, false, 0, true);
                PopUpManager.addPopUp(window, this, true);
                PopUpUtil.centerPopUp(window);
                return;
            }
            newActivity = _activity == null;
            if (activity == null) {
                activity = new ReportDelivery();
            }
            if (scheduleConfiguration.save()) {

                _activity.users = users;
                _activity.emails = emails;
                deliveryConfiguration.save(_activity);
                filterConfiguration.save(_activity);

                _activity.htmlEmail = htmlEmailCheckbox.selected;
                if (_fixedReportID > 0) {
                    _activity.reportID = _fixedReportID;
                    _activity.reportName = _fixedReportName;
                } else {
                    _activity.reportID = reportID;
                }
                _activity.senderID = fromComboBox.selectedItem.userID;
                _activity.body = bodyTextInput.text;
                _activity.subject = subjectInput.text;
                var utcOffset:int = new Date().getTimezoneOffset();
                ProgressAlert.alert(this, "Saving...", null, exportService.addOrUpdateSchedule);
                exportService.addOrUpdateSchedule.send(_activity, utcOffset);
            }
        }

        private function saved():void {
            if (newActivity) {
                dispatchEvent(new ScheduleActivityEvent(ScheduleActivityEvent.NEW_ACTIVITY, _activity));
            } else {
                dispatchEvent(new ScheduleActivityEvent(ScheduleActivityEvent.EDIT_ACTIVITY, _activity));
            }
            PopUpManager.removePopUp(this);
        }

        [Bindable]
        private var htmlEmail:Boolean = true;

        private function insertReportLink():void {
            var key:String;
            if (_fixedReportURLKey != null) {
                key = _fixedReportURLKey;
            } else {
                key = deliveryConfiguration.getUrlKey();
            }
            var url:String = "<a href=\"https://www.easy-insight.com/app/#reportID=" + key + "\">View the Report in Easy Insight</a>";
            bodyTextInput.insertTextAtCursor(url);
        }

        [Bindable]
        private var recipients:ArrayCollection = new ArrayCollection();

        private function gotReports():void {
            reports = analysisService.getInsightDescriptors.lastResult as ArrayCollection;
            var sort:Sort = new Sort();
            sort.fields = [ new SortField("name")];
            reports.sort = sort;
            reports.refresh();
        }

        private function addRecipient():void {
            var window:RecipientWindow = new RecipientWindow();
            window.addEventListener(RecipientEvent.ADD_RECIPIENT, onAddUser, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onAddUser(event:RecipientEvent):void {
            if (event.user != null) recipients.addItem(event.user);
            else recipients.addItem(event.email);
        }

        private var _fixedReportID:int;

        private var _fixedReportDataSourceID:int;


        public function set fixedReportDataSourceID(value:int):void {
            _fixedReportDataSourceID = value;
        }

        private var _fixedReportName:String;

        private var _fixedReportURLKey:String;

        protected override function createChildren():void {
            super.createChildren();
            reportChangeable = _fixedReportID == 0;
        }

        [Bindable]
        private var reportChangeable:Boolean;

        public function set fixedReportID(value:int):void {
            _fixedReportID = value;
        }

        public function set fixedReportName(value:String):void {
            _fixedReportName = value;
        }

        public function set fixedReportURLKey(value:String):void {
            _fixedReportURLKey = value;
        }

        private function gotUserStubs():void {
            var users:ArrayCollection = shareService.getUserStubs.lastResult as ArrayCollection;
            var fakeUser:UserStub = new UserStub();
            fakeUser.userID = 0;
            fakeUser.firstName = null;
            fakeUser.fullName = "reports@easy-insight.com";
            users.addItemAt(fakeUser, 0);
            this.users = users;
        }


        private var _dataSourceID:int;

        [Bindable(event="dataSourceIDChanged")]
        public function get dataSourceID():int {
            return _dataSourceID;
        }

        public function set dataSourceID(value:int):void {
            if (_dataSourceID == value) return;
            _dataSourceID = value;
            dispatchEvent(new Event("dataSourceIDChanged"));
        }

        [Bindable]
        private var filterDefinitions:ArrayCollection;

        [Bindable]
        private var users:ArrayCollection;

        [Bindable]
        private var fromUserID:int;

        [Bindable]
        private var body:String;

        [Bindable]
        private var subjectLine:String;

        [Bindable]
        private var formatValue:String;

        [Bindable]
        private var deliveryLabel:String;
        ]]></mx:Script>
    <mx:RemoteObject destination="exportService" id="exportService">
        <mx:method name="addOrUpdateSchedule" result="saved()"/>
    </mx:RemoteObject>
    <mx:RemoteObject destination="analysisDefinition" id="analysisService">
        <mx:method name="getInsightDescriptors" result="gotReports()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="shareService" destination="share">
        <mx:method name="getUserStubs" result="gotUserStubs()"/>
    </mx:RemoteObject>
    <mx:VBox paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5" backgroundColor="#FFFFFF">
        <mx:TabNavigator creationPolicy="all" width="600" height="500" id="tabNavigator">
            <mx:VBox label="Report" paddingLeft="10" paddingTop="10">
                <schedule:ReportDeliveryConfiguration id="deliveryConfiguration" reportChangeable="{reportChangeable}"
                        reports="{reports}" reportID="{reportID}" formatValue="{formatValue}" deliveryLabel="{deliveryLabel}"/>
            </mx:VBox>
            <schedule:FilterDeliveryConfiguration label="Filters" paddingLeft="10" paddingRight="10" id="filterConfiguration"
                    customFilters="{customFilters}" reportID="{reportID}" dataSourceID="{dataSourceID}"/>
            <mx:VBox label="Scheduling and Recipients" paddingLeft="10" paddingRight="10">
                <mx:Label text="When and where should the emails go?" fontSize="14" width="100%" textAlign="center"/>
                <schedule:ScheduleConfiguration activity="{activity}" id="scheduleConfiguration"/>
                <mx:Form paddingBottom="0" paddingLeft="0" paddingRight="0" paddingTop="0">
                    <mx:FormItem label="Send From:">
                        <util:SmartComboBox dataProvider="{users}" selectedValue="{fromUserID}"
                                            selectedProperty="userID" id="fromComboBox" labelField="displayName"/>
                    </mx:FormItem>
                    <mx:FormItem label="Recipients:">
                        <mx:VBox>
                            <mx:Button label="Add Recipient..." click="addRecipient()"/>
                            <mx:DataGrid dataProvider="{recipients}" rowHeight="28">
                                <mx:columns>
                                    <util:EIDataGridColumn headerText="User" dataField="displayName" width="300"
                                                           itemRenderer="com.easyinsight.schedule.RecipientRenderer"/>
                                    <util:EIDataGridColumn headerText="" dataField="displayName"
                                                           itemRenderer="com.easyinsight.schedule.RecipientControls"
                                                           width="50"
                                                           sortable="false"/>
                                </mx:columns>
                            </mx:DataGrid>
                        </mx:VBox>
                    </mx:FormItem>
                </mx:Form>
            </mx:VBox>
            <mx:VBox label="Email Setup" horizontalAlign="center">
                <mx:Label text="How do you want to set up the report email?" fontSize="14"/>
                <mx:HBox>
                    <mx:CheckBox label="HTML Email" selected="{htmlEmail}" id="htmlEmailCheckbox"/>
                    <mx:Button toolTip="Insert Report Link" click="insertReportLink()"
                               icon="@Embed(source='../../../../assets/link.png')"/>
                </mx:HBox>
                <mx:Form>
                    <mx:FormItem label="Subject Line:">
                        <mx:TextInput width="400" id="subjectInput" text="{subjectLine}"/>
                    </mx:FormItem>
                    <mx:FormItem label="Email Body:">
                        <schedule:CursorTextArea id="bodyTextInput" text="{body}" width="400" height="300"
                                                 borderStyle="inset" borderThickness="1"/>
                    </mx:FormItem>
                </mx:Form>
            </mx:VBox>
        </mx:TabNavigator>
        <mx:HBox width="100%" horizontalAlign="center">
            <util:SaveButton label="Save" click="save()"/>
            <util:CancelButton label="Cancel" click="PopUpManager.removePopUp(this)"/>
        </mx:HBox>
    </mx:VBox>
    <mx:StringValidator id="subjectValidator" source="{subjectInput}" property="text" minLength="1"/>
</util:EITitleWindow>