<?xml version="1.0" ?>
<listing:ArghButton xmlns:mx="http://www.adobe.com/2006/mxml"
                    xmlns:listing="com.easyinsight.listing.*" itemClick="onClick(event)" popUpStyleName="dropAreaPopup">
    <mx:Metadata>
        [Event(name="filterSetManage", type="flash.events.Event")]
        [Event(name="addonReportManage", type="flash.events.Event")]
    </mx:Metadata>
    <mx:Script><![CDATA[
        import com.easyinsight.SimpleReportEditor;
        import com.easyinsight.WindowManagement;
        import com.easyinsight.analysis.AnalysisChangedEvent;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.analysis.DataViewFactory;
        import com.easyinsight.analysis.GenericDefinitionEditWindow;
        import com.easyinsight.analysis.JoinOverrideEvent;
        import com.easyinsight.analysis.LimitsConfigWindow;
        import com.easyinsight.analysis.ReportJoinWindow;
        import com.easyinsight.analysis.ReportPreferencesEvent;
        import com.easyinsight.analysis.StyleConfiguration;
        import com.easyinsight.code.MarmotScriptEvent;
        import com.easyinsight.code.ReportMarmotScriptWindow;
        import com.easyinsight.util.PopUpUtil;

        import mx.collections.ArrayCollection;
        import mx.events.MenuEvent;

        import mx.managers.PopUpManager;

        private var _wrappers:ArrayCollection;
        private var _dataSourceID:int;
        private var _analysisDefinition:AnalysisDefinition;
        private var _dataView:DataViewFactory;
        private var _reportEditor:SimpleReportEditor;


        public function set reportEditor(value:SimpleReportEditor):void {
            _reportEditor = value;
        }

        private function onClick(event:MenuEvent):void {
            var target:String = event.item.data;
            var items:ArrayCollection = new ArrayCollection();
            for each (var wrapper:AnalysisItemWrapper in _wrappers) {
                items.addItem(wrapper.analysisItem);
            }
            if (target == "editProperties") {
                editReportProperties();
            } else if (target == "customizeCode") {
                customCode();
            } else if (target == "customizeJoins") {
                joinControl();
            } else if (target == "addonReports") {
                addonReports();
            } else if (target == "filterSets") {
                filterSets();
            } else if (target == "limits") {
                var limitItems:ArrayCollection = StyleConfiguration.getLimitItems(_analysisDefinition, _wrappers);
                editLimitProperties(limitItems);
            } else if (target == "caching") {
                var cacheItems:ArrayCollection = StyleConfiguration.getCachingItems(_analysisDefinition);
                editLimitProperties(cacheItems);
            } else if (target == "colorScheme") {
                var colorItems:ArrayCollection = StyleConfiguration.getColorSchemeItems(_analysisDefinition);
                editLimitProperties(colorItems);
            } else if (target == "formatting") {
                var formatItems:ArrayCollection = StyleConfiguration.getFormItems(_analysisDefinition);
                editLimitProperties(formatItems);
            } else if (target == "visuals") {
                var visualItems:ArrayCollection = StyleConfiguration.getVisualItems(_analysisDefinition);
                editLimitProperties(visualItems);
            } else if (target == "advancedConfig") {
                var advancedItems:ArrayCollection = StyleConfiguration.createExperimentalPage(_analysisDefinition);
                editLimitProperties(advancedItems);
            } else if (target == "custom") {
                var reportItems:ArrayCollection = StyleConfiguration.createReportPage(_analysisDefinition, _wrappers);
                if (reportItems.length > 0) {
                    for each (var o:Object in reportItems) {
                        if (o.label == event.item.label) {
                            editLimitProperties(o.items);
                        }
                    }
                }
            }
        }


        override protected function commitProperties():void {
            super.commitProperties();
            if (_analysisDefinition != null && _joinsCustomizable >0) {
                var os:ArrayCollection = new ArrayCollection();
                if (_joinsCustomizable == 1) {
                    os = new ArrayCollection([
                        {label: "Edit Properties      ", data: "editProperties"},
                        {label: "Customize Code", data: "customizeCode"},
                        {label: "Filter Sets", data: "filterSets"}
                    ]);
                } else {
                    os = new ArrayCollection([
                        {label: "Edit Properties      ", data: "editProperties"},
                        {label: "Customize Code", data: "customizeCode"},
                        {label: "Customize Joins", data: "customizeJoins"},
                        {label: "Addon Reports", data: "addonReports"},
                        {label: "Filter Sets", data: "filterSets"}
                    ]);
                }
                var limitItems:ArrayCollection = StyleConfiguration.getLimitItems(_analysisDefinition, new ArrayCollection());
                if (limitItems.length > 0) {
                    os.addItem({label: "Limits", data: "limits"});
                }
                var cacheItems:ArrayCollection = StyleConfiguration.getCachingItems(_analysisDefinition);
                if (cacheItems.length > 0) {
                    os.addItem({label: "Caching", data: "caching"});
                }
                var reportItems:ArrayCollection = StyleConfiguration.createReportPage(_analysisDefinition, new ArrayCollection());
                if (reportItems.length > 0) {
                    for each (var o:Object in reportItems) {
                        os.addItem({label: o.label, data: "custom"});
                    }
                }
                this.dataProvider = os;
                invalidateMenuDataProvider(os);
            }

        }

        public function set wrappers(value:ArrayCollection):void {
            _wrappers = value;
        }

        public function set dataSourceID(value:int):void {
            _dataSourceID = value;
        }

        public function set analysisDefinition(value:AnalysisDefinition):void {
            _analysisDefinition = value;
            invalidateProperties();
        }

        public function set dataView(value:DataViewFactory):void {
            _dataView = value;
        }

        public function joinControl():void {
            var window:ReportJoinWindow = new ReportJoinWindow();
            window.report = _analysisDefinition;
            window.addonReports = _analysisDefinition.addonReports;
            var items:ArrayCollection = new ArrayCollection();
            for each (var wrapper:AnalysisItemWrapper in _wrappers) {
                items.addItem(wrapper.analysisItem);
            }
            window.fields = items;
            window.dataSourceID = _dataSourceID;
            window.startJoins = _analysisDefinition.joinOverrides;
            window.addEventListener(JoinOverrideEvent.JOIN_OVERRIDE_SET, onJoinOverride, false, 0, true);
            WindowManagement.manager.addWindow(window);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onJoinOverride(event:JoinOverrideEvent):void {
            _analysisDefinition.joinOverrides = event.joins;
            _dataView.refresh();
        }

        private var _joinsCustomizable:int;


        [Bindable(event="joinsCustomizableChanged")]
        public function get joinsCustomizable():int {
            return _joinsCustomizable;
        }

        public function set joinsCustomizable(value:int):void {
            if (_joinsCustomizable == value) return;
            _joinsCustomizable = value;
            invalidateProperties();
            dispatchEvent(new Event("joinsCustomizableChanged"));
        }

        private function customCode():void {
            var window:ReportMarmotScriptWindow = new ReportMarmotScriptWindow();
            window.script = _analysisDefinition.marmotScript;
            window.afterScript = _analysisDefinition.reportRunMarmotScript;
            window.addEventListener(MarmotScriptEvent.SAVE_SCRIPT, onScript, false, 0, true);
            WindowManagement.manager.addWindow(window);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onPreferences(event:ReportPreferencesEvent):void {
            dispatchEvent(new AnalysisChangedEvent());
            _dataView.refresh();
        }

        private function editLimitProperties(items:ArrayCollection):void {
            var window:LimitsConfigWindow = new LimitsConfigWindow();
            window.items = items;
            window.addEventListener(ReportPreferencesEvent.REPORT_PREFERENCES, onPreferences, false, 0, true);
            window.definition = _analysisDefinition;
            window.allFields = _wrappers;
            WindowManagement.manager.addWindow(window);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function editReportProperties(index:int = 0):void {
            var window:GenericDefinitionEditWindow = new GenericDefinitionEditWindow();
            window.addEventListener(ReportPreferencesEvent.REPORT_PREFERENCES, onPreferences, false, 0, true);
            window.definition = _analysisDefinition;
            window.allFields = _wrappers;
            window.startIndex = index;
            WindowManagement.manager.addWindow(window);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onScript(event:MarmotScriptEvent):void {
            _analysisDefinition.marmotScript = event.script;
            _analysisDefinition.reportRunMarmotScript = event.afterScript;
            dispatchEvent(new AnalysisChangedEvent());
            _dataView.refresh();
        }

        private function filterSets():void {
            dispatchEvent(new Event("filterSetManage"));
        }

        private function addonReports():void {
            dispatchEvent(new Event("addonReportManage"));
        }
        ]]></mx:Script>
</listing:ArghButton>