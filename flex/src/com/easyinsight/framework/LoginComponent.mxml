<?xml version="1.0" ?>
<mx:Box xmlns:mx="http://www.adobe.com/2006/mxml"
        creationComplete="addEventListener(KeyboardEvent.KEY_UP, keyedSignon); focusManager.setFocus(userName)">
    <mx:Metadata>
        [Event(name="loginEvent2", type="com.easyinsight.framework.LoginEvent")]
        [Event(name="loginClose", type="flash.events.Event")]
    </mx:Metadata>
    <mx:states>
        <mx:State name="ResetPassword" enterState="enterPasswordResetStateChange()" exitState="exitPasswordResetStateChange()">
            <mx:RemoveChild target="{loginForm}"/>
            <mx:AddChild relativeTo="{coreBox}">
                <mx:Form>
                    <mx:FormItem label="Email Address:" direction="horizontal">
                        <mx:TextInput id="emailAddressInput"/>
                        <mx:Label text="{emailFailure}" visible="{emailFailure != null}"/>
                    </mx:FormItem>
                    <mx:FormItem label="">
                        <mx:Button label="Reset Password" click="resetPassword()"/>
                    </mx:FormItem>
                    <mx:FormItem label="">
                        <mx:Button label="Back to Login" click="currentState=''"/>
                        <mx:Button label="Cancel" click="cancel()" visible="{_cancelVisible}"/>
                    </mx:FormItem>
                </mx:Form>
            </mx:AddChild>
        </mx:State>
        <mx:State name="PasswordReset">
            <mx:AddChild relativeTo="{activationBox}">
                <mx:Label text="An email has been sent to your email address to allow you to reset your password."/>
            </mx:AddChild>
        </mx:State>
        <mx:State name="AdditionalInfo">
            <mx:AddChild relativeTo="{activationBox}">
                <mx:Label text="{_additionalInfo}"/>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script>
		<![CDATA[

        import mx.messaging.config.ServerConfig;
        import mx.rpc.AsyncResponder;
        import mx.rpc.AsyncToken;
        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;
        import mx.validators.Validator;

        private function passwordResetEnter(event:KeyboardEvent):void {
            if (event.keyCode == Keyboard.ENTER) {
                resetPassword();
            }
        }

        private function exitPasswordResetStateChange():void {
            this.removeEventListener(KeyboardEvent.KEY_UP, passwordResetEnter);
            this.addEventListener(KeyboardEvent.KEY_UP, keyedSignon);
        }

        private function enterPasswordResetStateChange():void {
            this.removeEventListener(KeyboardEvent.KEY_UP, keyedSignon);
            this.addEventListener(KeyboardEvent.KEY_UP, passwordResetEnter);
        }

        [Bindable]
        private var _additionalInfo:String;

        [Bindable]
        private var _cancelVisible:Boolean;

        [Bindable]
        private var resetResult:String;

        [Bindable]
        private var emailFailure:String;

        public function set additionalInfo(value:String):void {
            _additionalInfo = value;
        }

        private function delinquentAccount():void {
            var delinquent:Boolean = authService.isAccountDelinquentOrClosed.lastResult as Boolean;
            if (delinquent)
                navigateToURL(new URLRequest("billing/login.jsp"), "_self");
        }

        public function setupFocus():void {
            focusManager.setFocus(userName);
        }

        private function toBaseState():void {
            if (_additionalInfo == null) {
                currentState = "";
            } else {
                currentState = "AdditionalInfo";
            }
        }

        public function set cancelVisible(value:Boolean):void {
            _cancelVisible = value;
        }

        private function resetPassword():void {
            var results:Array = Validator.validateAll([emailValidator]);
            if (results.length > 0) {
                emailAddressInput.setFocus();
                emailAddressInput.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return;
            }
            stackIndex = 1;
            authService.remindPassword.send(emailAddressInput.text);
        }

        private function reset():void {
            stackIndex = 0;
            var result:Boolean = authService.remindPassword.lastResult as Boolean;
            if (result) {
                currentState = "PasswordReset";
            } else {
                emailFailure = "No such email address was found in Easy Insight.";
            }
        }

        private function handleAuthentication():void {
            var authResult:UserServiceResponse = authService.authenticate.lastResult as UserServiceResponse;
            var successful:Boolean = authResult.successful;
            if (successful) {
                if (authService.channelSet == null) {
                    authService.channelSet = ServerConfig.getChannelSet(authService.destination);
                }
                var token:AsyncToken = authService.channelSet.login(this.userNameStr, this.passwordStr);
                token.addResponder(new AsyncResponder(
                        function (event:ResultEvent, token:Object = null):void {
                            switch (event.result) {
                                case "success":
                                    /*User.initializeUser(authResult);
                                    User.getInstance().userName = authResult.userName;*/
                                    dispatchEvent(new LoginEvent(LoginEvent.LOGIN, authResult));
                                    //User.getEventNotifier().dispatchEvent(new LoginEvent(LoginEvent.LOGIN, _targetURL, _newAccount));
                                    break;
                                default:
                                    trace(event.result);
                            }
                        },
                        function (event:FaultEvent, token:Object = null):void {
                            switch (event.fault.faultCode) {
                                case "Client.Authentication":
                                default:
                                    failureMessage = event.fault.faultString;
                            }
                        }
                        ));

            } else {
                stackIndex = 0;
                failureMessage = authResult.failureMessage;
            }
        }

        private var userNameStr:String;
        private var passwordStr:String;

        public function signon(userNameStr:String, passwordStr:String):void {
            stackIndex = 1;
            this.userNameStr = userNameStr;
            this.passwordStr = passwordStr;
            authService.authenticate.send(this.userNameStr, this.passwordStr);
        }

        private function cancel():void {
            dispatchEvent(new Event("loginClose"));
        }

        private function keyedSignon(event:KeyboardEvent):void {
            if (event.keyCode == Keyboard.ENTER) {
                defaultSignon();
            }
        }

        private function defaultSignon():void {
            var results:Array = Validator.validateAll([userNameValidator]);
            if (results.length > 0) {
                userName.setFocus();
                userName.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return;
            }
            var passwordResults:Array = Validator.validateAll([passwordValidator]);
            if (passwordResults.length > 0) {
                password.setFocus();
                password.dispatchEvent(new MouseEvent(MouseEvent.MOUSE_OVER));
                return;
            }
            signon(userName.text, password.text);
        }

        [Bindable]
        private var failureMessage:String = "";

        [Bindable]
        private var stackIndex:int;
                ]]>
	</mx:Script>
    <mx:VBox id="coreBox" paddingTop="10" paddingBottom="10" paddingLeft="10" paddingRight="10">

        <mx:Box id="activationBox" width="100%" horizontalAlign="center" paddingTop="10"/>
        <mx:VBox id="loginForm" paddingLeft="0" paddingRight="0" paddingTop="0" paddingBottom="0">
        <mx:ViewStack selectedIndex="{stackIndex}">
            <mx:Form paddingBottom="0" paddingLeft="0" paddingRight="0" paddingTop="0">
                <mx:FormItem label="User Name or Email:" direction="horizontal">
                    <mx:TextInput id="userName" tabIndex="1"/>
                </mx:FormItem>
                <mx:FormItem label="Password:" direction="horizontal">
                    <mx:TextInput id="password" displayAsPassword="true" tabIndex="2"/>
                    <mx:LinkButton fontSize="10" textDecoration="underline" label="Forgot your Password?"
                                   click="currentState='ResetPassword'"/>
                </mx:FormItem>
            </mx:Form>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle">
                <mx:ProgressBar label="Connecting..." indeterminate="true"/>
            </mx:Box>
        </mx:ViewStack>
        <mx:Spacer height="10"/>
        <mx:HBox width="100%" horizontalAlign="center">
            <mx:Button label="Sign In" click="defaultSignon()" tabIndex="3"/>
        </mx:HBox>
        <mx:Spacer height="10"/>
        </mx:VBox>
        <mx:Spacer height="10"/>
        <mx:HBox width="100%" horizontalAlign="center">
            <mx:Text text="{failureMessage}" fontSize="14"/>
        </mx:HBox>
    </mx:VBox>
    <mx:StringValidator id="userNameValidator" minLength="3" source="{userName}" property="text"/>
    <mx:StringValidator id="passwordValidator" minLength="3" source="{password}" property="text"/>
    <mx:EmailValidator id="emailValidator" source="{emailAddressInput}" property="text"/>
    <mx:RemoteObject id="authService" destination="login">
        <mx:method name="authenticate" result="handleAuthentication()" fault="failureMessage = event.fault.faultString"/>
        <mx:method name="remindPassword" result="reset()"  fault="emailFailure = event.fault.faultString"/>
    </mx:RemoteObject>
</mx:Box>