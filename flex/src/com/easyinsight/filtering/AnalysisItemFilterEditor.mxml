<?xml version="1.0"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*"
         creationComplete="setup()" implements="com.easyinsight.filtering.IFilterDetailEditor">
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemHandle;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.util.PopUpUtil;

        import mx.collections.ArrayCollection;
        import mx.managers.PopUpManager;

        [Bindable]
        private var filterFields:ArrayCollection;

        private function setup():void {
        }

        private var analysisItemFilter:AnalysisItemFilterDefinition;

        public function makeUpdates():FilterDefinition {
            var handles:ArrayCollection = new ArrayCollection();
            for each (var v:AnalysisItem in filterFields) {
                var handle:AnalysisItemHandle = new AnalysisItemHandle();
                handle.analysisItemID = v.analysisItemID;
                handle.name = v.display;
                handles.addItem(handle);
            }
            analysisItemFilter.availableHandles = handles;
            analysisItemFilter.availableTags = selectedTags;
            analysisItemFilter.useFullyQualifiedNames = useFullyQualifiedNamesCheckbox.selected;
            analysisItemFilter.expandDates = expandDatesBox.selectedItem.data;
            return analysisItemFilter;
        }

        public function set filterDefinition(filterDefinition:FilterDefinition):void {
            analysisItemFilter = filterDefinition as AnalysisItemFilterDefinition;

            useFullyQualifiedNames = analysisItemFilter.useFullyQualifiedNames;

            var selectedItems:ArrayCollection;
            if (analysisItemFilter.availableHandles != null && analysisItemFilter.availableHandles.length > 0) {
                selectedItems = new ArrayCollection();
                var byAnalysisItemName:Object = new Object();
                for each (var available:AnalysisItem in allFields) {
                    byAnalysisItemName[available.display] = available;
                }

                for each (var handle:AnalysisItemHandle in analysisItemFilter.availableHandles) {
                    var item:AnalysisItem = byAnalysisItemName[handle.name];
                    if (item != null) {
                        selectedItems.addItem(item);
                    }
                }

            } else if (analysisItemFilter.availableItems != null) {
                selectedItems = analysisItemFilter.availableItems;
            } else {
                selectedItems = new ArrayCollection();
            }

            filterFields = selectedItems;
            selectedTags = analysisItemFilter.availableTags;
            selectedExpansion = analysisItemFilter.expandDates;
        }

        public function set feedID(feedID:int):void {
        }

        private var allFields:ArrayCollection;

        public function set fields(value:ArrayCollection):void {
            allFields = new ArrayCollection();
            for each (var wrapper:AnalysisItemWrapper in value) {
                allFields.addItem(wrapper.analysisItem);
            }
        }

        private function configureExplicitFields():void {
            var window:MultiFieldPickWindow = new MultiFieldPickWindow();
            window.selectedItems = filterFields;
            window.analysisItems = allFields;
            window.addEventListener(MultiFieldEvent.MULTI_FIELD_UPDATE, onMultiFieldUpdate, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onMultiFieldUpdate(event:MultiFieldEvent):void {
            filterFields = event.fields;
        }

        private function configureTags():void {
            var window:MultiTagPickWindow = new MultiTagPickWindow();
            window.selectedTags = selectedTags;
            window.addEventListener(MultiFieldEvent.MULTI_FIELD_UPDATE, onMultiTagUpdate, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        [Bindable]
        private var useFullyQualifiedNames:Boolean;

        private var selectedTags:ArrayCollection;

        private function onMultiTagUpdate(event:MultiFieldEvent):void {
            selectedTags = event.fields;
        }

        [Bindable]
        private var selectedExpansion:int;
        ]]></mx:Script>
    <mx:HBox>
        <mx:Button label="Configure Fields..." click="configureExplicitFields()" styleName="grayButton"/>
        <mx:Button label="Configure Tags..." click="configureTags()" styleName="grayButton"/>
    </mx:HBox>
    <mx:CheckBox label="Use Fully Qualified Names" id="useFullyQualifiedNamesCheckbox" selected="{useFullyQualifiedNames}"/>
    <util:SmartComboBox id="expandDatesBox" selectedProperty="data" selectedValue="{selectedExpansion}" labelField="label">
        <mx:ArrayCollection>
            <mx:Array>
                <mx:Object label="None" data="0"/>
                <mx:Object label="Add Year, Quarter, Month, Week, and Day as Options" data="1"/>
            </mx:Array>
        </mx:ArrayCollection>
    </util:SmartComboBox>
</mx:VBox>
