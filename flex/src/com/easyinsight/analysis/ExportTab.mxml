<?xml version="1.0" ?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" label="Export" icon="@Embed(source='../../../../assets/tables_add.png')" height="43">
    <mx:Script>
    <![CDATA[
        import com.easyinsight.framework.LoginEvent;
        import com.easyinsight.framework.User;
        import com.easyinsight.report.URLWindow;
        import com.easyinsight.schedule.ReportDelivery;
        import com.easyinsight.schedule.ReportDeliveryScheduleWindow;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.controls.Alert;
        import mx.managers.PopUpManager;

        private function onLogin(event:LoginEvent):void {
            User.getEventNotifier().removeEventListener(LoginEvent.LOGIN, onLogin);
        }
        private var _analysisDefinition:AnalysisDefinition;

        [Bindable]
        private var _dataView:DataViewFactory;

        [Bindable]
        private var _exportVisible:Boolean;


        public function set exportVisible(value:Boolean):void {
            _exportVisible = value;
        }

        public function set dataView(value:DataViewFactory):void {
            _dataView = value;
        }

        [Bindable(event="analysisDefinitionChanged")]
        public function get analysisDefinition():AnalysisDefinition {
            return _analysisDefinition;
        }

        public function set analysisDefinition(value:AnalysisDefinition):void {
            if (_analysisDefinition == value) return;
            _analysisDefinition = value;
            dispatchEvent(new Event("analysisDefinitionChanged"));
        }

        private function embed():void {
            if (analysisDefinition.analysisID > 0) {
                var embedWindow:EmbedReportWindow = new EmbedReportWindow();
                embedWindow.urlKey = analysisDefinition.urlKey;
                embedWindow.reportType = analysisDefinition.reportType;
                embedWindow.dataSourceID = analysisDefinition.dataFeedID;
                embedWindow.reportName = analysisDefinition.name;
                PopUpManager.addPopUp(embedWindow, this, true);
                PopUpUtil.centerPopUp(embedWindow);
            } else {
                Alert.show("You need to save the report first.");
            }
        }

        private function exportPNG():void {
            new PNGCreator().exportPNG(_dataView.getCoreView(), this, analysisDefinition.name);
        }

        private function exportExcel():void {
            _dataView.updateExportMetadata();
            new ExcelCreator().exportExcel(analysisDefinition, this);
        }

        private function exportPDF():void {
            _dataView.updateExportMetadata();
            new PDFCreator().exportReportToPDF(analysisDefinition, this, _dataView.getCoreView());
        }

        private function netvibes():void {
            navigateToURL(new URLRequest("http://www.netvibes.com/subscribe.php?module=UWA&moduleUrl=" + escape("https://www.easy-insight.com/app/widgets/UWAWidget.html?insightCombo=" + escape(String(analysisDefinition.analysisID)) + "&reportName=" + escape(analysisDefinition.name) + "&reportType=" + escape(String(analysisDefinition.type)) + "&dataSourceID=" + escape(String(analysisDefinition.dataFeedID)))));
        }

        private function igoogle():void {
            navigateToURL(new URLRequest("http://www.google.com/ig/add?moduleurl=" + escape("http://www.netvibes.com/api/uwa/compile/google.php?moduleUrl=" + escape("https://www.easy-insight.com/app/widgets/UWAWidget.html?insightCombo=" + escape(String(analysisDefinition.analysisID)) + "&reportName=" + escape(analysisDefinition.name) + "&reportType=" + escape(String(analysisDefinition.type)) + "&dataSourceID=" + escape(String(analysisDefinition.dataFeedID))))));
        }

        private function url():void {
            //var escapedName:String = _reportName.replace(/[ @\"&*#$%^~]/g, "-");
            var url:String = "https://www.easy-insight.com/app/#reportID=" + analysisDefinition.urlKey;
            var urlWindow:URLWindow = new URLWindow();
            urlWindow.url = url;
            PopUpManager.addPopUp(urlWindow, this, true);
            PopUpUtil.centerPopUp(urlWindow);
        }

        public function removeEmbed():void {
            removeChild(embedButton);
        }

        public function removePNG():void {
            removeChild(imageButton);
        }

        public function removeEmail():void {
            removeChild(emailButton);
        }

        private function email():void {
            _dataView.updateExportMetadata();
            var window:EmailReportWindow = new EmailReportWindow();
            window.coreView = _dataView.getCoreView();
            window.report = _analysisDefinition;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function schedule():void {
            if (analysisDefinition.analysisID == 0) {
                Alert.show("You need to save the report first.");
            } else {
                ProgressAlert.alert(this, "Getting current email delivery info...", null, exportService.getReportDelivery);
                exportService.getReportDelivery.send(analysisDefinition.analysisID, new Date().getTimezoneOffset());
            }
        }

        private function gotReportDelivery():void {
            var delivery:ReportDelivery = exportService.getReportDelivery.lastResult as ReportDelivery;
            var window:ReportDeliveryScheduleWindow = new ReportDeliveryScheduleWindow();
            window.fixedReportID = analysisDefinition.analysisID;
            window.fixedReportName = analysisDefinition.name;
            window.fixedReportURLKey = analysisDefinition.urlKey;
            window.fixedReportDataSourceID = analysisDefinition.dataFeedID;
            window.activity = delivery;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }
        ]]></mx:Script>
    <mx:RemoteObject id="exportService" destination="exportService">
        <mx:method name="getReportDelivery" result="gotReportDelivery()"/>
    </mx:RemoteObject>
    <mx:Button id="excelButton" toolTip="Export to Excel"
               icon="@Embed(source='../../../../assets/table.png')" click="exportExcel()"
            labelPlacement="right" label="Export to Excel" x="10" y="10"/>
    <mx:Button id="pdfButton" toolTip="Export to PDF"
               icon="@Embed(source='../../../../assets/document_chart.png')" click="exportPDF()"
            labelPlacement="right" label="Export to PDF" x="143" y="10"/>
    <mx:Button id="emailButton" toolTip="Email Report" click="email()"
               icon="@Embed(source='../../../../assets/mail.png')" labelPlacement="right" label="Email Report" x="268" y="10"/>
    <mx:Button id="scheduleButton" toolTip="Schedule Delivery" click="schedule()"
               icon="@Embed(source='../../../../assets/calendar.png')" labelPlacement="right" label="Email Scheduling" x="388" y="10"/>
    <mx:Button id="imageButton" toolTip="Export PNG Image"
               icon="@Embed(source='../../../../assets/camera.png')" click="exportPNG()" labelPlacement="right" label="Export to PNG" x="531" y="10"/>
    <mx:Button id="linkButton" toolTip="Link by URL" click="url()"
               icon="@Embed(source='../../../../assets/link.png')" labelPlacement="right" label="Hyperlink" x="657" y="10"/>
    <mx:Button id="embedButton" toolTip="Embed" click="embed()"
               icon="@Embed(source='../../../../assets/plasma_tv.png')" labelPlacement="right" label="Embed in Website" x="759" y="10"/>
    <!--<mx:Button id="createFeedButton" toolTip="Create Derived Data Source" click="createFeed()"
               icon="@Embed(source='../../../../assets/data_add.png')"/>-->
</mx:Canvas>