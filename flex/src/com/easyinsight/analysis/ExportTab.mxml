<?xml version="1.0" ?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml" label="Export" icon="@Embed(source='../../../../assets/tables_add.png')" paddingLeft="10"
             paddingTop="10" paddingBottom="10" paddingRight="10" creationComplete="onCreation()">
    <mx:states>
        <mx:State name="NotLoggedIn">            
            <mx:RemoveChild target="{createFeedButton}"/>
        </mx:State>
        <mx:State name="Professional">

        </mx:State>
        <mx:State name="Group" basedOn="Professional">

        </mx:State>
        <mx:State name="Individual" basedOn="Group">
            <mx:RemoveChild target="{createFeedButton}"/>
        </mx:State>
        <mx:State name="Free" basedOn="Individual">

        </mx:State>
    </mx:states>
    <mx:Script>
    <![CDATA[
        import com.easyinsight.account.Account;
        import com.easyinsight.analysis.feed.AnalysisBasedFeedCreationWindow;
        import com.easyinsight.framework.LoginEvent;
        import com.easyinsight.framework.User;
        import com.easyinsight.report.URLWindow;
        import com.easyinsight.schedule.ReportDelivery;
        import com.easyinsight.schedule.ReportDeliveryScheduleWindow;
        import com.easyinsight.util.PopUpUtil;

        import mx.controls.Alert;
        import mx.managers.PopUpManager;

        private function onLogin(event:LoginEvent):void {
            setState();
            User.getEventNotifier().removeEventListener(LoginEvent.LOGIN, onLogin);
        }

        private function setState():void {
            switch (User.getInstance().getAccountType()) {
                case Account.PREMIUM:
                    currentState = "Professional";
                    break;
                case Account.PRO:
                    currentState = "Group";
                    break;
                case Account.BASIC:
                case Account.PLUS:
                    currentState = "Individual";
                    break;
                case Account.PERSONAL:
                    currentState = "Free";
                    break;
            }
        }

        private function onCreation():void {
            if (User.getInstance() == null) {
                currentState = "NotLoggedIn";
                User.getEventNotifier().addEventListener(LoginEvent.LOGIN, onLogin);
            } else {
                setState();
            }
        }

        private var _analysisDefinition:AnalysisDefinition;

        [Bindable]
        private var _dataView:DataViewFactory;

        [Bindable]
        private var _exportVisible:Boolean;


        public function set exportVisible(value:Boolean):void {
            _exportVisible = value;
        }

        public function set dataView(value:DataViewFactory):void {
            _dataView = value;
        }

        [Bindable(event="analysisDefinitionChanged")]
        public function get analysisDefinition():AnalysisDefinition {
            return _analysisDefinition;
        }

        public function set analysisDefinition(value:AnalysisDefinition):void {
            if (_analysisDefinition == value) return;
            _analysisDefinition = value;
            dispatchEvent(new Event("analysisDefinitionChanged"));
        }

        private function createFeed():void {
            if (analysisDefinition.analysisID == 0) {
                Alert.show("You need to save the report first.");
            } else {
                var feedCreationWindow:AnalysisBasedFeedCreationWindow = AnalysisBasedFeedCreationWindow(PopUpManager.
                        createPopUp(this, AnalysisBasedFeedCreationWindow, true));
                feedCreationWindow.analysisDefinition = analysisDefinition;
                PopUpUtil.centerPopUp(feedCreationWindow);
            }
        }

        private function embed():void {
            if (analysisDefinition.analysisID > 0) {
                var embedWindow:EmbedReportWindow = new EmbedReportWindow();
                embedWindow.urlKey = analysisDefinition.urlKey;
                embedWindow.reportType = analysisDefinition.reportType;
                embedWindow.dataSourceID = analysisDefinition.dataFeedID;
                embedWindow.reportName = analysisDefinition.name;
                PopUpManager.addPopUp(embedWindow, this, true);
                PopUpUtil.centerPopUp(embedWindow);
            } else {
                Alert.show("You need to save the report first.");
            }
        }

        private function exportPNG():void {
            new PNGCreator().draw(_dataView.getCoreView());
        }

        private function exportExcel():void {
            _dataView.updateExportMetadata();
            new ExcelCreator().exportExcel(analysisDefinition, this);
        }

        /*private function exportPDF():void {
         _dataView.updateExportMetadata();
         new PDFCreator().createPDF(_dataView.getCoreView());
         }*/

        private function netvibes():void {
            flash.net.navigateToURL(new URLRequest("http://www.netvibes.com/subscribe.php?module=UWA&moduleUrl=" + escape("https://www.easy-insight.com/app/widgets/UWAWidget.html?insightCombo=" + escape(String(analysisDefinition.analysisID)) + "&reportName=" + escape(analysisDefinition.name) + "&reportType=" + escape(String(analysisDefinition.type)) + "&dataSourceID=" + escape(String(analysisDefinition.dataFeedID)))));
        }

        private function igoogle():void {
            flash.net.navigateToURL(new URLRequest("http://www.google.com/ig/add?moduleurl=" + escape("http://www.netvibes.com/api/uwa/compile/google.php?moduleUrl=" + escape("https://www.easy-insight.com/app/widgets/UWAWidget.html?insightCombo=" + escape(String(analysisDefinition.analysisID)) + "&reportName=" + escape(analysisDefinition.name) + "&reportType=" + escape(String(analysisDefinition.type)) + "&dataSourceID=" + escape(String(analysisDefinition.dataFeedID))))));
        }

        private function url():void {
            //var escapedName:String = _reportName.replace(/[ @\"&*#$%^~]/g, "-");
            var url:String = "https://www.easy-insight.com/app/#reportID=" + analysisDefinition.urlKey;
            var urlWindow:URLWindow = new URLWindow();
            urlWindow.url = url;
            PopUpManager.addPopUp(urlWindow, this, true);
            PopUpUtil.centerPopUp(urlWindow);
        }

        public function removeDLS():void {
            removeChild(createFeedButton);
        }

        public function removeEmbed():void {
            removeChild(embedButton);
        }

        public function removePNG():void {
            removeChild(imageButton);
        }

        public function removeNetvibes():void {
            removeChild(netvibesButton);
        }

        public function removeIGoogle():void {
            removeChild(igoogleButton);
        }

        private function email():void {
            exportService.getReportDelivery.send(analysisDefinition.analysisID, new Date().getTimezoneOffset());
        }

        private function gotReportDelivery():void {
            var delivery:ReportDelivery = exportService.getReportDelivery.lastResult as ReportDelivery;
            var window:ReportDeliveryScheduleWindow = new ReportDeliveryScheduleWindow();
            window.fixedReportID = analysisDefinition.analysisID;
            window.fixedReportName = analysisDefinition.name;
            window.fixedReportURLKey = analysisDefinition.urlKey;
            window.activity = delivery;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }
        ]]></mx:Script>
    <mx:RemoteObject id="exportService" destination="exportService">
        <mx:method name="getReportDelivery" result="gotReportDelivery()"/>
    </mx:RemoteObject>
    <mx:Button id="linkButton" toolTip="Link by URL" click="url()"
               icon="@Embed(source='../../../../assets/link.png')"/>
    <mx:Button id="embedButton" toolTip="Embed" click="embed()"
               icon="@Embed(source='../../../../assets/plasma_tv.png')"/>
    <mx:Button id="emailButton" toolTip="Email Delivery" click="email()"
               icon="@Embed(source='../../../../assets/mail.png')"/>
    <mx:Button id="imageButton" toolTip="Export PNG Image"
               icon="@Embed(source='../../../../assets/camera.png')" click="exportPNG()"/>
    <mx:Button id="excelButton" toolTip="Export to Excel"
               icon="@Embed(source='../../../../assets/table.png')" click="exportExcel()"/>
    <mx:Button id="createFeedButton" toolTip="Create Derived Data Source" click="createFeed()"
               icon="@Embed(source='../../../../assets/data_add.png')"/>
    <!--<mx:Button id="pdfButton" toolTip="Export to PDF"
               icon="@Embed(source='../../../../assets/table.png')" click="exportPDF()"/>-->
    <mx:Button id="netvibesButton" toolTip="Add to netvibes"
               icon="@Embed(source='../../../../assets/uwa-netvibes.png')" click="netvibes()"/>
    <mx:Button id="igoogleButton" toolTip="Add to iGoogle"
               icon="@Embed(source='../../../../assets/uwa-google.png')" click="igoogle()"/>
</mx:HBox>