<?xml version="1.0"?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer" verticalScrollPolicy="off" width="100%"
        height="100%">
    <mx:states>
        <mx:State name="noData">
            <mx:RemoveChild target="{dataGrid}"/>
            <mx:AddChild relativeTo="{box}">
                <mx:TextArea fontSize="22" text="We didn't find any data for the fields and filters that you specified in the report."
                        borderStyle="none" backgroundAlpha="0" width="500" height="65" editable="false" selectable="false" focusEnabled="false"/>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisMeasure;
        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.Value;

        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.formatters.Formatter;

        [Bindable]
        private var dataColl:ArrayCollection;

        override protected function measure():void {
            super.measure();
            box.width = this.width;
            box.height = this.height;
        }

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            if (dataSet.length == 0) {
                currentState = "noData";
                return;
            }
            var vert:CombinedVerticalListDefinition = analysisDefinition as CombinedVerticalListDefinition;
            var dColl:ArrayCollection = new ArrayCollection();
            var columns:ArrayCollection = new ArrayCollection();
            var labelColumn:SortableAdvancedDataGridColumn = new SortableAdvancedDataGridColumn(0, 0);
            labelColumn.sortable = false;
            labelColumn.itemRenderer = new ClassFactory(VerticalListRowHeaderRenderer);
            var labelHeaderFactory:ClassFactory = new ClassFactory(VerticalListHeaderRenderer);
            labelHeaderFactory.properties = { headerText: "" };
            labelColumn.headerRenderer = labelHeaderFactory;
            labelColumn.width = vert.headerWidth;
            labelColumn.dataField = "Label";
            columns.addItem(labelColumn);
            var firstRow:Boolean = true;
            if (vert == null) {
                return;
            }
            var measures:ArrayCollection = VerticalListDefinition(vert.reports.getItemAt(0)).measures;
            for each (var measure:AnalysisMeasure in measures) {

                var obj:Object = new Object();
                obj["Label"] = measure.display;
                obj["baseMeasure"] = measure;

                for (var i:int = 0; i < vert.reports.length; i++) {
                    var report:VerticalListDefinition = vert.reports.getItemAt(i) as VerticalListDefinition;
                    var applyMeasure:AnalysisMeasure;
                    for each (var testMeasure:AnalysisMeasure in report.measures) {
                        if (testMeasure.display == measure.display) {
                            applyMeasure = testMeasure;
                            break;
                        }
                    }
                    var formatter:Formatter = applyMeasure.getFormatter();
                    var reportData:ArrayCollection = dataSet.getItemAt(i) as ArrayCollection;
                    for each (var row:Object in reportData) {
                        var columnValue:String;
                        var val:Value;
                        if (report.column == null) {
                            columnValue = report.name;
                        } else {
                            val = row[report.column.qualifiedName()];
                            columnValue = val.toString();
                        }
                        if (val != null && val.type() == Value.EMPTY) {
                            continue;
                        }
                        var measureValue:Value = row[applyMeasure.qualifiedName()];
                        var text:String;
                        if (measureValue != null) {
                            var num:Number = measureValue.toNumber();
                            text = formatter.format(num);
                        } else {
                            text = "";
                        }
                        obj[columnValue] = text;
                        obj[columnValue + "measure"] = applyMeasure;
                        if (firstRow) {
                            var dataColumn:SortableAdvancedDataGridColumn = new SortableAdvancedDataGridColumn(i + 1, val == null ? 0 : val.toSortValue().toNumber());
                            dataColumn.width = vert.columnWidth;
                            dataColumn.headerText = columnValue.toString();
                            dataColumn.dataField = columnValue.toString();
                            var factory:ClassFactory = new ClassFactory(VerticalListRenderer);
                            factory.properties = { qualifiedName: columnValue.toString() };
                            dataColumn.itemRenderer = factory;
                            dataColumn.sortable = false;
                            var headerFactory:ClassFactory = new ClassFactory(VerticalListHeaderRenderer);
                            headerFactory.properties = { headerText: columnValue.toString() };
                            dataColumn.headerRenderer = headerFactory;
                            columns.addItem(dataColumn);
                        }
                    }
                }

                if (firstRow) {
                    firstRow = false;
                }
                dColl.addItem(obj);
            }
            if (columns.length == 1) {
                currentState = "noData";
            } else {
                currentState = "";
            }
            var sort:Sort = new Sort();
            sort.fields = [ new SortField("reportIndex"), new SortField("sortValue") ];
            columns.sort = sort;
            columns.refresh();
            dataGrid.columns = columns.toArray();
            dataGrid.rowCount = dColl.length + 1;
            dataGrid.dataProvider = dColl;
            invalidateSize();
        }

        public function updateExportMetadata():void {
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }

        public function preserveValues():Boolean {
            return true;
        }
        ]]></mx:Script>
    <mx:Box horizontalAlign="center" id="box" verticalScrollPolicy="auto">
        <mx:AdvancedDataGrid id="dataGrid" headerColors="{[0x333333, 0x333333]}" fontSize="10" verticalScrollPolicy="off" variableRowHeight="true" horizontalGridLines="false" horizontalGridLineColor="#DDDDDD" verticalGridLines="false"
                alternatingItemColors="{[0xFFFFFF, 0xFFFFFF]}" backgroundColor="#FFFFFF" borderStyle="none" selectionMode="none">
        </mx:AdvancedDataGrid>
    </mx:Box>
</mx:Module>
