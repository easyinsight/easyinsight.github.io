<?xml version="1.0" ?>
<charts:CartesianChartModule xmlns:mx="http://www.adobe.com/2006/mxml"
                             implements="com.easyinsight.analysis.IReportRenderer"
                             width="100%" height="100%" xmlns:charts="com.easyinsight.analysis.charts.*">

    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisItem;

        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.ChartDefinition;

        import com.easyinsight.analysis.SortFunctionFactory;
        import com.easyinsight.analysis.charts.xaxisbased.column.BetterBoxBarRenderer;

        import mx.charts.AxisRenderer;

        import mx.charts.BarChart;
        import mx.charts.CategoryAxis;
        import mx.charts.ChartItem;
        import mx.charts.HitData;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.CartesianChart;
        import mx.charts.chartClasses.CartesianTransform;
        import mx.charts.chartClasses.ChartBase;
        import mx.charts.chartClasses.DataTransform;
        import mx.charts.chartClasses.IAxisRenderer;
        import mx.charts.chartClasses.NumericAxis;
        import mx.charts.chartClasses.Series;
        import mx.charts.series.BarSeries;
        import mx.charts.series.items.BarSeriesItem;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.core.ClassFactory;

        override protected function getDimensionItem():AnalysisItem {
            return BarChartDefinition(chartDef).yaxis;
        }

        override protected function getMeasures():ArrayCollection {
            return BarChartDefinition(chartDef).measures;
        }

        override protected function createChartObject():ChartBase {
            return new BarChart();
        }

        override protected function useGradientColor():Boolean {
            return BarChartDefinition(chartDef).gradientColor > 0;
        }

        override protected function getGradientColor():uint {
            return BarChartDefinition(chartDef).gradientColor;
        }

        override protected function useChartColor():Boolean {
            return BarChartDefinition(chartDef).useChartColor;
        }

        override protected function getChartColor():uint {
            return BarChartDefinition(chartDef).chartColor;
        }

        override protected function getColorScheme():String {
            return BarChartDefinition(chartDef).colorScheme;
        }

        override protected function sortData(dataSet:ArrayCollection):void {
            var columnChartDef:BarChartDefinition = chartDef as BarChartDefinition;
            var firstMeasure:AnalysisMeasure = columnChartDef.measures.getItemAt(0) as AnalysisMeasure;
            if (columnChartDef.columnSort != ChartDefinition.SORT_UNSORTED) {
                var sort:Sort = new Sort();
                if (columnChartDef.columnSort == ChartDefinition.SORT_X_ASCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(getDimensionItem(), false);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_X_DESCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(getDimensionItem(), true);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_Y_ASCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(firstMeasure, false);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_Y_DESCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(firstMeasure, true);
                }
                dataSet.sort = sort;
                dataSet.refresh();
            }
        }


        override protected function getAngle():int {
            return 90;
        }

        override protected function formatDataTip(hd:HitData):String {
            var dt:String = "";
            var barSeriesItem:BarSeriesItem = hd.chartItem as BarSeriesItem;
            var series:Series = barSeriesItem.element as Series;
            var dataTransform:DataTransform = series.dataTransform;
            var n:String = series.displayName;
            if (n != null && n.length > 0)
                dt += "<b>" + n + "</b><BR/>";

            var xName:String = dataTransform.getAxis(CartesianTransform.VERTICAL_AXIS).displayName;
            if (xName != "")
                dt += "<i>" + xName + ":</i> ";
            dt += dimensionFormatter.format(BarSeriesItem(hd.chartItem).xValue) + "\n";

            var yName:String = dataTransform.getAxis(CartesianTransform.HORIZONTAL_AXIS).displayName;

            if (yName != "")
                dt += "<i>" + yName + ":</i> ";
            dt += measureFormatter.format(BarSeriesItem(hd.chartItem).xValue) + "\n";

            return dt;
        }

        override protected function direction():String {
            return "vertical";
        }

        override protected function assignDimensionAxis(axis:CategoryAxis, chart:ChartBase, axisRenderer:IAxisRenderer):void {
            CartesianChart(chart).verticalAxisRenderers = [ axisRenderer ];
            CartesianChart(chart).verticalAxis = axis;
        }

        override protected function assignMeasureAxis(axis:NumericAxis, chart:ChartBase, axisRenderer:IAxisRenderer):void {
            CartesianChart(chart).horizontalAxisRenderers = [ axisRenderer ];
            CartesianChart(chart).horizontalAxis = axis;
        }

        override protected function createSeries(measure:AnalysisMeasure, dataSet:ArrayCollection, xField:String, measureNumber:int):Series {
            var barSeries:BarSeries = new BarSeries();
            //noinspection JSSuspiciousNameCombination
            barSeries.yField = xField;
            barSeries.xField = measure.qualifiedName();
            barSeries.labelFunction = function (element:ChartItem, series:Series):String {
                var barSeriesItem:BarSeriesItem = element as BarSeriesItem;
                return measure.getFormatter().format(barSeriesItem.xNumber);
            };
            barSeries.dataProvider = dataSet;
            barSeries.dataFunction = function (series:Series, item:Object, fieldName:String):Object {
                if (fieldName == 'xValue')
                    return(item[measure.qualifiedName()].toNumber());
                else if (fieldName == "yValue")
                    return(item[xField].toString());
                else
                    return null;
            };
            //barSeries.setStyle("labelPosition",BarChartDefinition(chartDef).labelPosition);
            if (BarChartDefinition(chartDef).labelPosition == "auto") {
                var cf:ClassFactory = new ClassFactory(BetterBoxBarRenderer);
                cf.properties = { labelFontSize:BarChartDefinition(chartDef).labelFontSize, labelFontWeight:BarChartDefinition(chartDef).labelFontWeight,
                    formatter:measure.getFormatter(),
                    labelInsideFontColor: BarChartDefinition(chartDef).useInsideLabelFontColor ? BarChartDefinition(chartDef).labelInsideFontColor : 0xFFFFFF,
                    labelOutsideFontColor: BarChartDefinition(chartDef).useOutsideLabelFontColor ? BarChartDefinition(chartDef).labelOutsideFontColor : 0x222222};
                barSeries.setStyle("itemRenderer", cf);
            }

            barSeries.setStyle("fontSize", 18);
            barSeries.displayName = measure.display;
            barSeries.setStyle("showDataEffect", interpolateIn);
            barSeries.setStyle("hideDataEffect", interpolateIn);
            return barSeries;
        }
        ]]></mx:Script>
    <mx:SeriesSlide id="interpolateIn" duration="1000" direction="up"/>
    <mx:SeriesSlide id="interpolateOut" duration="1000" direction="down"/>
</charts:CartesianChartModule>