<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
        width="100%" height="100%">
    <mx:Script>
		<![CDATA[
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.filtering.FilterRawData;

        import mx.charts.ChartItem;
        import mx.charts.chartClasses.IAxis;
        import mx.formatters.Formatter;
        import mx.charts.HitData;
        import mx.collections.ArrayCollection;

        [Bindable]
        private var graphData:ArrayCollection = new ArrayCollection();

        [Bindable]
        private var dimension:String = "";

        [Bindable]
        private var measure1:String = "";
        [Bindable]
        private var measure1Name:String = "";

        private var measure1Formatter:Formatter;

        private var dimensionItem:AnalysisItem;

        [Bindable]
        private var measure2:String = "";
        [Bindable]
        private var measure2Name:String = "";

        private var measure2Formatter:Formatter;

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object):void {
            var plotChartDef:PlotChartDefinition = analysisDefinition as PlotChartDefinition;
            if (dataSet.length > 0) {
                measure1 = plotChartDef.xaxisMeasure.qualifiedName();
                measure1Name = plotChartDef.xaxisMeasure.display;
                measure2 = plotChartDef.yaxisMeasure.qualifiedName();
                measure2Name = plotChartDef.yaxisMeasure.display;
                measure1Formatter = plotChartDef.xaxisMeasure.getFormatter();
                measure2Formatter = plotChartDef.yaxisMeasure.getFormatter();
                dimension = plotChartDef.dimension.qualifiedName();
                dimensionItem = plotChartDef.dimension;
            }
            graphData = dataSet;
        }

        private function dtFunc(hd:HitData):String {
            return hd.item[dimension];
        }

        public function createFilterRawData():FilterRawData {
            var filterRawData:FilterRawData = new FilterRawData();
            for (var i:int = 0; i < plotChart.selectedChartItems.length; i++) {
                var obj:ChartItem = plotChart.selectedChartItems[i];
                filterRawData.addPair(dimensionItem, obj.item[dimensionItem.key.createString()]);
            }
            return filterRawData;
        }

        private function renderXAxis(labelValue:Object, previousValue:Object, axis:IAxis):String {
            if (measure1Formatter != null) {
                return measure1Formatter.format(labelValue);
            } else {
                return String(labelValue);
            }
        }

        private function renderYAxis(labelValue:Object, previousValue:Object, axis:IAxis):String {
            if (measure2Formatter != null) {
                return measure2Formatter.format(labelValue);
            } else {
                return String(labelValue);
            }
        }


        public function updateExportMetadata():void {
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }
		]]>
	</mx:Script>

	<mx:SeriesZoom id="zoomIn" duration="1000"/>
    <mx:SeriesZoom id="zoomOut" duration="1000"/>

	<mx:PlotChart id="plotChart" dataProvider="{graphData}" width="100%" height="100%" showDataTips="true" dataTipFunction="dtFunc"
		selectionMode="multiple">
		<mx:horizontalAxis>
			<mx:LinearAxis labelFunction="renderXAxis" baseAtZero="false" title="{measure1Name}" id="xAxis"/>
		</mx:horizontalAxis>
		<mx:horizontalAxisRenderers>
			<mx:AxisRenderer axis="{xAxis}">

			</mx:AxisRenderer>
		</mx:horizontalAxisRenderers>
		<mx:verticalAxis>
			<mx:LinearAxis labelFunction="renderYAxis" baseAtZero="false" title="{measure2Name}" id="yAxis"/>
		</mx:verticalAxis>
		<mx:verticalAxisRenderers>
			<mx:AxisRenderer axis="{yAxis}">

			</mx:AxisRenderer>
		</mx:verticalAxisRenderers>
		<mx:series>
			<mx:PlotSeries xField="{measure1}" yField="{measure2}" hideDataEffect="zoomOut" showDataEffect="zoomIn"/>
		</mx:series>
	</mx:PlotChart>
</mx:Module>