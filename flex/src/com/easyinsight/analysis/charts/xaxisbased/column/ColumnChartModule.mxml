<?xml version="1.0" ?>
<charts:CartesianChartModule xmlns:mx="http://www.adobe.com/2006/mxml"
                             implements="com.easyinsight.analysis.IReportRenderer"
                             width="100%" height="100%" xmlns:charts="com.easyinsight.analysis.charts.*">
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisItem;

        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.ChartDefinition;

        import com.easyinsight.analysis.SortFunctionFactory;

        import mx.charts.AxisRenderer;

        import mx.charts.CategoryAxis;
        import mx.charts.ChartItem;
        import mx.charts.ColumnChart;
        import mx.charts.HitData;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.CartesianChart;
        import mx.charts.chartClasses.CartesianTransform;
        import mx.charts.chartClasses.ChartBase;
        import mx.charts.chartClasses.DataTransform;
        import mx.charts.chartClasses.IAxisRenderer;
        import mx.charts.chartClasses.NumericAxis;
        import mx.charts.chartClasses.Series;
        import mx.charts.series.ColumnSeries;
        import mx.charts.series.items.ColumnSeriesItem;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;

        override protected function getDimensionItem():AnalysisItem {
            return ColumnChartDefinition(chartDef).xaxis;
        }

        override protected function getMeasures():ArrayCollection {
            return ColumnChartDefinition(chartDef).measures;
        }

        override protected function createChartObject():ChartBase {
            return new ColumnChart();
        }

        override protected function useGradientColor():Boolean {
            return ColumnChartDefinition(chartDef).gradientColor > 0;
        }

        override protected function getGradientColor():uint {
            return ColumnChartDefinition(chartDef).gradientColor;
        }

        override protected function useChartColor():Boolean {
            return ColumnChartDefinition(chartDef).useChartColor;
        }

        override protected function getChartColor():uint {
            return ColumnChartDefinition(chartDef).chartColor;
        }

        override protected function getColorScheme():String {
            return ColumnChartDefinition(chartDef).colorScheme;
        }

        override protected function sortData(dataSet:ArrayCollection):void {
            var columnChartDef:ColumnChartDefinition = chartDef as ColumnChartDefinition;
            var firstMeasure:AnalysisMeasure = columnChartDef.measures.getItemAt(0) as AnalysisMeasure;
            if (columnChartDef.columnSort != ChartDefinition.SORT_UNSORTED) {
                var sort:Sort = new Sort();
                if (columnChartDef.columnSort == ChartDefinition.SORT_X_ASCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(getDimensionItem(), false);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_X_DESCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(getDimensionItem(), true);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_Y_ASCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(firstMeasure, false);
                } else if (columnChartDef.columnSort == ChartDefinition.SORT_Y_DESCENDING) {
                    sort.compareFunction = SortFunctionFactory.createSortFunction(firstMeasure, true);
                }
                dataSet.sort = sort;
                dataSet.refresh();
            }
        }

        override protected function formatDataTip(hd:HitData):String {
            var dt:String = "";
            var columnSeriesItem:ColumnSeriesItem = hd.chartItem as ColumnSeriesItem;
            var series:Series = columnSeriesItem.element as Series;
            var dataTransform:DataTransform = series.dataTransform;
            var n:String = series.displayName;
            if (n != null && n.length > 0)
                dt += "<b>" + n + "</b><BR/>";

            var xName:String = dataTransform.getAxis(CartesianTransform.HORIZONTAL_AXIS).displayName;
            if (xName != "")
                dt += "<i>" + xName + ":</i> ";
            dt += dimensionFormatter.format(ColumnSeriesItem(hd.chartItem).xValue) + "\n";

            var yName:String = dataTransform.getAxis(CartesianTransform.VERTICAL_AXIS).displayName;

            if (yName != "")
                dt += "<i>" + yName + ":</i> ";
            dt += measureFormatter.format(ColumnSeriesItem(hd.chartItem).yValue) + "\n";

            return dt;
        }

        override protected function direction():String {
            return "horizontal";
        }

        override protected function assignDimensionAxis(axis:CategoryAxis, chart:ChartBase, axisRenderer:IAxisRenderer):void {

            CartesianChart(chart).horizontalAxis = axis;
            CartesianChart(chart).horizontalAxisRenderer = axisRenderer;
        }

        override protected function assignMeasureAxis(axis:NumericAxis, chart:ChartBase, axisRenderer:IAxisRenderer):void {
            CartesianChart(chart).verticalAxis = axis;
            CartesianChart(chart).verticalAxisRenderer = axisRenderer;
            axis.baseAtZero = chartDef.yAxisBaseAtZero;
        }

        override protected function createSeries(measure:AnalysisMeasure, dataSet:ArrayCollection, xField:String, measureNumber:int):Series {
            var columnSeries:ColumnSeries = new ColumnSeries();
            columnSeries.xField = xField;
            columnSeries.yField = measure.qualifiedName();
            columnSeries.labelFunction = function (element:ChartItem, series:Series):String {
                var columnSeriesItem:ColumnSeriesItem = element as ColumnSeriesItem;
                return measure.getFormatter().format(columnSeriesItem.yNumber);
            };
            columnSeries.dataProvider = dataSet;
            if (ColumnChartDefinition(chartDef).labelPosition == "auto") {
                var cf:ClassFactory = new ClassFactory(BetterBoxRenderer);
                cf.properties = { labelFontSize: ColumnChartDefinition(chartDef).labelFontSize, labelFontWeight: ColumnChartDefinition(chartDef).labelFontWeight,
                    formatter: measure.getFormatter(),
                    labelInsideFontColor: ColumnChartDefinition(chartDef).useInsideLabelFontColor ? ColumnChartDefinition(chartDef).labelInsideFontColor : 0xFFFFFF,
                    labelOutsideFontColor: ColumnChartDefinition(chartDef).useOutsideLabelFontColor ? ColumnChartDefinition(chartDef).labelOutsideFontColor : 0x222222};
                columnSeries.setStyle("itemRenderer", cf);
            }

            columnSeries.dataFunction = function(series:Series, item:Object, fieldName:String):Object {
                if (measure == null) {
                    return null;
                }
                if (fieldName == 'yValue')
                    return(item[measure.qualifiedName()].toNumber());
                else if (fieldName == "xValue")
                    return(item[xField].toString());
                else
                    return null;
            };
            //columnSeries.setStyle("labelPosition",ColumnChartDefinition(chartDef).labelPosition);
            columnSeries.displayName = measure.display;
            columnSeries.setStyle("showDataEffect", interpolateIn);
            columnSeries.setStyle("hideDataEffect", interpolateIn);
            return columnSeries;
        }
        ]]></mx:Script>
    <mx:SeriesSlide id="interpolateIn" duration="1000" direction="up"/>
    <mx:SeriesSlide id="interpolateOut" duration="1000" direction="down"/>
</charts:CartesianChartModule>