<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
        width="100%" height="100%" creationComplete="onCreation()">
    <mx:Script><![CDATA[

        import com.easyinsight.analysis.AnalysisDateDimension;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.charts.Chart3DVisuals;
        import com.easyinsight.filtering.FilterRawData;

        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import ilog.charts3d.AreaChart3D;
        import ilog.charts3d.series.AreaSeries3D;

        import ilog.charts3d.series.items.AreaSeries3DItem;

        import mx.charts.CategoryAxis;
        import mx.charts.DateTimeAxis;
        import mx.charts.HitData;
        import mx.charts.Legend;
        import mx.charts.chartClasses.IAxis;
        import mx.charts.events.ChartItemEvent;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.managers.PopUpManager;

        [Bindable]
        private var chartData:ArrayCollection;

        private var areaChart:AreaChart3D;

        private var xAxisItem:AnalysisItem;
        private var yAxisItem:AnalysisItem;
        private var measureItem:AnalysisItem;

        private var legend:Legend;

        private var visuals:Chart3DVisuals = new Chart3DVisuals();

        private function dataTipFunction(hitData:HitData):String {
            var string:String;
            var lineSeries:AreaSeries3D = hitData.element as AreaSeries3D;
            var lineSeriesItem:AreaSeries3DItem = hitData.chartItem as AreaSeries3DItem;
            string = lineSeries.displayName + "\n" +
                     xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + measureItem.getFormatter().format(lineSeriesItem.yValue);
            return string;
        }

        private function onCreation():void {
            visuals.addEventListener(CustomChangeEvent.CUSTOM_CHANGE, onCustomChange);
        }

        private function onCustomChange(event:CustomChangeEvent):void {
            dispatchEvent(event);
        }

        override protected function createChildren():void {
            super.createChildren();
            if (areaChart == null) {
                areaChart = new AreaChart3D();
                areaChart.percentHeight = 100;
                areaChart.percentWidth = 100;
                //areaChart.dataProvider = chartData;
                areaChart.selectionMode = "multiple";
            }
            hbox.addChild(areaChart);
            if (legend == null) {
                legend = new Legend();
                legend.direction = "vertical";
                legend.percentHeight = 100;
            }
            hbox.addChild(legend);
        }

        private function onClick(event:ChartItemEvent):void {
            if (event.shiftKey) {

                var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this);
                window.data = event.hitData.item;
                PopUpManager.addPopUp(window, this);
                window.x = event.stageX + 5;
                window.y = event.stageY + 5;
            }
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            var areaChartDef:Area3DChartDefinition = analysisDefinition as Area3DChartDefinition;

            this.chartData = new ArrayCollection(dataSet.toArray());

            hbox.removeChild(areaChart);

            areaChart = new AreaChart3D();
            areaChart.percentHeight = 100;
            areaChart.percentWidth = 100;
            visuals.chart = areaChart;
            areaChart.dataProvider = chartData;
            areaChart.selectionMode = "multiple";
            areaChart.showDataTips = true;
            areaChart.dataTipFunction = dataTipFunction;
            areaChart.addEventListener(ChartItemEvent.ITEM_CLICK, onClick);

            if (dataSet.length > 0) {
                if (areaChartDef.elevationAngle > 0) {
                    visuals.elevationAngle = areaChartDef.elevationAngle;
                    visuals.rotationAngle = areaChartDef.rotationAngle;
                }


                var xAxisDimension:AnalysisItem = areaChartDef.xaxis;

                xAxisItem = xAxisDimension;
                measureItem = areaChartDef.measure;

                var sort:Sort = new Sort();
                var sortField:SortField = new SortField(areaChartDef.measure.qualifiedName(), true);
                sort.fields = [ sortField ];
                if (chartData.length > 0) {
                    var firstObj:Object = chartData.getItemAt(0);
                    var firstVal:Object = firstObj[areaChartDef.measure.qualifiedName()];
                    if (firstVal is Number) {
                        sortField.numeric = true;
                    }

                    chartData.sort = sort;
                    chartData.refresh();

                    if (firstVal is Number) {
                        var categories:ArrayCollection = new ArrayCollection();
                        for each (var obj:Object in chartData) {
                            var valObj:Object = obj[xAxisDimension.qualifiedName()];
                            if (categories.getItemIndex(valObj) == -1) {
                                categories.addItem(valObj);
                            }
                        }
                    }
                }


                var xAxis:IAxis;

                if (xAxisDimension.hasType(AnalysisItemTypes.DATE)) {
                    var dateDimension:AnalysisDateDimension = xAxisDimension as AnalysisDateDimension;
                    var dateAxis:DateTimeAxis = new DateTimeAxis();
                    switch (dateDimension.dateLevel) {
                        case AnalysisItemTypes.YEAR_LEVEL:
                            dateAxis.dataUnits = "years";
                            break;
                        case AnalysisItemTypes.MONTH_LEVEL:
                            dateAxis.dataUnits = "months";
                            break;
                        case AnalysisItemTypes.DAY_LEVEL:
                            dateAxis.dataUnits = "days";
                            break;
                        case AnalysisItemTypes.HOUR_LEVEL:
                            dateAxis.dataUnits = "hours";
                            break;
                        case AnalysisItemTypes.MINUTE_LEVEL:
                            dateAxis.dataUnits = "minutes";
                            break;
                    }
                    dateAxis.displayName = dateDimension.display;
                    if (additionalProperties["xAxisMinimum"] != null) {
                        dateAxis.minimum = additionalProperties["xAxisMinimum"];
                    }
                    if (additionalProperties["xAxisMaximum"] != null) {
                        dateAxis.maximum = additionalProperties["xAxisMaximum"];
                    }
                    xAxis = dateAxis;
                } else {
                    var categoryAxis:CategoryAxis = new CategoryAxis();
                    categoryAxis.displayName = xAxisDimension.display;
                    categoryAxis.dataProvider = categories;
                    xAxis = categoryAxis;
                }

                areaChart.horizontalAxis = xAxis;

                var mySeries:Array = new Array();
                var analysisMeasure:AnalysisMeasure = areaChartDef.measure as AnalysisMeasure;
                var uniques:ArrayCollection = new ArrayCollection();

                var seriesData:Object = new Object();
                for (var i:int = 0; i < dataSet.length; i++) {
                    var object:Object = dataSet.getItemAt(i);
                    if (object[areaChartDef.xaxis.qualifiedName()] == null ||
                            object[areaChartDef.xaxis.qualifiedName()] == "") {
                        continue;
                    }
                    var dimensionValue:String = object[areaChartDef.yaxis.qualifiedName()];
                    if (dimensionValue == null || dimensionValue == "") {
                        dimensionValue = "[ No Value ]";
                    }
                    var newSeriesData:ArrayCollection = seriesData[dimensionValue];
                    if (newSeriesData == null) {
                        newSeriesData = new ArrayCollection();
                        seriesData[dimensionValue] = newSeriesData;
                    }
                    var newObject:Object = new Object();
                    newObject[areaChartDef.xaxis.qualifiedName()] = object[areaChartDef.xaxis.qualifiedName()];
                    newObject[dimensionValue] = object[analysisMeasure.qualifiedName()];
                    newSeriesData.addItem(newObject);
                    //allItems.addItem(newObject);
                    if (!uniques.contains(dimensionValue)) {
                        uniques.addItem(dimensionValue);
                    }
                }
                for (i = 0; i < uniques.length; i++) {
                    var key:String = uniques.getItemAt(i) as String;
                    var uniqueLineSeries:AreaSeries3D = new AreaSeries3D();
                    uniqueLineSeries.xField = areaChartDef.xaxis.qualifiedName();
                    if (key == null || key == "") {
                        key = "[ No Value ]";
                    }
                    uniqueLineSeries.yField = key;
                    uniqueLineSeries.displayName = key;
                    uniqueLineSeries.dataProvider = seriesData[key];
                    mySeries.push(uniqueLineSeries);
                }

                areaChart.series = mySeries;
            }

            legend.dataProvider = areaChart;
            legend.direction = "vertical";
            hbox.addChildAt(areaChart, 0);
        }


        public function createFilterRawData():FilterRawData {
            return null;
        }

        public function updateExportMetadata():void {
        }

        public function preserveValues():Boolean {
            return false;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }]]></mx:Script>
    <mx:HBox width="100%" height="100%" id="hbox"/>
</mx:Module>