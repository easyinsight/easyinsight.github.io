<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
        width="100%" height="100%">
    <mx:states>
        <mx:State name="hideLegend">
            <mx:RemoveChild target="{legend}"/>
        </mx:State>
    </mx:states>
    <!--<mx:Style>
        .axisTitles {
            fontSize: 14;
            fontFamily: "Tahoma";
        }

        CartesianChart {
            axisTitleStyleName:axisTitles;
            fontFamily: "Tahoma";
            fontSize: 14;
        }
    </mx:Style>-->
    <mx:Script><![CDATA[

        import com.easyinsight.analysis.AnalysisDateDimension;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.filtering.FilterRawData;

        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import mx.charts.CategoryAxis;
        import mx.charts.DateTimeAxis;
        import mx.charts.HitData;
        import mx.charts.Legend;
        import mx.charts.AreaChart;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.IAxis;
        import mx.charts.events.ChartItemEvent;
        import mx.charts.renderers.CircleItemRenderer;
        import mx.charts.series.AreaSeries;
        import mx.charts.series.AreaSet;
        import mx.charts.series.items.AreaSeriesItem;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.managers.PopUpManager;

        [Bindable]
        private var chartData:ArrayCollection;

        private var areaChart:AreaChart;

        private var xAxisItem:AnalysisItem;
        private var yAxisItem:AnalysisItem;
        private var measureItem:AnalysisItem;

        [Bindable]
        private var legend:Legend;

        private var def:AreaChartDefinition;

        private function dataTipFunction(hitData:HitData):String {
            var string:String;
            var lineSeries:AreaSeries = hitData.element as AreaSeries;
            var lineSeriesItem:AreaSeriesItem = hitData.chartItem as AreaSeriesItem;
            if (def.multiMeasure) {
                for each (var aMeasure:AnalysisMeasure in def.measures) {
                    if (aMeasure.display == lineSeries.displayName) {
                        string = lineSeries.displayName + "\n" +
                                xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + aMeasure.getFormatter().format(lineSeriesItem.yValue);
                        break;
                    }
                }
            } else {
                string = lineSeries.displayName + "\n" +
                        xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + measureItem.getFormatter().format(lineSeriesItem.yValue);
            }
            return string;
        }

        override protected function createChildren():void {
            super.createChildren();
            if (areaChart == null) {
                areaChart = new AreaChart();
                areaChart.percentHeight = 100;
                areaChart.percentWidth = 100;
                //areaChart.dataProvider = chartData;
                areaChart.selectionMode = "multiple";
            }
            hbox.addChild(areaChart);
            if (legend == null) {
                legend = new Legend();
                legend.direction = "vertical";
                legend.percentHeight = 100;
            }
            hbox.addChild(legend);
        }

        private function onClick(event:ChartItemEvent):void {

            var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this, areaChartDef, event.hitData.item);
            PopUpManager.addPopUp(window, this);
            window.x = event.stageX + 5;
            window.y = event.stageY + 5;
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        private var areaChartDef:AreaChartDefinition;

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            areaChartDef = analysisDefinition as AreaChartDefinition;

            this.chartData = new ArrayCollection(dataSet.toArray());

            hbox.removeChild(areaChart);

            areaChart = new AreaChart();
            areaChart.setStyle("fontFamily", areaChartDef.getFont());
            areaChart.setStyle("fontSize", areaChartDef.fontSize);
            areaChart.showDataTips = true;
            areaChart.dataTipFunction = dataTipFunction;
            areaChart.percentHeight = 100;
            areaChart.percentWidth = 100;
            //areaChart.dataProvider = chartData;
            areaChart.type = areaChartDef.stackingType;
            //areaChart.selectionMode = "multiple";
            var verticalAxis:LinearAxis = new LinearAxis();
            verticalAxis.baseAtZero = areaChartDef.baseAtZero == "true";
            if (!areaChartDef.multiMeasure && areaChartDef.measure != null) {
                verticalAxis.title = areaChartDef.measure.display;
            } else {
                verticalAxis.title = "Measures";
            }
            areaChart.verticalAxis = verticalAxis;

            if (dataSet.length > 0) {

                if (areaChartDef.showLegend) {
                    currentState = "";
                } else {
                    currentState = "hideLegend";
                }

                this.def = areaChartDef;
                var xAxisDimension:AnalysisItem = areaChartDef.xaxis;

                xAxisItem = xAxisDimension;
                yAxisItem = areaChartDef.yaxis;
                measureItem = areaChartDef.measure;

                var sort:Sort = new Sort();
                var sortField:SortField = new SortField(xAxisDimension.qualifiedName(), true);
                sort.fields = [ sortField ];
                if (chartData.length > 0) {
                    var firstObj:Object = chartData.getItemAt(0);
                    var firstVal:Object = firstObj[xAxisDimension.qualifiedName()];
                    if (firstVal is Number) {
                        sortField.numeric = true;
                    }

                    chartData.sort = sort;
                    chartData.refresh();

                    if (firstVal is Number) {
                        var categories:ArrayCollection = new ArrayCollection();
                        for each (var obj:Object in chartData) {
                            var valObj:Object = obj[xAxisDimension.qualifiedName()];
                            if (categories.getItemIndex(valObj) == -1) {
                                categories.addItem(valObj);
                            }
                        }
                    }
                }


                var xAxis:IAxis;

                if (xAxisDimension.hasType(AnalysisItemTypes.DATE)) {
                    var dateDimension:AnalysisDateDimension = xAxisDimension as AnalysisDateDimension;
                    var dateAxis:DateTimeAxis = new DateTimeAxis();
                    switch (dateDimension.dateLevel) {
                        case AnalysisItemTypes.YEAR_LEVEL:
                            dateAxis.dataUnits = "years";
                            break;
                        case AnalysisItemTypes.MONTH_LEVEL:
                        case AnalysisItemTypes.QUARTER_OF_YEAR:
                            dateAxis.dataUnits = "months";
                            break;
                        case AnalysisItemTypes.DAY_LEVEL:
                            dateAxis.dataUnits = "days";
                            break;
                        case AnalysisItemTypes.HOUR_LEVEL:
                            dateAxis.dataUnits = "hours";
                            break;
                        case AnalysisItemTypes.MINUTE_LEVEL:
                            dateAxis.dataUnits = "minutes";
                            break;
                    }
                    dateAxis.displayName = dateDimension.display;
                    if (additionalProperties["xAxisMinimum"] != null) {
                        dateAxis.minimum = additionalProperties["xAxisMinimum"];
                    }
                    if (additionalProperties["xAxisMaximum"] != null) {
                        dateAxis.maximum = additionalProperties["xAxisMaximum"];
                    }
                    dateAxis.title = xAxisDimension.display;
                    xAxis = dateAxis;
                } else {
                    var categoryAxis:CategoryAxis = new CategoryAxis();
                    categoryAxis.displayName = xAxisDimension.display;
                    categoryAxis.dataProvider = categories;
                    categoryAxis.title = xAxisDimension.display;
                    xAxis = categoryAxis;
                }

                areaChart.horizontalAxis = xAxis;

                var mySeries:Array = new Array();
                var analysisMeasure:AnalysisMeasure = areaChartDef.measure as AnalysisMeasure;
                var uniques:ArrayCollection = new ArrayCollection();

                var seriesData:Object = new Object();
                var points:ArrayCollection = areaChartDef.populateData(dataSet, seriesData, uniques);
                areaChart.dataProvider = points;
                for (var i:int = 0; i < uniques.length; i++) {
                    var key:String = uniques.getItemAt(i) as String;
                    var uniqueLineSeries:AreaSeries = new AreaSeries();
                    uniqueLineSeries.setStyle("form", areaChartDef.form);
                    //uniqueLineSeries.setStyle("itemRenderer", new ClassFactory(CircleItemRenderer));
                    uniqueLineSeries.xField = areaChartDef.xaxis.qualifiedName();
                    if (key == null || key == "") {
                        key = "[ No Value ]";
                    }
                    uniqueLineSeries.yField = key;
                    uniqueLineSeries.setStyle("areaStroke", strokes[i % 6]);
                    uniqueLineSeries.setStyle("areaFill", fills[i % 6]);
                    uniqueLineSeries.displayName = key;
                    //uniqueLineSeries.dataProvider = seriesData[key];
                    mySeries.push(uniqueLineSeries);
                }
                /*areaSet.type = "100%";
                areaSet.series = mySeries;*/

                areaChart.series = mySeries;
            }

            legend.dataProvider = areaChart;
            legend.direction = "vertical";
            hbox.addChildAt(areaChart, 0);
        }


        public function createFilterRawData():FilterRawData {
            return null;
        }

        public function updateExportMetadata():void {
        }

        public function preserveValues():Boolean {
            return false;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }]]></mx:Script>
    <mx:Array id="fills">
        <mx:SolidColor color="0xa6bc59" alpha=".9"/>
        <mx:SolidColor color="0x597197" alpha=".9"/>
        <mx:SolidColor color="0xd6ab2a" alpha=".9"/>
        <mx:SolidColor color="0xd86068" alpha=".9"/>
        <mx:SolidColor color="0x5d9942" alpha=".9"/>
        <mx:SolidColor color="0x7a4c6c" alpha=".9"/>
    </mx:Array>

    <!-- Define custom Strokes. -->
    <mx:Array id="strokes">
        <mx:Stroke color="0xa6bc59" weight="2"/>
        <mx:Stroke color="0x597197" weight="2"/>
        <mx:Stroke color="0xd6ab2a" weight="2"/>
        <mx:Stroke color="0xd86068" weight="2"/>
        <mx:Stroke color="0x5d9942" weight="2"/>
        <mx:Stroke color="0x7a4c6c" weight="2"/>
    </mx:Array>
    <mx:HBox width="100%" height="100%" id="hbox"/>
</mx:Module>