<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.ISelectableReportRenderer"
           width="100%" height="100%">
    <mx:Style>
        .axisTitles {
            fontSize: 14;
            fontFamily: "Tahoma";
        }

        CartesianChart {
            axisTitleStyleName:axisTitles;
            fontFamily: "Tahoma";
            fontSize: 14;
        }
    </mx:Style>
    <mx:Script><![CDATA[

        import com.easyinsight.analysis.AnalysisDateDimension;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.FillProvider;
        import com.easyinsight.filtering.FilterRawData;

        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import mx.charts.CategoryAxis;
        import mx.charts.ChartItem;
        import mx.charts.DateTimeAxis;
        import mx.charts.HitData;
        import mx.charts.Legend;
        import mx.charts.LineChart;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.IAxis;
        import mx.charts.events.ChartItemEvent;
        import mx.charts.renderers.CircleItemRenderer;
        import mx.charts.series.LineSeries;
        import mx.charts.series.items.LineSeriesItem;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.graphics.IStroke;
        import mx.graphics.SolidColor;
        import mx.graphics.Stroke;
        import mx.managers.PopUpManager;

        [Bindable]
        private var chartData:ArrayCollection;

        private var lineChart:LineChart;

        private var xAxisItem:AnalysisItem;
        private var yAxisItem:AnalysisItem;
        private var measureItem:AnalysisItem;

        private var def:LineChartDefinition;

        private var legend:Legend;

        override protected function createChildren():void {
            super.createChildren();
            if (lineChart == null) {
                lineChart = new LineChart();
                lineChart.percentHeight = 100;
                lineChart.percentWidth = 100;
            }
            hbox.addChild(lineChart);
            if (legend == null) {
                legend = new Legend();
                legend.direction = "vertical";
                legend.percentHeight = 100;
            }
            hbox.addChild(legend);
        }

        private function dataTipFunction(hitData:HitData):String {
            var string:String;
            var lineSeries:LineSeries = hitData.element as LineSeries;
            var lineSeriesItem:LineSeriesItem = hitData.chartItem as LineSeriesItem;
            if (def.multiMeasure) {
                for each (var aMeasure:AnalysisMeasure in def.measures) {
                    if (aMeasure.display == lineSeries.displayName) {
                        string = lineSeries.displayName + "\n" +
                                xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + aMeasure.getFormatter().format(lineSeriesItem.yValue);
                        break;
                    }
                }
            } else {
                string = lineSeries.displayName + "\n" +
                        xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + measureItem.getFormatter().format(lineSeriesItem.yValue);
            }
            return string;
        }

        private function onClick(event:ChartItemEvent):void {

            var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this, def, event.hitData.item);
            PopUpManager.addPopUp(window, this);
            window.x = event.stageX + 5;
            window.y = event.stageY + 5;
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            var lineChartDef:LineChartDefinition = analysisDefinition as LineChartDefinition;

            if (dataSet.length > 0) {

                coreBox.setStyle("backgroundAlpha", lineChartDef.backgroundAlpha);

                stackIndex = 0;
                this.def = lineChartDef;

                this.chartData = new ArrayCollection(dataSet.toArray());

                hbox.removeChild(lineChart);

                lineChart = new LineChart();
                legend.visible = true;
                lineChart.percentHeight = 100;
                lineChart.percentWidth = 100;
                lineChart.dataProvider = chartData;
                lineChart.showDataTips = true;
                lineChart.selectionMode = _selectionMode;
                lineChart.addEventListener(ChartItemEvent.ITEM_CLICK, onClick);
                lineChart.dataTipFunction = dataTipFunction;
                var verticalAxis:LinearAxis = new LinearAxis();
                verticalAxis.baseAtZero = lineChartDef.baseAtZero == "true";
                lineChart.verticalAxis = verticalAxis;

                var strokes:Array = [];
                var colors:Array = FillProvider.createSolidColors();
                for each (var color:SolidColor in colors) {
                    var newStroke:Stroke = new Stroke();
                    newStroke.color = color.color;
                    newStroke.alpha = 1;
                    strokes.push(newStroke);
                }
                var xAxisDimension:AnalysisItem = lineChartDef.xaxis;

                var sort:Sort = new Sort();
                var sortField:SortField = new SortField(xAxisDimension.qualifiedName(), true);
                sort.fields = [ sortField ];
                if (chartData.length > 0) {
                    var firstObj:Object = chartData.getItemAt(0);
                    var firstVal:Object = firstObj[xAxisDimension.qualifiedName()];
                    if (firstVal is Number) {
                        sortField.numeric = true;
                    }

                    chartData.sort = sort;
                    chartData.refresh();

                    if (firstVal is Number) {
                        var categories:ArrayCollection = new ArrayCollection();
                        for each (var obj:Object in chartData) {
                            var valObj:Object = obj[xAxisDimension.qualifiedName()];
                            if (categories.getItemIndex(valObj) == -1) {
                                categories.addItem(valObj);
                            }
                        }
                    }
                }


                var xAxis:IAxis;
                xAxisItem = xAxisDimension;
                yAxisItem = lineChartDef.yaxis;
                if (xAxisDimension.hasType(AnalysisItemTypes.DATE)) {
                    var dateDimension:AnalysisDateDimension = xAxisDimension as AnalysisDateDimension;
                    var dateAxis:DateTimeAxis = new DateTimeAxis();
                    switch (dateDimension.dateLevel) {
                        case AnalysisItemTypes.YEAR_LEVEL:
                            dateAxis.dataUnits = "years";
                            break;
                        case AnalysisItemTypes.MONTH_LEVEL:
                        case AnalysisItemTypes.QUARTER_OF_YEAR:
                            dateAxis.dataUnits = "months";
                            break;
                        case AnalysisItemTypes.DAY_LEVEL:
                            dateAxis.dataUnits = "days";
                            break;
                        case AnalysisItemTypes.HOUR_LEVEL:
                            dateAxis.dataUnits = "hours";
                            break;
                        case AnalysisItemTypes.MINUTE_LEVEL:
                            dateAxis.dataUnits = "minutes";
                            break;
                    }
                    dateAxis.displayName = dateDimension.display;
                    if (additionalProperties != null && additionalProperties["xAxisMinimum"] != null) {
                        dateAxis.minimum = additionalProperties["xAxisMinimum"];
                    }
                    if (additionalProperties != null && additionalProperties["xAxisMaximum"] != null) {
                        dateAxis.maximum = additionalProperties["xAxisMaximum"];
                    }
                    xAxis = dateAxis;
                } else {
                    var categoryAxis:CategoryAxis = new CategoryAxis();
                    categoryAxis.displayName = xAxisDimension.display;
                    categoryAxis.dataProvider = categories;
                    xAxis = categoryAxis;
                }

                lineChart.horizontalAxis = xAxis;

                var mySeries:Array = new Array();
                measureItem = lineChartDef.measure as AnalysisMeasure;
                var uniques:ArrayCollection = new ArrayCollection();

                var seriesData:Object = new Object();
                lineChartDef.populateData(dataSet, seriesData, uniques);
                for (var i:int = 0; i < uniques.length; i++) {
                    var key:String = uniques.getItemAt(i) as String;
                    var uniqueLineSeries:LineSeries = new LineSeries();
                    uniqueLineSeries.setStyle("form", lineChartDef.form);
                    uniqueLineSeries.selectable = true;
                    uniqueLineSeries.setStyle("fill", colors[i % colors.length]);
                    uniqueLineSeries.interpolateValues = lineChartDef.interpolateValues == "true";
                    var stroke:IStroke = strokes[i % strokes.length];
                    stroke.weight = lineChartDef.strokeWeight;
                    uniqueLineSeries.setStyle("lineStroke", stroke);
                    uniqueLineSeries.setStyle("stroke", stroke);
                    uniqueLineSeries.setStyle("itemRenderer", new ClassFactory(CircleItemRenderer));
                    uniqueLineSeries.xField = lineChartDef.xaxis.qualifiedName();
                    if (key == null || key == "") {
                        key = "[ No Value ]";
                    }
                    uniqueLineSeries.yField = key;
                    uniqueLineSeries.displayName = key;
                    uniqueLineSeries.dataProvider = seriesData[key];
                    mySeries.push(uniqueLineSeries);
                }

                lineChart.series = mySeries;

                hbox.addChildAt(lineChart, 0);

                legend.dataProvider = lineChart;
                legend.direction = "vertical";
            } else {
                if (lineChartDef.measure != null && lineChartDef.xaxis != null && lineChartDef.yaxis != null) stackIndex = 2;
                else stackIndex = 1;
                if (lineChart != null) {
                    lineChart.visible = false;
                    legend.visible = false;
                }
            }
        }


        public function createFilterRawData():FilterRawData {
            var filterRawData:FilterRawData = new FilterRawData();
            for (var i:int = 0; i < lineChart.selectedChartItems.length; i++) {
                var obj:ChartItem = lineChart.selectedChartItems[i];
                filterRawData.addPair(measureItem, obj.item[measureItem.qualifiedName()]);
            }
            return filterRawData;
        }

        public function updateExportMetadata():void {
        }

        public function preserveValues():Boolean {
            return false;
        }

        [Bindable]
        private var stackIndex:int = 0;

        private var _selectionMode:String = "none";

        public function set selectionEnabled(mode:Boolean):void {
            if (mode) {
                _selectionMode = "multiple";
            } else {
                _selectionMode = "none";
            }
            if (lineChart != null) {
                lineChart.selectionMode = _selectionMode;
            }
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }]]></mx:Script>
    <mx:Canvas width="100%" height="100%" backgroundColor="#FFFFFF" id="coreBox">
        <mx:HBox width="100%" height="100%" id="hbox"/>

        <mx:ViewStack width="100%" height="100%" selectedIndex="{stackIndex}" creationPolicy="all" mouseChildren="false"
                      mouseEnabled="false">
            <mx:Canvas width="100%" height="100%"/>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000"
                    backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center"
                        verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22"
                                 text="You haven't set up a report yet. Drag two Groupings and one Measure into the labeled areas above the canvas to create a report."
                                 borderStyle="none" backgroundAlpha="0" width="500" height="95"/>
                </mx:Box>
            </mx:Box>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000"
                    backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center"
                        verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22"
                                 text="We didn't find any data for the fields and filters that you specified in the report."
                                 borderStyle="none" backgroundAlpha="0" width="500" height="65"/>
                </mx:Box>
            </mx:Box>
        </mx:ViewStack>
    </mx:Canvas>
</mx:Module>