<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
           width="100%" height="100%">
    <mx:states>
        <mx:State name="hideLegend">
            <mx:RemoveChild target="{legend}"/>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisDateDimension;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.FillProvider;
        import com.easyinsight.analysis.MultiColor;
        import com.easyinsight.analysis.charts.ChartUtil;
        import com.easyinsight.analysis.charts.EILegend;

        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import mx.charts.AxisRenderer;

        import mx.charts.CategoryAxis;
        import mx.charts.DateTimeAxis;
        import mx.charts.HitData;
        import mx.charts.LineChart;
        import mx.charts.LinearAxis;
        import mx.charts.chartClasses.IAxis;
        import mx.charts.events.ChartItemEvent;
        import mx.charts.series.LineSeries;
        import mx.charts.series.items.LineSeriesItem;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.graphics.IStroke;
        import mx.graphics.SolidColor;
        import mx.graphics.Stroke;
        import mx.managers.PopUpManager;

        [Bindable]
        private var chartData:ArrayCollection;

        private var lineChart:LineChart;

        private var xAxisItem:AnalysisItem;
        private var yAxisItem:AnalysisItem;
        private var measureItem:AnalysisItem;

        private var def:LineChartDefinition;

        [Bindable]
        private var legend:EILegend;

        override protected function createChildren():void {
            super.createChildren();
            if (lineChart == null) {
                lineChart = new LineChart();
                lineChart.percentHeight = 100;
                lineChart.percentWidth = 100;
            }
            hbox.addChild(lineChart);
            if (legend == null) {
                legend = new EILegend();
                legend.percentHeight = 100;
            }
            hbox.addChild(legend);
        }

        private function dataTipFunction(hitData:HitData):String {
            var string:String;
            var lineSeries:LineSeries = hitData.element as LineSeries;
            var lineSeriesItem:LineSeriesItem = hitData.chartItem as LineSeriesItem;
            if (def.multiMeasure) {
                for each (var aMeasure:AnalysisMeasure in def.measures) {
                    if (aMeasure.display == lineSeries.displayName) {
                        string = lineSeries.displayName + "\n" +
                                xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + aMeasure.getFormatter().format(lineSeriesItem.yValue);
                        break;
                    }
                }
            } else {
                string = lineSeries.displayName + "\n" +
                        xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + measureItem.getFormatter().format(lineSeriesItem.yValue);
            }
            return string;
        }

        private function onClick(event:ChartItemEvent):void {

            var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this, def, event.hitData.item);
            PopUpManager.addPopUp(window, this);
            window.x = event.stageX + 5;
            window.y = event.stageY + 5;
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        private function legendDataChanged(event:Event):void {
            legend.dataProvider = lineChart.legendData;
        }

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            var lineChartDef:LineChartDefinition = analysisDefinition as LineChartDefinition;

            if (dataSet.length > 0) {

                if (lineChartDef.showLegend) {
                    currentState = "";
                } else {
                    currentState = "hideLegend";
                }

                coreBox.setStyle("backgroundAlpha", lineChartDef.backgroundAlpha);

                this.def = lineChartDef;

                this.chartData = new ArrayCollection(dataSet.toArray());

                lineChart.removeEventListener("legendDataChanged", legendDataChanged);
                hbox.removeChild(lineChart);

                lineChart = new LineChart();
                lineChartDef.styleChart(lineChart);
                lineChart.addEventListener("legendDataChanged", legendDataChanged);
                legend.maxWidth = lineChartDef.legendMaxWidth;
                legend.visible = true;
                lineChart.percentHeight = 100;
                lineChart.percentWidth = 100;
                lineChart.dataProvider = chartData;
                lineChart.showDataTips = true;
                lineChart.addEventListener(ChartItemEvent.ITEM_CLICK, onClick);
                lineChart.backgroundElements = ChartUtil.gridLine("horizontal");
                lineChart.dataTipFunction = dataTipFunction;
                var verticalAxis:LinearAxis = new LinearAxis();
                verticalAxis.baseAtZero = lineChartDef.baseAtZero == "true";
                if (!lineChartDef.multiMeasure && lineChartDef.measure != null) {
                    verticalAxis.title = lineChartDef.measure.display;
                } else {
                    if (lineChartDef.measures.length == 1) {
                        verticalAxis.title = AnalysisItem(lineChartDef.measures.getItemAt(0)).display;
                    } else {
                        verticalAxis.title = "Measures";
                    }
                }
                var hAxisRenderer:AxisRenderer = new AxisRenderer();
                hAxisRenderer.setStyle("axisStroke", ChartUtil.axisStroke());
                hAxisRenderer.axis = verticalAxis;
                lineChart.verticalAxisRenderer = hAxisRenderer;
                lineChart.verticalAxis = verticalAxis;

                var strokes:Array = [];
                var useCustomColors:Boolean;
                var colors:Array;
                useCustomColors = (lineChartDef.multiColors != null && lineChartDef.multiColors.length && MultiColor(lineChartDef.multiColors.getItemAt(0)).color1StartEnabled);
                if (useCustomColors) {
                    colors = [];
                    for each (var mc:MultiColor in lineChartDef.multiColors) {
                        if (mc.color1StartEnabled) {
                            colors.push(new SolidColor(mc.color1Start));
                        }
                    }
                } else {
                    colors = FillProvider.createSolidColors();
                }

                for each (var color:SolidColor in colors) {
                    var newStroke:Stroke = new Stroke();
                    newStroke.color = color.color;
                    newStroke.alpha = 1;
                    strokes.push(newStroke);
                }
                var xAxisDimension:AnalysisItem = lineChartDef.xaxis;

                var sort:Sort = new Sort();
                var sortField:SortField = new SortField(xAxisDimension.qualifiedName(), true);
                sort.fields = [ sortField ];
                if (chartData.length > 0) {
                    var firstObj:Object = chartData.getItemAt(0);
                    var firstVal:Object = firstObj[xAxisDimension.qualifiedName()];
                    if (firstVal is Number) {
                        sortField.numeric = true;
                    }

                    chartData.sort = sort;
                    chartData.refresh();

                    if (firstVal is Number) {
                        var categories:ArrayCollection = new ArrayCollection();
                        for each (var obj:Object in chartData) {
                            var valObj:Object = obj[xAxisDimension.qualifiedName()];
                            if (categories.getItemIndex(valObj) == -1) {
                                categories.addItem(valObj);
                            }
                        }
                    }
                }


                var xAxis:IAxis;
                xAxisItem = xAxisDimension;
                yAxisItem = lineChartDef.yaxis;
                if (xAxisDimension.hasType(AnalysisItemTypes.DATE)) {
                    var dateDimension:AnalysisDateDimension = xAxisDimension as AnalysisDateDimension;
                    var dateAxis:DateTimeAxis = new DateTimeAxis();
                    dateAxis.alignLabelsToUnits = lineChartDef.alignLabelsToUnits;
                    switch (dateDimension.dateLevel) {
                        case AnalysisItemTypes.YEAR_LEVEL:
                            dateAxis.dataUnits = "years";
                            break;
                        case AnalysisItemTypes.MONTH_LEVEL:
                        case AnalysisItemTypes.QUARTER_OF_YEAR:
                            dateAxis.dataUnits = "months";
                            break;
                        case AnalysisItemTypes.DAY_LEVEL:
                            dateAxis.dataUnits = "days";
                            break;
                        case AnalysisItemTypes.HOUR_LEVEL:
                            dateAxis.dataUnits = "hours";
                            break;
                        case AnalysisItemTypes.MINUTE_LEVEL:
                            dateAxis.dataUnits = "minutes";
                            break;
                    }
                    dateAxis.displayName = dateDimension.display;
                    if (additionalProperties != null && additionalProperties["xAxisMinimum"] != null) {
                        dateAxis.minimum = additionalProperties["xAxisMinimum"];
                    }
                    if (additionalProperties != null && additionalProperties["xAxisMaximum"] != null) {
                        dateAxis.maximum = additionalProperties["xAxisMaximum"];
                    }
                    dateAxis.title = dateDimension.display;
                    xAxis = dateAxis;
                } else {
                    var categoryAxis:CategoryAxis = new CategoryAxis();
                    categoryAxis.displayName = xAxisDimension.display;
                    categoryAxis.dataProvider = categories;
                    categoryAxis.title = xAxisDimension.display;
                    xAxis = categoryAxis;
                }

                var vAxisRenderer:AxisRenderer = new AxisRenderer();
                vAxisRenderer.setStyle("axisStroke", ChartUtil.axisStroke());
                vAxisRenderer.axis = xAxis;
                lineChart.horizontalAxisRenderer = vAxisRenderer;
                lineChart.horizontalAxis = xAxis;

                var mySeries:Array = new Array();
                measureItem = lineChartDef.measure as AnalysisMeasure;
                var uniques:ArrayCollection = new ArrayCollection();

                var seriesData:Object = new Object();
                lineChartDef.populateData(dataSet, seriesData, uniques);
                for (var i:int = 0; i < uniques.length; i++) {
                    var key:String = uniques.getItemAt(i) as String;
                    var uniqueLineSeries:LineSeries = new LineSeries();
                    uniqueLineSeries.setStyle("form", lineChartDef.form);
                    uniqueLineSeries.selectable = true;
                    uniqueLineSeries.setStyle("fill", colors[i % colors.length]);
                    uniqueLineSeries.interpolateValues = lineChartDef.interpolateValues == "true";
                    var stroke:IStroke = strokes[i % strokes.length];
                    stroke.weight = lineChartDef.strokeWeight;
                    uniqueLineSeries.setStyle("lineStroke", stroke);
                    uniqueLineSeries.setStyle("stroke", stroke);
                    if (lineChartDef.showPoints) {
                        var cf:ClassFactory = new ClassFactory(EICircleItemRenderer);
                        cf.properties = { analysisItem:lineChartDef.multiMeasure ? lineChartDef.xaxis : lineChartDef.yaxis};
                        uniqueLineSeries.setStyle("itemRenderer", cf);
                    }
                    uniqueLineSeries.xField = lineChartDef.xaxis.qualifiedName();
                    if (key == null || key == "") {
                        key = "[ No Value ]";
                    }
                    uniqueLineSeries.yField = key;
                    uniqueLineSeries.displayName = key;
                    uniqueLineSeries.dataProvider = seriesData[key];
                    mySeries.push(uniqueLineSeries);
                }

                lineChart.series = mySeries;

                hbox.addChildAt(lineChart, 0);
                //legend.direction = "vertical";
            }
        }

        public function updateExportMetadata():void {
        }

        public function preserveValues():Boolean {
            return false;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }
        ]]></mx:Script>
    <mx:Canvas width="100%" height="100%" backgroundColor="#FFFFFF" id="coreBox">
        <mx:Box width="100%" height="100%" id="hbox" direction="horizontal"/>
    </mx:Canvas>
</mx:Module>