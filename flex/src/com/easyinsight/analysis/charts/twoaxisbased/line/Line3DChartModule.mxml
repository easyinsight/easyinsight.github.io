<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" implements="com.easyinsight.analysis.IReportRenderer"
           width="100%" height="100%" creationComplete="onCreation()">
    <mx:states>
        <mx:State name="hideLegend">
            <mx:RemoveChild target="{legend}"/>
        </mx:State>
    </mx:states>
    <mx:Style>
        .axisTitles {
            fontSize: 14;
            fontFamily: "Tahoma";
        }

        CartesianChart3D {
            axisTitleStyleName:axisTitles;
            fontFamily: "Tahoma";
            fontSize: 14;
        }
    </mx:Style>
    <mx:Script><![CDATA[

        import com.easyinsight.analysis.AnalysisDateDimension;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;

        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.charts.Chart3DVisuals;
        import com.easyinsight.filtering.FilterRawData;

        import com.easyinsight.pseudocontext.PseudoContextWindow;

        import ilog.charts3d.LineChart3D;
        import ilog.charts3d.series.LineSeries3D;

        import ilog.charts3d.series.items.LineSeries3DItem;

        import mx.charts.CategoryAxis;
        import mx.charts.DateTimeAxis;
        import mx.charts.HitData;
        import mx.charts.Legend;
        import mx.charts.chartClasses.IAxis;
        import mx.charts.events.ChartItemEvent;
        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.managers.PopUpManager;

        [Bindable]
        private var chartData:ArrayCollection;

        private var lineChart:LineChart3D;

        private var xAxisItem:AnalysisItem;
        private var yAxisItem:AnalysisItem;
        private var measureItem:AnalysisItem;

        [Bindable]
        private var legend:Legend;

        private var visuals:Chart3DVisuals = new Chart3DVisuals();

        private function onCreation():void {
            visuals.addEventListener(CustomChangeEvent.CUSTOM_CHANGE, onCustomChange);
        }

        private function onCustomChange(event:CustomChangeEvent):void {
            dispatchEvent(event);
        }

        private function dataTipFunction(hitData:HitData):String {
            var string:String;
            var lineSeries:LineSeries3D = hitData.element as LineSeries3D;
            var lineSeriesItem:LineSeries3DItem = hitData.chartItem as LineSeries3DItem;
            string = lineSeries.displayName + "\n" +
                    xAxisItem.getFormatter().format(lineSeriesItem.xValue) + "\n" + measureItem.getFormatter().format(lineSeriesItem.yValue);
            return string;
        }

        override protected function createChildren():void {
            super.createChildren();
            if (lineChart == null) {
                lineChart = new LineChart3D();
                lineChart.percentHeight = 100;
                lineChart.percentWidth = 100;
                //lineChart.dataProvider = chartData;
                lineChart.selectionMode = "multiple";
            }
            hbox.addChild(lineChart);
            if (legend == null) {
                legend = new Legend();
                legend.direction = "vertical";
                legend.percentHeight = 100;
            }
            hbox.addChild(legend);
        }

        private function onClick(event:ChartItemEvent):void {

            var window:PseudoContextWindow = new PseudoContextWindow(yAxisItem, passThrough, this, lineChartDef, event.hitData.item);
            PopUpManager.addPopUp(window, this);
            window.x = event.stageX + 5;
            window.y = event.stageY + 5;
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        private var lineChartDef:Line3DChartDefinition;

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {

            lineChartDef = analysisDefinition as Line3DChartDefinition;

            if (dataSet.length > 0) {

                if (lineChartDef.showLegend) {
                    currentState = "";
                } else {
                    currentState = "hideLegend";
                }

                stackIndex = 0;

                this.chartData = new ArrayCollection(dataSet.toArray());

                hbox.removeChild(lineChart);
                legend.visible = true;

                lineChart = new LineChart3D();
                lineChart.percentHeight = 100;
                lineChart.percentWidth = 100;
                lineChart.addEventListener(ChartItemEvent.ITEM_CLICK, onClick);
                lineChart.dataTipFunction = dataTipFunction;
                visuals.chart = lineChart;
                lineChart.dataProvider = chartData;
                lineChart.selectionMode = "multiple";
                lineChart.showDataTips = false;

                if (lineChartDef.elevationAngle > 0) {

                    visuals.elevationAngle = lineChartDef.elevationAngle;
                    visuals.rotationAngle = lineChartDef.rotationAngle;
                }


                var xAxisDimension:AnalysisItem = lineChartDef.xaxis;

                xAxisItem = xAxisDimension;
                yAxisItem = lineChartDef.yaxis;
                measureItem = lineChartDef.measure;

                var sort:Sort = new Sort();
                var sortField:SortField = new SortField(xAxisDimension.qualifiedName(), true);
                sort.fields = [ sortField ];
                if (chartData.length > 0) {
                    var firstObj:Object = chartData.getItemAt(0);
                    var firstVal:Object = firstObj[xAxisDimension.qualifiedName()];
                    if (firstVal is Number) {
                        sortField.numeric = true;
                    }

                    chartData.sort = sort;
                    chartData.refresh();

                    if (firstVal is Number) {
                        var categories:ArrayCollection = new ArrayCollection();
                        for each (var obj:Object in chartData) {
                            var valObj:Object = obj[xAxisDimension.qualifiedName()];
                            if (categories.getItemIndex(valObj) == -1) {
                                categories.addItem(valObj);
                            }
                        }
                    }
                }


                var xAxis:IAxis;

                if (xAxisDimension.hasType(AnalysisItemTypes.DATE)) {
                    var dateDimension:AnalysisDateDimension = xAxisDimension as AnalysisDateDimension;
                    var dateAxis:DateTimeAxis = new DateTimeAxis();
                    switch (dateDimension.dateLevel) {
                        case AnalysisItemTypes.YEAR_LEVEL:
                            dateAxis.dataUnits = "years";
                            break;
                        case AnalysisItemTypes.MONTH_LEVEL:
                        case AnalysisItemTypes.QUARTER_OF_YEAR:
                            dateAxis.dataUnits = "months";
                            break;
                        case AnalysisItemTypes.DAY_LEVEL:
                            dateAxis.dataUnits = "days";
                            break;
                        case AnalysisItemTypes.HOUR_LEVEL:
                            dateAxis.dataUnits = "hours";
                            break;
                        case AnalysisItemTypes.MINUTE_LEVEL:
                            dateAxis.dataUnits = "minutes";
                            break;
                    }
                    dateAxis.displayName = dateDimension.display;
                    if (additionalProperties["xAxisMinimum"] != null) {
                        dateAxis.minimum = additionalProperties["xAxisMinimum"];
                    }
                    if (additionalProperties["xAxisMaximum"] != null) {
                        dateAxis.maximum = additionalProperties["xAxisMaximum"];
                    }
                    xAxis = dateAxis;
                } else {
                    var categoryAxis:CategoryAxis = new CategoryAxis();
                    categoryAxis.displayName = xAxisDimension.display;
                    categoryAxis.dataProvider = categories;
                    xAxis = categoryAxis;
                }

                lineChart.horizontalAxis = xAxis;

                var mySeries:Array = new Array();
                var uniques:ArrayCollection = new ArrayCollection();

                var seriesData:Object = new Object();
                lineChartDef.populateData(dataSet, seriesData, uniques);
                for (var i:int = 0; i < uniques.length; i++) {
                    var key:String = uniques.getItemAt(i) as String;
                    var uniqueLineSeries:LineSeries3D = new LineSeries3D();
                    uniqueLineSeries.xField = lineChartDef.xaxis.qualifiedName();
                    if (key == null || key == "") {
                        key = "[ No Value ]";
                    }
                    uniqueLineSeries.yField = key;
                    uniqueLineSeries.displayName = key;
                    uniqueLineSeries.dataProvider = seriesData[key];
                    mySeries.push(uniqueLineSeries);
                }

                lineChart.series = mySeries;


                legend.dataProvider = lineChart;
                legend.direction = "vertical";
                hbox.addChildAt(lineChart, 0);
            } else {
                if (lineChartDef.measure != null && lineChartDef.xaxis != null && lineChartDef.yaxis != null) stackIndex = 2;
                else stackIndex = 1;
                if (lineChart != null) {
                    lineChart.visible = false;
                    legend.visible = false;
                }
            }
        }


        public function createFilterRawData():FilterRawData {
            return null;
        }

        [Bindable]
        private var stackIndex:int = 0;

        public function updateExportMetadata():void {
        }

        public function preserveValues():Boolean {
            return false;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
        }]]></mx:Script>
    <mx:Canvas width="100%" height="100%">
        <mx:HBox width="100%" height="100%" id="hbox"/>

        <mx:ViewStack width="100%" height="100%" selectedIndex="{stackIndex}" creationPolicy="all" mouseChildren="false"
                      mouseEnabled="false">
            <mx:Canvas width="100%" height="100%"/>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000"
                    backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center"
                        verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22"
                                 text="You haven't set up a report yet. Drag two Groupings and one Measure into the labeled areas above the canvas to create a report."
                                 borderStyle="none" backgroundAlpha="0" width="500" height="95" editable="false" selectable="false" focusEnabled="false"/>
                </mx:Box>
            </mx:Box>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000"
                    backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center"
                        verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22"
                                 text="We didn't find any data for the fields and filters that you specified in the report."
                                 borderStyle="none" backgroundAlpha="0" width="500" height="65" editable="false" selectable="false" focusEnabled="false"/>
                </mx:Box>
            </mx:Box>
        </mx:ViewStack>
    </mx:Canvas>
</mx:Module>