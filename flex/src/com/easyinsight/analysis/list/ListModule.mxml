<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:analysis="com.easyinsight.analysis.*" implements="com.easyinsight.analysis.ISelectableReportRenderer"
        width="100%" height="100%" creationComplete="globalListeners()">
    <mx:states>
        <mx:State name="keywordSearch">
            <mx:AddChild relativeTo="{topBar}">
                <mx:HBox width="100%">
                    <mx:TextInput id="keywordInput" change="onKeyword(event)" creationComplete="onKeywordState(event)"/>
                    <mx:Button icon="{scrollDownIcon}" click="scrollGridDown()" toolTip="Next Occurrence"/>
                    <mx:Button icon="{scrollUpIcon}" click="scrollGridUp()" toolTip="Previous Occurrence"/>
                    <mx:CheckBox id="keywordCaseSensitive" label="Case sensitive"/>
                    <mx:CheckBox id="keywordWholeWords" label="Match whole words only"/>
                    <mx:Spacer width="100%"/>
                    <mx:Button icon="{deleteIcon}" toolTip="Close Search Bar" click="hits = new ArrayCollection(); currentState=''"/>
                </mx:HBox>
            </mx:AddChild>
        </mx:State>
        <mx:State name="lookupTable">
            <mx:AddChild relativeTo="{topBar}">
                <mx:HBox width="100%" horizontalAlign="center">
                    <mx:Label text="You have unsaved changes to lookup tables."/>
                    <mx:Button label="Save Lookup Tables" click="updateLookupTables()"/>
                </mx:HBox>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AnalysisCellRenderer;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.DateValue;
        import com.easyinsight.analysis.EmptyValue;
        import com.easyinsight.analysis.FeedMetadata;
        import com.easyinsight.analysis.ListViewHeaderRenderer;
        import com.easyinsight.analysis.LookupTableCellRenderer;
        import com.easyinsight.analysis.LookupTableDateCellRenderer;
        import com.easyinsight.analysis.NumericValue;
        import com.easyinsight.analysis.OrderedSortField;
        import com.easyinsight.analysis.ReportRendererEvent;
        import com.easyinsight.analysis.SortFunctionFactory;
        import com.easyinsight.analysis.StringValue;
        import com.easyinsight.analysis.TextCellRenderer;
        import com.easyinsight.analysis.Value;
        import com.easyinsight.etl.LookupPair;
        import com.easyinsight.etl.LookupTable;
        import com.easyinsight.filtering.FilterRawData;

        import mx.collections.ArrayCollection;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.controls.AdvancedDataGrid;
        import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
        import mx.controls.advancedDataGridClasses.SortInfo;
        import mx.events.DragEvent;
        import mx.events.FlexEvent;
        import mx.events.IndexChangedEvent;
        import mx.managers.DragManager;
        import mx.managers.IFocusManagerComponent;

        private function dragCompleteHandler(event:DragEvent):void {
            event.preventDefault();
        }

        private var lookupTables:ArrayCollection;

        public function set feedMetadata(feedMetadata:FeedMetadata):void {
            lookupTables = feedMetadata.lookupTables;
        }

        [Bindable]
        [Embed(source="../../../../../assets/navigate_cross.png")]
        public var deleteIcon:Class;

        [Bindable]
        [Embed(source="../../../../../assets/arrow2_down_blue.png")]
        public var scrollDownIcon:Class;

        [Bindable]
        [Embed(source="../../../../../assets/arrow2_up_blue.png")]
        public var scrollUpIcon:Class;

        [Bindable]
        private var myDataSet:ArrayCollection = new ArrayCollection();
        private var columnMap:Object = new Object();

        private var listDef:ListDefinition;

        private var hits:ArrayCollection = new ArrayCollection();

        private function rowNumber(item:Object, iCol:int):String {
            var index:int = dataGrid.dataProvider.getItemIndex(item) + 1;
            return String(index);
        }

        private function onKeywordState(event:FlexEvent):void {
            focusManager.setFocus(event.currentTarget as IFocusManagerComponent);
        }

        private function onKeyword(event:Event):void {
            var keyword:String = keywordInput.text;
            if (keyword.length > 1) {
                if (!keywordCaseSensitive.selected) {
                    keyword = keyword.toLowerCase();
                }
                var hitColl:ArrayCollection = new ArrayCollection();
                for each (var obj:Object in myDataSet) {
                    var match:Boolean = false;
                    for each (var valueObj:Object in obj) {
                        var objVal:String = String(valueObj);
                        if (!keywordCaseSensitive.selected) {
                            objVal = objVal.toLowerCase();
                        }
                        if (keywordWholeWords.selected) {
                            match = objVal == keyword;
                        } else {
                            match = objVal.indexOf(keyword) != -1;
                        }
                        if (match) {
                            break;
                        }
                    }
                    if (match) {
                        hitColl.addItem(obj);
                    }
                }
                hits = hitColl;
            } else {
                hits = new ArrayCollection();
            }
            dataGrid.invalidateDisplayList();
            dataGrid.validateNow();
            scrollFirstCurrent();
        }

        private function scrollGridUp():void {
            var currentVerticalPosition:int = dataGrid.verticalScrollPosition;
            for (var i:int = hits.length - 1; i >= 0; i--) {
                var obj:Object = hits.getItemAt(i);
                var index:int = myDataSet.getItemIndex(obj);
                if (index < currentVerticalPosition && index >= 0) {
                    dataGrid.verticalScrollPosition = index;
                    break;
                }
            }
        }

        private function scrollFirstCurrent():void {
            var currentVerticalPosition:int = dataGrid.verticalScrollPosition;
            for each (var obj:Object in hits) {
                var index:int = myDataSet.getItemIndex(obj);
                if (index == currentVerticalPosition) {
                    break;
                } else if (index > currentVerticalPosition && index <= dataGrid.maxVerticalScrollPosition) {
                    dataGrid.verticalScrollPosition = index;
                    break;
                }
            }
        }

        private function scrollGridDown():void {
            var currentVerticalPosition:int = dataGrid.verticalScrollPosition;
            for each (var obj:Object in hits) {
                var index:int = myDataSet.getItemIndex(obj);
                if (index > currentVerticalPosition && index <= dataGrid.maxVerticalScrollPosition) {
                    dataGrid.verticalScrollPosition = index;
                    break;
                }
            }
        }

        private var summaryRows:ArrayCollection = new ArrayCollection();

        private function rowColor(item:Object, color:uint):uint {
            if (hits.getItemIndex(item) > -1)
                return 0xEBEF80;
            if (summaryRows.getItemIndex(item) > -1) {
                return listDef.summaryRowBackgroundColor;
            }
            return color;
        }

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {
            var def:ListDefinition = analysisDefinition as ListDefinition;
            dataGrid.setStyle("fontFamily", def.fontName);
            dataGrid.setStyle("fontSize", def.fontSize);
            dataGrid.setStyle("color", def.textColor);
            dataGrid.setStyle("alternatingItemColors", [ def.rowColor1, def.rowColor2 ]);
            dataGrid.setStyle("headerColors", [ def.headerColor1, def.headerColor2 ]);
            if (def.fixedWidth > 0) {
                dataGrid.width = def.fixedWidth;
            } else {
                dataGrid.width = 0;
                dataGrid.percentWidth = 100;
            }
            this.listDef = def;
            if (dataSet.length > 0) {
                stackIndex = 0;
                summaryRows = new ArrayCollection();
                if (def.summaryTotal) {
                    var summaryRow:Object = new Object();
                    for each (var defItem:AnalysisItem in def.columns) {
                        if (defItem.hasType(AnalysisItemTypes.MEASURE)) {
                            var value:Value = additionalProperties["summary" + defItem.qualifiedName()];
                            value.summary = true;
                            summaryRow[defItem.qualifiedName()] = value;
                        } else {
                            var emptyValue:EmptyValue = new EmptyValue();
                            emptyValue.summary = true;
                            summaryRow[defItem.qualifiedName()] = emptyValue;
                        }
                    }
                    summaryRows.addItem(summaryRow);
                    dataSet.addItem(summaryRow);
                }
                if (firstLoad) {
                    var existingSort:Sort = new Sort();
                    var sortFields:ArrayCollection = new ArrayCollection();
                    for (var l:int = 0; l < def.columns.length; l++) {
                        var sortHeader:AnalysisItem = def.columns.getItemAt(l) as AnalysisItem;
                        if (sortHeader.sort > 0) {
                            var sortField:OrderedSortField = new OrderedSortField();
                            sortField.descending = sortHeader.sort == 2;
                            sortField.compareFunction = SortFunctionFactory.createSortFunction(sortHeader, sortField.descending);
                            sortField.name = sortHeader.qualifiedName();
                            sortField.order = sortHeader.sortSequence;
                            sortFields.addItem(sortField);
                        }
                    }
                    var sortOfSorts:Sort = new Sort();
                    sortOfSorts.fields = [ new SortField("order", false, false) ];
                    sortFields.sort = sortOfSorts;
                    sortFields.refresh();
                    existingSort.fields = sortFields.toArray();
                    myDataSet = dataSet;
                    myDataSet.sort = existingSort;
                    myDataSet.refresh();
                    firstLoad = false;
                } else {
                    var sort:Sort = myDataSet.sort;
                    myDataSet = dataSet;
                    myDataSet.sort = sort;
                    myDataSet.refresh();
                }

                var columns:Array = new Array();
                var initColumn:int = 0;
                if (def.showLineNumbers) {
                    var lineNumberColumn:AdvancedDataGridColumn = new AdvancedDataGridColumn();
                    lineNumberColumn.width = 30;
                    lineNumberColumn.sortable = false;
                    lineNumberColumn.labelFunction = rowNumber;
                    columns[0] = lineNumberColumn;
                    initColumn++;
                }
                var newColumns:int = 0;
                for each (var newHeader:AnalysisItem in def.columns) {
                    var exists:Boolean = false;
                    var qualifiedName:String = newHeader.qualifiedName();
                    for each (var existingColumn:AdvancedDataGridColumn in dataGrid.columns) {
                        if (existingColumn.dataField == qualifiedName) {
                            exists = true;
                            break;
                        }
                    }
                    if (!exists) {
                        newColumns++;
                    }
                }

                var explicitWidth:Boolean = true;
                for each (var item:AnalysisItem in def.columns) {
                    if (item.width == 0) {
                        explicitWidth = false;
                    }
                }

                // we need to reduce the size of existing columns

                var variableHeight:Boolean = false;

                activeLookupTables = new ArrayCollection();
                pairMaps = new ArrayCollection();

                for (var k:int = 0; k < def.columns.length; k++) {
                    var myHeader:AnalysisItem = def.columns.getItemAt(k) as AnalysisItem;
                    var myLookupTable:LookupTable = null;
                    if (lookupEditing) {
                        if (myHeader.lookupTableID > 0) {
                            for each (var lookupTable:LookupTable in lookupTables) {
                                if (lookupTable.lookupTableID == myHeader.lookupTableID) {
                                    myLookupTable = lookupTable;
                                    break;
                                }
                            }
                        }
                        if (myLookupTable != null) {
                            var found:Boolean = false;
                            for each (var lookupTest:AnalysisItem in def.columns) {
                                if (lookupTest.qualifiedName() == myLookupTable.sourceField.qualifiedName()) {
                                    found = true;
                                    break;
                                }
                            }
                            if (!found) {
                                myLookupTable = null;
                            }
                        }

                        if (myLookupTable != null) {
                            var pairMap:Object = new Object();
                            for each (var pair:LookupPair in lookupTable.lookupPairs) {
                                pairMap[pair.sourceValue.getValue()] = pair.targetValue;
                            }
                            activeLookupTables.addItem(myLookupTable);
                            pairMaps.addItem(pairMap);
                        }
                    }
                    columnMap[myHeader.qualifiedName()] = myHeader;
                    var columnHeaderObject:AdvancedDataGridColumn = new AdvancedDataGridColumn();
                    if (explicitWidth) columnHeaderObject.width = myHeader.width;
                    columnHeaderObject.sortCompareFunction = SortFunctionFactory.createSortFunction(myHeader, columnHeaderObject.sortDescending, columnHeaderObject);
                    columnHeaderObject.headerText = myHeader.display;
                    columnHeaderObject.dataField = myHeader.qualifiedName();
                    var headerFactory:ClassFactory = new ClassFactory(ListViewHeaderRenderer);
                    headerFactory.properties = { eiColor: def.headerTextColor };
                    columnHeaderObject.headerRenderer = headerFactory;
                    var factory:ClassFactory = new ClassFactory(getCellRendererClass(myHeader, myLookupTable));
                    if (myHeader.hasType(AnalysisItemTypes.TEXT)) {
                        variableHeight = true;
                    }
                    if (myLookupTable == null) {
                        factory.properties = { analysisItem: myHeader, selectionEnabled: selectionEnabled, report: def };
                    } else {
                        factory.properties = { analysisItem: myHeader, lookupTable: myLookupTable, pairMap: pairMap };
                    }
                    columnHeaderObject.itemRenderer = factory;
                    columns[k + initColumn] = columnHeaderObject;
                }
                dataGrid.variableRowHeight = variableHeight;
                dataGrid.columns = columns;
                dataGrid.rowCount = dataSet.length;
            } else {
                if (def.columns.length > 0) stackIndex = 2;
                else stackIndex = 1;
                dataGrid.columns = [];
                myDataSet = new ArrayCollection();
            }

        }

        private function updateLookupTables():void {
            for (var i:int = 0; i < activeLookupTables.length; i++) {
                var activeLookupTable:LookupTable = activeLookupTables.getItemAt(i) as LookupTable;
                var pairMap:Object = pairMaps.getItemAt(i) as Object;
                var pairs:ArrayCollection = new ArrayCollection();
                for (var key:String in pairMap) {
                    var result:Object = pairMap[key];
                    var lookupPair:LookupPair = new LookupPair();
                    lookupPair.sourceValue = new StringValue();
                    StringValue(lookupPair.sourceValue).value = key;
                    var resultValue:Value;
                    if (result == null) {
                        continue;
                    }
                    if (result is Value) {
                        resultValue = result as Value;
                    } else if (result is String) {
                        resultValue = new StringValue();
                        StringValue(resultValue).value = String(result);
                    } else if (result is Date) {
                        resultValue = new DateValue();
                        DateValue(resultValue).date = result as Date;
                    } else {
                        resultValue = new NumericValue();
                        NumericValue(resultValue).value = Number(result);
                    }
                    lookupPair.targetValue = resultValue;
                    pairs.addItem(lookupPair);
                }
                activeLookupTable.lookupPairs = pairs;
                feedService.updateLookupTable.send(activeLookupTable);
            }
            currentState = "";
        }

        private var activeLookupTables:ArrayCollection;

        private var pairMaps:ArrayCollection;

        private function getCellRendererClass(item:AnalysisItem, lookupTable:LookupTable):Class {
            if (lookupTable != null) {
                if (lookupTable.targetField.hasType(AnalysisItemTypes.DATE)) {
                    return LookupTableDateCellRenderer;
                } else {
                    return LookupTableCellRenderer;
                }
            }
            if (item.hasType(AnalysisItemTypes.TEXT)) {
                return TextCellRenderer;
            } else {
                return AnalysisCellRenderer;
            }
        }

        private var firstLoad:Boolean = true;

        private function globalListeners():void {
            dataGrid.setStyle("borderThickness", 1);
            dataGrid.setStyle("borderStyle", "solid");
            dataGrid.setStyle("borderColor", 0xB7BABC);
            addEventListener(KeyboardEvent.KEY_DOWN, stageKeyDown);
            addEventListener(LookupTableChangeEvent.LOOKUP_TABLE_CHANGE, onLookupTableChange);
        }

        private function onLookupTableChange(event:LookupTableChangeEvent):void {
            currentState = "lookupTable";
        }

        private function stageKeyDown(event:KeyboardEvent):void {
            if (event.ctrlKey && event.keyCode == 70) {
                currentState = "keywordSearch";
                if (keywordInput != null) {
                    focusManager.setFocus(keywordInput);
                }
            } else if (event.keyCode == Keyboard.ESCAPE) {
                currentState = "";
            } else if (currentState == "keywordSearch" && event.keyCode == Keyboard.F3) {
                if (event.shiftKey) {
                    scrollGridUp();
                } else {
                    scrollGridDown();
                }
            }
        }

        private function keyboardHandler(event:KeyboardEvent):void {
            if (event.ctrlKey && event.keyCode == 67) {
                var selectedObjects:Array = dataGrid.selectedCells;
                if (selectedObjects.length > 0) {
                    var selectionString:String = "";
                    for (var i:int = 0; i < selectedObjects.length; i++) {
                        var selectedObject:Object = selectedObjects[i];
                        var data:Object = dataGrid.dataProvider[selectedObject.rowIndex];
                        var dataField:String = dataGrid.columns[selectedObject.columnIndex].dataField;

                        var dataString:String = data[dataField];
                        selectionString = selectionString + dataString;
                        if (i < selectedObjects.length - 1) {
                            selectionString += ",";
                        }
                        System.setClipboard(selectionString);
                    }
                }
            }
        }

        private function dragEnterHandler(event:DragEvent):void {
            var newItem:AnalysisItemWrapper = event.dragSource.dataForFormat("treeDataGridItems")[0];
            if (newItem.isAnalysisItem()) {
                var targetGrid:AdvancedDataGrid = AdvancedDataGrid(event.currentTarget);
                dataGrid.setStyle("borderColor", "green");
                DragManager.acceptDragDrop(targetGrid);
            }
        }

        private function dragDropHandler(event:DragEvent):void {
            event.preventDefault();
            var newItem:AnalysisItemWrapper = event.dragSource.dataForFormat("treeDataGridItems")[0];
            if (newItem.isAnalysisItem()) {
                var targetGrid:AdvancedDataGrid = AdvancedDataGrid(event.currentTarget);
                dataGrid.setStyle("borderColor", 0xB7BABC);
                dispatchEvent(new ReportRendererEvent(ReportRendererEvent.ADD_ITEM, newItem.analysisItem));
            }
        }

        protected function dragOverHandler(event:DragEvent):void {
            DragManager.showFeedback(DragManager.MOVE);
        }

        protected function dragExitHandler(event:DragEvent):void {
            dataGrid.setStyle("borderColor", 0xB7BABC);
        }

        private function headersShifted(event:IndexChangedEvent):void {
            var keys:Array = [];
            for (var i:int = 0; i < dataGrid.columns.length; i++) {
                var column:AdvancedDataGridColumn = dataGrid.columns[i];
                keys[i] = column.dataField;
            }
            dispatchEvent(new ColumnReorderEvent(keys));
        }

        public function createFilterRawData():FilterRawData {
            var filterRawData:FilterRawData = new FilterRawData();
            var selectedObjects:Array = dataGrid.selectedCells;
            for (var i:int = 0; i < selectedObjects.length; i++) {
                var selectedObject:Object = selectedObjects[i];
                var data:Object = dataGrid.dataProvider[selectedObject.rowIndex];
                var dataField:String = dataGrid.columns[selectedObject.columnIndex].dataField;

                var dataString:Object = String(data[dataField]);

                var analysisItem:AnalysisItem = columnMap[dataField];
                filterRawData.addPair(analysisItem, dataString);
            }
            return filterRawData;
        }

        public function updateExportMetadata():void {
            if (listDef != null) {
                for (var i:int = 0; i < listDef.columns.length; i++) {
                    var item:AnalysisItem = listDef.columns.getItemAt(i) as AnalysisItem;
                    var columnIndex:int = findColumnIndex(item.qualifiedName());
                    item.itemPosition = columnIndex;
                    var column:AdvancedDataGridColumn = dataGrid.columns[columnIndex];
                    if (column != null) {
                        item.width = column.width;
                        var sortInfo:SortInfo = dataGrid.getFieldSortInfo(column);
                        if (sortInfo != null && sortInfo.status == SortInfo.ACTUALSORT) {
                            item.sortSequence = sortInfo.sequenceNumber;
                            if (sortInfo.descending) {
                                item.sort = 2;
                            } else {
                                item.sort = 1;
                            }
                        } else {
                            item.sort = 0;
                        }
                    }
                }
            }
        }

        private function findColumnIndex(qualifiedName:String):int {
            for (var i:int = 0; i < dataGrid.columns.length; i++) {
                var column:AdvancedDataGridColumn = dataGrid.columns[i];
                if (column.dataField == qualifiedName) {
                    return i;
                }
            }
            return -1;
        }

        public function preserveValues():Boolean {
            return true;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
            if (event is EnableKeywordEvent) {
                currentState = "keywordSearch";
            } else if (event is EnableLookupEditingEvent) {
                lookupEditing = !lookupEditing;
            }
        }

        private var lookupEditing:Boolean;

        [Bindable]
        private var stackIndex:int = 0;

        public function selectable():Boolean {
            return true;
        }

        private var _selectionEnabled:Boolean;


        [Bindable(event="selectionEnabledChanged")]
        public function get selectionEnabled():Boolean {
            return _selectionEnabled;
        }

        public function set selectionEnabled(value:Boolean):void {
            if (_selectionEnabled == value) return;
            _selectionEnabled = value;
            dispatchEvent(new Event("selectionEnabledChanged"));
            if (_selectionEnabled) {
                gridSelectionMode = "multipleCells";
            } else {
                gridSelectionMode = "singleCell";
            }
            if (listDef != null) {
                for (var i:int = 0; i < listDef.columns.length; i++) {
                    var column:AdvancedDataGridColumn = dataGrid.columns[i];
                    var field:AnalysisItem = listDef.columns.getItemAt(i) as AnalysisItem;
                    var factory:ClassFactory = new ClassFactory(getCellRendererClass(field, null));
                    factory.properties = { analysisItem: field, selectionEnabled: selectionEnabled };
                    column.itemRenderer = factory;
                }
            }
        }

        private var _gridSelectionMode:String = "singleCell";

        [Bindable(event="gridSelectionModeChanged")]
        public function get gridSelectionMode():String {
            return _gridSelectionMode;
        }

        public function set gridSelectionMode(value:String):void {
            if (_gridSelectionMode == value) return;
            _gridSelectionMode = value;
            dispatchEvent(new Event("gridSelectionModeChanged"));
        }

        public function clearContents():void {
        }]]></mx:Script>
    <mx:RemoteObject id="feedService" destination="feeds">
        <mx:method name="updateLookupTable"/>
    </mx:RemoteObject>
    <mx:Canvas width="100%" height="100%">
        <mx:VBox width="100%" height="100%">
            <mx:Box id="topBar" width="100%"/>
            <analysis:ListViewGrid id="dataGrid" dataProvider="{myDataSet}" width="100%" height="100%"
                               dragEnter="dragEnterHandler(event)" dragComplete="dragCompleteHandler(event)"
                               dragDrop="dragDropHandler(event)" selectionMode="{gridSelectionMode}"
                               keyUp="keyboardHandler(event)" rowColorFunction="rowColor"
                               headerShift="headersShifted(event)" fontSize="12" dragExit="dragExitHandler(event)"
                               dragOver="dragOverHandler(event)" fontFamily="Tahoma"/>
        </mx:VBox>
        <mx:ViewStack width="100%" height="100%" selectedIndex="{stackIndex}" creationPolicy="all" mouseChildren="false" mouseEnabled="false">
            <mx:Canvas width="100%" height="100%"/>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000" backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center" verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22" text="You haven't set up a report yet. You can drag the fields from the left into this canvas or into the drop areas above to get started!"
                        borderStyle="none" backgroundAlpha="0" width="500" height="95" editable="false" selectable="false" focusEnabled="false"/>
                </mx:Box>
            </mx:Box>
            <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle" backgroundColor="#000000" backgroundAlpha=".1">
                <mx:Box borderStyle="solid" borderThickness="1" cornerRadius="10" horizontalAlign="center" verticalAlign="middle"
                        backgroundColor="#FFFFFF" paddingBottom="15" paddingLeft="15" paddingRight="15" paddingTop="15">
                    <mx:TextArea fontSize="22" text="We didn't find any data for the fields and filters that you specified in the report."
                        borderStyle="none" backgroundAlpha="0" width="500" height="65" editable="false" selectable="false" focusEnabled="false"/>
                </mx:Box>
            </mx:Box>
        </mx:ViewStack>
    </mx:Canvas>

</mx:Module>