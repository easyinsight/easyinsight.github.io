<?xml version="1.0" ?>
<mx:Module xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:analysis="com.easyinsight.analysis.*"
           implements="com.easyinsight.analysis.IReportRenderer" width="100%" height="100%"
           creationComplete="setupListeners()">
    <mx:states>
        <mx:State name="lookupTable">
            <mx:AddChild relativeTo="{topBar}">
                <mx:HBox width="100%" horizontalAlign="center">
                    <mx:Label text="You have unsaved changes to lookup tables."/>
                    <mx:Button label="Save Lookup Tables" click="updateLookupTables()"/>
                </mx:HBox>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import com.easyinsight.analysis.AggregationTypes;
        import com.easyinsight.analysis.AnalysisCellRenderer;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisHierarchyItem;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemTypes;
        import com.easyinsight.analysis.AnalysisMeasure;
        import com.easyinsight.analysis.CustomChangeEvent;
        import com.easyinsight.analysis.DateValue;
        import com.easyinsight.analysis.DerivedAnalysisDimension;
        import com.easyinsight.analysis.FeedMetadata;
        import com.easyinsight.analysis.HierarchyLevel;
        import com.easyinsight.analysis.ListViewHeaderRenderer;
        import com.easyinsight.analysis.LookupTableCellRenderer;
        import com.easyinsight.analysis.LookupTableDateCellRenderer;
        import com.easyinsight.analysis.NumericValue;
        import com.easyinsight.analysis.SortFunctionFactory;
        import com.easyinsight.analysis.StringValue;
        import com.easyinsight.analysis.SummaryCellRenderer;
        import com.easyinsight.analysis.TextCellRenderer;
        import com.easyinsight.analysis.TextReportFieldExtension;
        import com.easyinsight.analysis.TreeRow;
        import com.easyinsight.analysis.Value;
        import com.easyinsight.analysis.list.EnableLookupEditingEvent;
        import com.easyinsight.analysis.list.LookupTableChangeEvent;
        import com.easyinsight.etl.LookupPair;
        import com.easyinsight.etl.LookupTable;


        import mx.collections.ArrayCollection;
        import mx.collections.Grouping;
        import mx.collections.GroupingCollection;
        import mx.collections.GroupingField;
        import mx.collections.HierarchicalCollectionView;
        import mx.collections.HierarchicalData;
        import mx.collections.ICollectionView;
        import mx.collections.Sort;
        import mx.collections.SortField;
        import mx.collections.SummaryField;
        import mx.collections.SummaryRow;
        import mx.controls.advancedDataGridClasses.AdvancedDataGridColumn;
        import mx.controls.advancedDataGridClasses.AdvancedDataGridRendererProvider;
        import mx.controls.advancedDataGridClasses.SortInfo;
        import mx.formatters.Formatter;
        import mx.utils.ObjectUtil;

        [Bindable]
        private var myData:HierarchicalCollectionView;

        private var lookupTables:ArrayCollection;

        public function set feedMetadata(feedMetadata:FeedMetadata):void {
            lookupTables = feedMetadata.lookupTables;
        }

        private function setupListeners():void {
            addEventListener(LookupTableChangeEvent.LOOKUP_TABLE_CHANGE, onLookupTableChange);
        }

        private function onLookupTableChange(event:LookupTableChangeEvent):void {
            currentState = "lookupTable";
        }

        /*private var openState:Object = new Object();

        private function preserveOpenState():void {
            if (existingData != null) {
                openState = new Object();
                for each (var obj:Object in existingData) {
                    openState[String(obj.id)] = uploadGrid.isItemOpen(obj);
                    if (uploadGrid.isItemOpen(obj)) {
                    }
                }
            }
        }*/

        private var treeDef:TreeDefinition;

        private var fieldLength:int;

        [Embed(source="../../../../../assets/empty.png")]
        private var empty:Class;

        private function groupIconFunction(item:Object,depth:int):Class {
            return empty;
        }

        private function iconFunction(item:Object):Class {
            return empty;
        }

        public function renderReport(dataSet:ArrayCollection, analysisDefinition:AnalysisDefinition, clientProcessorMap:Object, additionalProperties:Object):void {
            var treeDefinition:TreeDefinition = analysisDefinition as TreeDefinition;
            if (dataSet.length > 0) {
                stackIndex = 0;

                activeLookupTables = new ArrayCollection();
                pairMaps = new ArrayCollection();

                var fieldsChanged:Boolean = false;
                if (treeDef != null) {
                    fieldsChanged = fieldLength != treeDefinition.items.length;
                }
                this.fieldLength = treeDefinition.items.length;
                //coreGrid.setStyle("fontFamily", "Lucida Grande");
                coreGrid.setStyle("fontSize", treeDefinition.fontSize);
                coreGrid.setStyle("color", treeDefinition.textColor);
                coreGrid.setStyle("alternatingItemColors", [ treeDefinition.rowColor1, treeDefinition.rowColor2 ]);
                coreGrid.setStyle("headerColors", [ treeDefinition.headerColor1, treeDefinition.headerColor2 ]);
                if (treeDefinition.autoExpandAll) {
                    coreGrid.groupIconFunction = groupIconFunction;
                    coreGrid.iconFunction = iconFunction;
                    coreGrid.displayDisclosureIcon = false;
                    coreGrid.displayItemsExpanded = true;
                    coreGrid.setStyle("depthColors", [0xBBBBBB, 0xFFFFFF, 0xFFFFFF, 0xFFFFFF, 0xFFFFFF, 0xFFFFFF, 0xFFFFFF]);
                }
                this.treeDef = treeDefinition;
                var explicitWidth:Boolean = true;
                for each (var treeItem:AnalysisItem in treeDef.getFields()) {
                    if (treeItem.width == 0) {
                        explicitWidth = false;
                    }
                }

                var hierarchy:AnalysisHierarchyItem = treeDefinition.hierarchy as AnalysisHierarchyItem;
                var hierData:HierarchicalData = new HierarchicalData();
                hierData.source = dataSet;
                var columns:Array = [];
                var analysisItems:Array = [];
                var hierDataView:HierarchicalCollectionView = new HierarchicalCollectionView(hierData);
                if (myData != null) {
                    hierDataView.sort = myData.sort;
                }
                for each (var item:AnalysisItem in treeDefinition.items) {
                    analysisItems.push(item);
                }
                var hierarchyColumn:AdvancedDataGridColumn = new AdvancedDataGridColumn();
                var hierarchyFactory:ClassFactory = new ClassFactory(CustomTreeRenderer);
                hierarchyFactory.properties = { analysisItem: hierarchy, report: treeDef };
                coreGrid.groupItemRenderer = hierarchyFactory;
                hierarchyColumn.headerText = hierarchy.display;
                hierarchyColumn.sortCompareFunction = sortCompare;
                var rendererProviders:Array = [];
                hierarchyColumn.labelFunction = function(item:Object, column:AdvancedDataGridColumn):String {
                    var treeRow:TreeRow = item as TreeRow;
                    if (treeRow.groupingField != null) {
                        var formatter:Formatter = treeRow.groupingField.getFormatter();
                        return formatter.format(treeRow.groupingColumn);
                    } else {
                        return "";
                    }
                };
                hierarchyColumn.dataField = "groupingColumn";
                var existingHierarchyIndex:int = findHierarchy(hierarchy.hierarchyLevels.getItemAt(hierarchy.hierarchyLevels.length - 1).analysisItem.qualifiedName());
                if (hierarchy.reportFieldExtension != null && hierarchy.reportFieldExtension is TextReportFieldExtension &&
                        TextReportFieldExtension(hierarchy.reportFieldExtension).fixedWidth > 0) {
                    var ext1:TextReportFieldExtension = hierarchy.reportFieldExtension as TextReportFieldExtension;
                    hierarchyColumn.width = ext1.fixedWidth;
                } else if (!fieldsChanged && existingHierarchyIndex != -1) {
                    var existingHierarchyColumn:AdvancedDataGridColumn = coreGrid.columns[0];
                    hierarchyColumn.width = existingHierarchyColumn.width;
                } else if (explicitWidth) {
                    hierarchyColumn.width = hierarchy.width;
                }
                if (hierarchy.reportFieldExtension != null && hierarchy.reportFieldExtension is TextReportFieldExtension) {
                    hierarchyColumn.sortable = TextReportFieldExtension(hierarchy.reportFieldExtension).sortable;
                }
                var hierarchyHeaderFactory:ClassFactory = new ClassFactory(ListViewHeaderRenderer);
                hierarchyHeaderFactory.properties = { eiColor: treeDefinition.headerTextColor };
                hierarchyColumn.headerRenderer = hierarchyHeaderFactory;
                columns[0] = hierarchyColumn;
                for (var k:int = 0; k < analysisItems.length; k++) {
                    var myHeader:AnalysisItem = analysisItems[k] as AnalysisItem;

                    var myLookupTable:LookupTable = null;
                    if (lookupEditing) {
                        if (myHeader.lookupTableID > 0) {
                            for each (var lookupTable:LookupTable in lookupTables) {
                                if (lookupTable.lookupTableID == myHeader.lookupTableID) {
                                    myLookupTable = lookupTable;
                                    break;
                                }
                            }
                        }
                        if (myLookupTable != null) {
                            var found:Boolean = false;
                            for each (var lookupTest:AnalysisItem in treeDef.items) {
                                if (lookupTest.key.toBaseKey().keyID == myLookupTable.sourceField.key.toBaseKey().keyID) {
                                    found = true;
                                    break;
                                }
                            }
                            if (!found) {
                                for each (var hierLevel:HierarchyLevel in hierarchy.hierarchyLevels) {
                                    if (hierLevel.analysisItem.key.toBaseKey().keyID == myLookupTable.sourceField.key.toBaseKey().keyID) {
                                        found = true;
                                        break;
                                    }
                                }
                                if (!found) {
                                    myLookupTable = null;
                                }
                            }
                        }

                        if (myLookupTable != null) {
                            var pairMap:Object = new Object();
                            for each (var pair:LookupPair in lookupTable.lookupPairs) {
                                pairMap[pair.sourceValue.getValue()] = pair.targetValue;
                            }
                            activeLookupTables.addItem(myLookupTable);
                            pairMaps.addItem(pairMap);
                        }
                    }

                    var columnHeaderObject:AdvancedDataGridColumn = new AdvancedDataGridColumn();
                    var existingIndex:int = findColumnIndex(myHeader.qualifiedName());
                    if (!fieldsChanged && existingIndex != -1) {
                        var existingColumn:AdvancedDataGridColumn = coreGrid.columns[existingIndex];
                        columnHeaderObject.width = existingColumn.width;
                        columnHeaderObject.sortDescending = existingColumn.sortDescending;
                    } else if (explicitWidth) {
                        columnHeaderObject.width = myHeader.width;
                    }
                    columnHeaderObject.sortCompareFunction = SortFunctionFactory.createSortFunction(myHeader, columnHeaderObject.sortDescending);
                    columnHeaderObject.headerText = myHeader.display;
                    columnHeaderObject.dataField = "groupingColumn";
                    var headerFactory:ClassFactory = new ClassFactory(ListViewHeaderRenderer);
                    headerFactory.properties = { eiColor: treeDefinition.headerTextColor };
                    columnHeaderObject.headerRenderer = headerFactory;

                    var factory:ClassFactory = new ClassFactory(getCellRendererClass(myHeader, myLookupTable));
                    var ext:TextReportFieldExtension = myHeader.reportFieldExtension as TextReportFieldExtension;
                    if (ext != null) {
                        if (ext.fixedWidth > 0) {
                            columnHeaderObject.width = ext.fixedWidth;
                            columnHeaderObject.resizable = false;
                        }
                        if (!ext.sortable) {
                            columnHeaderObject.sortable = false;
                        }
                    }
                    if (myLookupTable == null) {
                        factory.properties = { analysisItem: myHeader, report: treeDef };
                    } else {
                        factory.properties = { analysisItem: myHeader, lookupTable: myLookupTable, pairMap: pairMap };
                        var provider:AdvancedDataGridRendererProvider = new AdvancedDataGridRendererProvider();
                        provider.depth = hierarchy.hierarchyLevels.length;
                        provider.column = columnHeaderObject;
                        provider.columnSpan =  1;
                        provider.renderer = factory;
                        rendererProviders.push(provider);
                    }
                    columnHeaderObject.itemRenderer = factory;


                    columns[k + 1] = columnHeaderObject;
                }
                if (rendererProviders.length > 0) {
                    coreGrid.rendererProviders = rendererProviders;
                }

                myData = hierDataView;

                hierDataView.refresh();
                coreGrid.dataProvider = hierDataView;
                coreGrid.columns = columns;
            } else {
                if (treeDefinition.items.length > 0 && treeDefinition.hierarchy != null) stackIndex = 2;
                else stackIndex = 1;
                coreGrid.columns = [];
                myData = new HierarchicalCollectionView();
            }
        }

        private function sortCompare(obj1:Object, obj2:Object):int {
            var treeRow1:TreeRow = obj1 as TreeRow;
            var treeRow2:TreeRow = obj2 as TreeRow;
            var view:ICollectionView = ICollectionView(coreGrid.dataProvider);
            var sort:Sort = view.sort;
            if (sort == null) {
                return ObjectUtil.compare(treeRow1.groupingColumn.getValue(), treeRow2.groupingColumn.getValue());
            }
            var fields:Array = sort.fields;

            if (fields != null && fields.length == 1) {
                var sortField:SortField = fields[0];
                if (sortField.name == "groupingColumn") {
                    var num:int = -1;
                    if (sortField.descending) {
                        num = 1;
                    }
                    if (treeRow1.summaryColumn) {
                        return -num;
                    } else if (treeRow2.summaryColumn) {
                        return num;
                    } else {
                        return ObjectUtil.compare(treeRow1.groupingColumn.getValue(), treeRow2.groupingColumn.getValue());
                    }
                }
            }
            return ObjectUtil.compare(treeRow1.groupingColumn.getValue(), treeRow2.groupingColumn.getValue());


        }

        private function updateLookupTables():void {
            for (var i:int = 0; i < activeLookupTables.length; i++) {
                var activeLookupTable:LookupTable = activeLookupTables.getItemAt(i) as LookupTable;
                var pairMap:Object = pairMaps.getItemAt(i) as Object;
                var pairs:ArrayCollection = new ArrayCollection();
                for (var key:String in pairMap) {
                    var result:Object = pairMap[key];
                    var lookupPair:LookupPair = new LookupPair();
                    lookupPair.sourceValue = new StringValue();
                    StringValue(lookupPair.sourceValue).value = key;
                    var resultValue:Value;
                    if (result == null) {
                        continue;
                    }
                    if (result is Value) {
                        resultValue = result as Value;
                    } else if (result is String) {
                        resultValue = new StringValue();
                        StringValue(resultValue).value = String(result);
                    } else if (result is Date) {
                        resultValue = new DateValue();
                        DateValue(resultValue).date = result as Date;
                    } else {
                        resultValue = new NumericValue();
                        NumericValue(resultValue).value = Number(result);
                    }
                    lookupPair.targetValue = resultValue;
                    pairs.addItem(lookupPair);
                }
                activeLookupTable.lookupPairs = pairs;
                feedService.updateLookupTable.send(activeLookupTable);
            }
            currentState = "";
        }

        private var activeLookupTables:ArrayCollection;

        private var pairMaps:ArrayCollection;

        private static function getCellRendererClass(item:AnalysisItem, lookupTable:LookupTable):Class {
            if (lookupTable != null) {
                if (lookupTable.targetField.hasType(AnalysisItemTypes.DATE)) {
                    return LookupTableDateCellRenderer;
                } else {
                    return LookupTableCellRenderer;
                }
            }
            if (item.hasType(AnalysisItemTypes.TEXT)) {
                return TextCellRenderer;
            } else {
                if (item.hasType(AnalysisItemTypes.DERIVED_GROUPING)) {
                    var d:DerivedAnalysisDimension = item as DerivedAnalysisDimension;
                    return d.wordWrap ? TextCellRenderer : AnalysisCellRenderer;
                }
                var ext:TextReportFieldExtension = item.reportFieldExtension as TextReportFieldExtension;
                if (ext != null && ext.wordWrap) {
                    return TextCellRenderer;
                }
                return SummaryCellRenderer;
            }
        }

        public function updateExportMetadata():void {
            var hierarchy:AnalysisHierarchyItem = treeDef.hierarchy as AnalysisHierarchyItem;
            var hierarchyIndex:int = findHierarchy(hierarchy.hierarchyLevels.getItemAt(hierarchy.hierarchyLevels.length - 1).analysisItem.qualifiedName());
            updateItem(hierarchy, hierarchyIndex);
            for (var i:int = 0; i < treeDef.items.length; i++) {
                var item:AnalysisItem = treeDef.items.getItemAt(i) as AnalysisItem;
                var columnIndex:int = findColumnIndex(item.qualifiedName());
                updateItem(item, columnIndex);
            }
        }

        private function onStretch(event:Event):void {
            var hierarchy:AnalysisHierarchyItem = treeDef.hierarchy as AnalysisHierarchyItem;
            var hierarchyIndex:int = findHierarchy(hierarchy.hierarchyLevels.getItemAt(hierarchy.hierarchyLevels.length - 1).analysisItem.qualifiedName());
            updateItem(hierarchy, hierarchyIndex);
            for (var i:int = 0; i < treeDef.items.length; i++) {
                var item:AnalysisItem = treeDef.items.getItemAt(i) as AnalysisItem;
                var columnIndex:int = findColumnIndex(item.qualifiedName());
                updateItem(item, columnIndex);
            }
        }

        private function findHierarchy(qualifiedName:String):int {
            for (var i:int = 0; i < coreGrid.columns.length; i++) {
                var column:AdvancedDataGridColumn = coreGrid.columns[i];
                if (column.dataField == "groupingColumn") {
                    return i;
                }
            }
            return -1;
        }

        private function findColumnIndex(qualifiedName:String):int {
            for (var i:int = 0; i < coreGrid.columns.length; i++) {
                var column:AdvancedDataGridColumn = coreGrid.columns[i];
                if (column.dataField == qualifiedName) {
                    return i;
                }
            }
            return -1;
        }

        public function preserveValues():Boolean {
            return true;
        }

        public function onCustomChangeEvent(event:CustomChangeEvent):void {
            if (event is EnableLookupEditingEvent) {
                lookupEditing = !lookupEditing;
            }
        }

        private var lookupEditing:Boolean;

        private function updateItem(item:AnalysisItem, columnIndex:int):void {
            item.itemPosition = columnIndex;
            var column:AdvancedDataGridColumn = coreGrid.columns[columnIndex];

            if (item != null && column != null) {

                item.width = column.width;

                var sortInfo:SortInfo = coreGrid.getFieldSortInfo(column);
                if (sortInfo != null && sortInfo.status == SortInfo.ACTUALSORT) {
                    item.sortSequence = sortInfo.sequenceNumber;
                    if (sortInfo.descending) {
                        item.sort = 2;
                    } else {
                        item.sort = 1;
                    }
                } else {
                    item.sort = 0;
                }
            }
        }

        [Bindable]
        private var stackIndex:int = 0;
        ]]></mx:Script>
    <mx:RemoteObject id="feedService" destination="feeds">
        <mx:method name="updateLookupTable"/>
    </mx:RemoteObject>
    <mx:Canvas width="100%" height="100%">
        <mx:VBox width="100%" height="100%">
            <mx:Box id="topBar" width="100%"/>
            <analysis:ListViewGrid id="coreGrid" width="100%" height="100%" visible="true" columnStretch="onStretch(event)"
                                 variableRowHeight="true"/>
        </mx:VBox>
    </mx:Canvas>

</mx:Module>