<?xml version="1.0"?>
<util:EISlimWindow xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:util="com.easyinsight.util.*">
    <mx:Script><![CDATA[
        import com.easyinsight.util.CancelButton;
        import com.easyinsight.util.SaveButton;

        import mx.collections.ArrayCollection;
        import mx.controls.Button;
        import mx.core.UIComponent;
        import mx.managers.PopUpManager;

        [Bindable]
        public var prompt:String;

        private var options:ArrayCollection = new ArrayCollection();

        public function defineOption(optionName:String, optionEvent:String):void {
            var o:Object = new Object();
            o.name = optionName;
            o.event = optionEvent;
            options.addItem(o);
        }

        override protected function createChildren():void {
            super.createChildren();
            if (options.length == 0) {
                var save:Object = new Object();
                save.name = "Save";
                save.event = PromptEvent.PROMPT_SAVE;
                options.addItem(save);
                var discard:Object = new Object();
                discard.name = "Discard Changes";
                discard.event = PromptEvent.PROMPT_DISCARD;
                options.addItem(discard);
                var cancel:Object = new Object();
                cancel.name = "Cancel";
                cancel.event = PromptEvent.PROMPT_CANCEL;
                options.addItem(cancel);
            }
            for each (var o:Object in options) {
                var button:UIComponent;
                if (o.name == "Cancel") {
                    button = new CancelButton();
                    CancelButton(button).label = o.name;
                } else {
                    button = new SaveButton();
                    SaveButton(button).label = o.name;
                }
                button.setStyle("fontSize", 14);
                button.addEventListener(MouseEvent.CLICK, createHandler(o.event, this));
                box.addChild(button);
            }
        }

        private function createHandler(type:String, parent:SavePromptWindow):Function {
            return function(event:MouseEvent):void {
                    dispatchEvent(new PromptEvent(type));
                    PopUpManager.removePopUp(parent);
                };
        }

        private function save():void {
            dispatchEvent(new PromptEvent(PromptEvent.PROMPT_SAVE));
            PopUpManager.removePopUp(this);
        }

        private function discard():void {
            dispatchEvent(new PromptEvent(PromptEvent.PROMPT_DISCARD));
            PopUpManager.removePopUp(this);
        }

        private function cancel():void {
            dispatchEvent(new PromptEvent(PromptEvent.PROMPT_CANCEL));
            PopUpManager.removePopUp(this);
        }
        ]]></mx:Script>
    <mx:TextArea text="{prompt}" fontSize="14" width="500" height="50" focusEnabled="false" editable="false" selectable="false"
            textAlign="center"/>
    <mx:HBox id="box" horizontalAlign="center" width="100%"/>
</util:EISlimWindow>
