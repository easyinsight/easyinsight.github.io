<?xml version="1.0" encoding="utf-8"?>
<util:EISlimWindow xmlns:util="com.easyinsight.util.*" xmlns:mx="http://www.adobe.com/2006/mxml">
	<mx:Script>
		<![CDATA[
        import com.easyinsight.administration.sharing.UserStub;
        import com.easyinsight.framework.User;

        import mx.collections.ArrayCollection;
        import mx.managers.PopUpManager;

        [Bindable]
        private var url:String;

        private var _report:AnalysisDefinition;

        private var _urlKey:String;

        private var _reportType:int;

        private var _dataSourceID:int;

        private var _reportWidth:uint = 600;

        private var _reportHeight:uint = 500;

        private var _reportName:String;

        private var _showToolbar:Boolean = true;

        private var _showFilters:Boolean = true;

        private var _insecureEmbed:Boolean;

        private var _showHeader:Boolean;

        public function set report(value:AnalysisDefinition):void {
            _report = value;
        }

        [Bindable(event="showHeaderChanged")]
        public function get showHeader():Boolean {
            return _showHeader;
        }

        public function set showHeader(value:Boolean):void {
            if (_showHeader == value) return;
            _showHeader = value;
            dispatchEvent(new Event("showHeaderChanged"));
            updateURL();
        }

        [Bindable(event="showToolbarChanged")]
        public function get showToolbar():Boolean {
            return _showToolbar;
        }

        public function set showToolbar(value:Boolean):void {
            if (_showToolbar == value) return;
            _showToolbar = value;
            dispatchEvent(new Event("showToolbarChanged"));
            updateURL();
        }

        [Bindable(event="showFiltersChanged")]
        public function get showFilters():Boolean {
            return _showFilters;
        }

        public function set showFilters(value:Boolean):void {
            if (_showFilters == value) return;
            _showFilters = value;
            dispatchEvent(new Event("showFiltersChanged"));
            updateURL();
        }

        [Bindable(event="reportWidthChanged")]
        public function get reportWidth():uint {
            return _reportWidth;
        }

        public function set reportWidth(value:uint):void {
            if (_reportWidth == value) return;
            _reportWidth = value;
            dispatchEvent(new Event("reportWidthChanged"));
            updateURL();
        }

        [Bindable(event="reportHeightChanged")]
        public function get reportHeight():uint {
            return _reportHeight;
        }

        public function set reportHeight(value:uint):void {
            if (_reportHeight == value) return;
            _reportHeight = value;
            dispatchEvent(new Event("reportHeightChanged"));
            updateURL();
        }

        public function set reportType(value:int):void {
            _reportType = value;
            updateURL();
        }

        public function set dataSourceID(value:int):void {
            _dataSourceID = value;
            updateURL();
        }

        public function set urlKey(value:String):void {
            _urlKey = value;
            updateURL();
        }

        public function set reportName(value:String):void {
            this._reportName = value;
            updateURL();
        }

        private var _showAsFlash:String = "flash";


        [Bindable(event="showAsFlashChanged")]
        public function get showAsFlash():String {
            return _showAsFlash;
        }

        public function set showAsFlash(value:String):void {
            if (_showAsFlash == value) return;
            _showAsFlash = value;
            dispatchEvent(new Event("showAsFlashChanged"));
            updateURL();
        }

        private function updateURL():void {
            var str:String;
            var sym:String;
            if (_showAsFlash == "flash") {
                str = "https://www.easy-insight.com/app/embed.jsp?width=" + _reportWidth + "&height=" + _reportHeight + "&analysisID=" +
                        _urlKey + "&reportType=" + _reportType + "&reportName=" + escape(_reportName) + "&dataSourceID=" + _dataSourceID;
                sym = "&";
            } else {
                if (userBox != null) {
                    var user:UserStub = userBox.selectedItem as UserStub;
                    if (user != null) {
                        str = "https://www.easy-insight.com/app/html/user/"+user.userKey+"/report/" + _urlKey + "/embed";
                        sym = "?";
                    }
                }
                if (str == null) {
                    str = "https://www.easy-insight.com/app/html/report/" + _urlKey + "/embed";
                    sym = "?";
                }
            }
            if (_showToolbar) {
                str += sym + "showToolbar=1";
                sym = "&";
            }
            if (_showFilters) {
                str += sym + "showFilters=1";
            }
            if (_showHeader) {
                str += sym + "showHeader=1";
            }

            url = "<iframe width=\"" + _reportWidth + "\" height=\"" + _reportHeight + "\" src=\"" + str + "\"></iframe>";
        }

        private function copyToClipboard():void {
            System.setClipboard(url);
        }

        [Bindable]
        private var visibilityExplanation:String;

        [Bindable]
        private var visibilityIndex:int;

        [Bindable]
        private var users:ArrayCollection;

        private function gotUsers():void {
            var users:ArrayCollection = shareService.getUserStubs.lastResult as ArrayCollection;
            var noSelection:Object = { email:"[ No User Selected ]"};
            users.addItemAt(noSelection, 0);
            this.users = users;
        }

        override protected function createChildren():void {
            super.createChildren();
            if (_report.publiclyVisible) {
                visibilityExplanation = "Anyone viewing the page containing this report will be able to see it without needing to log in via Easy Insight credentials.";
            } else if (_report.publicWithKey) {
                visibilityExplanation = "Anyone viewing the page containing this report will be able to see it without needing to log in via Easy Insight credentials if the appropriate embed key property is passed in as part of the URL.";
                if (User.getInstance().accountAdmin) {
                    visibilityIndex = 1;
                    shareService.getUserStubs.send(false);
                }
            } else {
                visibilityExplanation = "This report will require users to be logged in.";
            }
        }
        ]]>
	</mx:Script>
    <mx:RemoteObject destination="share" id="shareService">
        <mx:method name="getUserStubs" result="gotUsers()"/>
    </mx:RemoteObject>
    <mx:HBox>
        <mx:VBox horizontalAlign="center" paddingBottom="5" paddingLeft="5" paddingRight="5" paddingTop="5">
            <mx:Label text="Do you want to embed the report as Flash or HTML5?"/>
            <mx:RadioButtonGroup id="embedTypeGroup" selectedValue="{showAsFlash}" change="showAsFlash = String(embedTypeGroup.selectedValue)"/>
            <mx:HBox>
                <mx:RadioButton label="Flash" value="flash" groupName="embedTypeGroup"/>
                <mx:RadioButton label="HTML5" value="html" groupName="embedTypeGroup"/>
            </mx:HBox>
            <mx:Form paddingTop="0" paddingBottom="0" paddingLeft="0" paddingRight="0">
                <mx:FormItem label="Report Width:">
                    <mx:NumericStepper id="widthStepper" value="{reportWidth}" minimum="50" maximum="2400" change="reportWidth = widthStepper.value"/>
                </mx:FormItem>
                <mx:FormItem label="Report Height:">
                    <mx:NumericStepper id="heightStepper" value="{reportHeight}" minimum="50" maximum="2400" change="reportHeight = heightStepper.value"/>
                </mx:FormItem>
                <mx:FormItem label="Show Tool Bar:">
                    <mx:CheckBox id="toolBarCheckbox" change="showToolbar = toolBarCheckbox.selected" selected="true"/>
                </mx:FormItem>
                <mx:FormItem label="Show Filters:">
                    <mx:CheckBox id="filtersCheckbox" change="showFilters = filtersCheckbox.selected" selected="true"/>
                </mx:FormItem>
                <mx:FormItem label="Show Header:">
                    <mx:CheckBox id="headerCheckbox" change="showHeader = headerCheckbox.selected"/>
                </mx:FormItem>
            </mx:Form>
            <mx:Text width="400" text="{visibilityExplanation}"/>
            <mx:ViewStack selectedIndex="{visibilityIndex}">
                <mx:Box/>
                <mx:VBox>
                    <mx:Text width="400" text="Since this report is publicly visible with an embed key, you can choose a user whose configuration and data level security permissions will be applied to this view."/>
                    <mx:HBox>
                        <mx:Text text="User:"/>
                        <mx:ComboBox id="userBox" labelField="email" dataProvider="{users}" change="updateURL()"/>
                    </mx:HBox>
                </mx:VBox>
            </mx:ViewStack>
        </mx:VBox>
        <mx:VBox>
            <mx:Label text="You can paste this HTML into any web page to embed the report into the page." fontSize="14"/>
            <mx:HBox id="quickBox" verticalAlign="middle">
                <mx:TextArea editable="false" text="{url}" width="400" height="200" wordWrap="true" id="firstTextArea" borderStyle="solid" borderThickness="1"/>
                <util:SaveButton label="Copy to Clipboard" click="copyToClipboard()"/>
            </mx:HBox>
        </mx:VBox>
    </mx:HBox>
    <util:CancelButton label="Close" click="PopUpManager.removePopUp(this)"/>
</util:EISlimWindow>
