<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" creationComplete="initListeners()"
	implements="com.easyinsight.listing.IPerspective" xmlns:listing="com.easyinsight.listing.*" 
	xmlns:customupload="com.easyinsight.customupload.*" doubleClickEnabled="true" backgroundColor="#DCE2F8">
	<mx:states>
		<mx:State name="notLoggedIn">
			<mx:RemoveChild target="{uploadBox}"/>
			<mx:RemoveChild target="{uploadGrid}"/>
			<mx:AddChild relativeTo="{coreContent}">
				<listing:GottaLoginPage/>
			</mx:AddChild>
		</mx:State>
	</mx:states>
	<mx:Script>
		<![CDATA[
        import com.easyinsight.customupload.SimpleFeedWindow;
        import com.easyinsight.customupload.UploadConfigEvent;
        import com.easyinsight.framework.AsyncInfoEvent;
        import com.easyinsight.framework.EIMessageListener;
        import com.easyinsight.framework.LoginEvent;
        import com.easyinsight.framework.User;
        import com.easyinsight.genredata.AnalyzeEvent;
        import com.easyinsight.help.HelpWindow;
        import com.easyinsight.help.MyDataHelp;
        import com.easyinsight.outboundnotifications.RefreshEventInfo;
        import com.easyinsight.report.ReportAnalyzeSource;
        import com.easyinsight.reportpackage.AddReportToPackageWindow;
        import com.easyinsight.reportpackage.PackageEvent;
        import com.easyinsight.reportpackage.ReportPackageDescriptor;
        import com.easyinsight.reportpackage.ReportPackageWindow;
        import com.easyinsight.solutions.InsightDescriptor;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.binding.utils.BindingUtils;
        import mx.collections.ArrayCollection;
        import mx.collections.HierarchicalData;
        import mx.collections.ICollectionView;
        import mx.collections.IHierarchicalCollectionView;
        import mx.collections.Sort;
        import mx.controls.Alert;
        import mx.events.AdvancedDataGridEvent;
        import mx.events.CloseEvent;
        import mx.events.ListEvent;
        import mx.managers.BrowserManager;
        import mx.managers.PopUpManager;
        import mx.rpc.events.FaultEvent;
        import mx.utils.ObjectUtil;
        import mx.utils.URLUtil;

        private function downloadAirWidget():void {
            var request:URLRequest = new URLRequest("/app/assets/EIAirWidget.air");
            request.method = URLRequestMethod.GET;
            navigateToURL(request);
        }

        private var initialEntry:Boolean = true;

        [Embed(source="../../../../assets/data_blue_x16.png")]
        public var listIcon:Class;

        [Embed(source="../../../../assets/package.png")]
        public var packageIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/monitor2.png")]
        public var monitorIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/chart_column.png")]
        public var crosstabIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/data_connection.png")]
        public var dataConnectionIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/data_table_x64.png")]
        public var joinIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/package_add.png")]
        public var packageAddIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/data_add_x64.png")]
        public var addIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/application_enterprise.png")]
        public var apiIcon:Class;

        [Bindable]
        [Embed(source="../../../../assets/helpx48.png")]
        public var helpIcon:Class;

        [Bindable]
        private var displayTreeData:ArrayCollection;
        private var sourceTreeData:ArrayCollection;

        private var keyword:String;

        private var _messageListener:EIMessageListener;

        private function onAsyncRefresh(event:AsyncInfoEvent):void {
            if (event.info.action == RefreshEventInfo.COMPLETE) {
                showLoading = true;
                uploadService.getFeedAnalysisTree.send(includeGroups, initialEntry);
            }
        }

        public function set messageListener(value:EIMessageListener):void {
            if (value == _messageListener) {
                return;
            }
            _messageListener = value;
            value.addEventListener(AsyncInfoEvent.ASYNC_INFO, onAsyncRefresh);
        }

        private function initListeners():void {
            addEventListener(KeyboardEvent.KEY_UP, keyHit);
            User.getEventNotifier().addEventListener(LoginEvent.LOGIN, onLogin);
            addEventListener(DeleteDataSourceEvent.DELETE_DATA_SOURCE, onDelete);
            addEventListener(UploadConfigEvent.UPLOAD_CONFIG_COMPLETE, uploadConfigComplete);
            messageListener = EIMessageListener.instance();
            BindingUtils.bindProperty(this, "includeGroups", groupInclude, "selected");
        }

        private function onLogin(event:LoginEvent):void {
            gotFocus();
        }

        private function keyHit(event:KeyboardEvent):void {
            if (event.keyCode == Keyboard.DELETE) {
                if (uploadGrid.selectedItems != null) {
                    if (uploadGrid.selectedItems.length > 1) {
                        Alert.show("You can only delete single items at the moment.");
                    } else {
                        deleteItem(uploadGrid.selectedItem);
                    }
                }
            }
        }

        private function onDelete(event:DeleteDataSourceEvent):void {
            deleteItem(event.descriptor);
        }

        private function deleteItem(selectedItem:Object):void {

            if (uploadGrid.selectedItem is DataFeedDescriptor) {
                var descriptor:DataFeedDescriptor = uploadGrid.selectedItem as DataFeedDescriptor;
                if (descriptor.role == DataFeedDescriptor.OWNER) {
                    Alert.show("Are you sure you want to delete this data source? You will not be able to undo this operation.", "Alert",
                            Alert.OK | Alert.CANCEL, this, alertFeedListener, null, Alert.CANCEL);
                } else {
                    Alert.show("Are you sure you want to cancel your subscription to this data source?", "Alert",
                            Alert.OK | Alert.CANCEL, this, alertFeedListener, null, Alert.CANCEL);
                }
            } else if (uploadGrid.selectedItem is InsightDescriptor) {
                Alert.show("Are you sure you want to delete this report? You will not be able to undo this operation.", "Alert",
                        Alert.OK | Alert.CANCEL, this, alertInsightListener, null, Alert.CANCEL);
            } else if (uploadGrid.selectedItem is ReportPackageDescriptor) {
                Alert.show("Are you sure you want to delete this package? You will not be able to undo this operation.", "Alert",
                        Alert.OK | Alert.CANCEL, this, alertPackageListener, null, Alert.CANCEL);
            }
        }

        private function alertFeedListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                var feedDescriptor:DataFeedDescriptor = uploadGrid.selectedItem as DataFeedDescriptor;
                ProgressAlert.alert(this, "Deleting data source...", "Deleted data souce!", uploadService.deleteUserUpload);
                uploadService.deleteUserUpload.send(feedDescriptor.dataFeedID);
            }
        }

        private function alertPackageListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                var reportDescriptor:ReportPackageDescriptor = uploadGrid.selectedItem as ReportPackageDescriptor;
                ProgressAlert.alert(this, "Deleting package...", "Deleted package!", packageService.deleteReportPackage);
                packageService.deleteReportPackage.send(reportDescriptor.id);
            }
        }

        private function deletedPackage():void {
            ProgressAlert.alert(this, "Refreshing...", null, uploadService.getFeedAnalysisTree);
            showLoading = true;
            uploadService.getFeedAnalysisTree.send(includeGroups, initialEntry);
        }

        private function alertInsightListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                var definition:InsightDescriptor = uploadGrid.selectedItem as InsightDescriptor;
                ProgressAlert.alert(this, "Deleting report...", "Deleted report!", analysisService.deleteAnalysisDefinition);
                analysisService.deleteAnalysisDefinition.send(definition.id);
            }
        }

        private function deletedAnalysis():void {
            ProgressAlert.alert(this, "Refreshing...", null, uploadService.getFeedAnalysisTree);
            showLoading = true;
            uploadService.getFeedAnalysisTree.send(includeGroups, initialEntry);
        }

        public function search():void {
            this.keyword = searchText.text.toLowerCase();
            IHierarchicalCollectionView(uploadGrid.dataProvider).refresh();
        }

        private function gridDoubleClick(event:ListEvent):void {
            var analyzeSource:AnalyzeSource = null;
            var selectedObject:Object = event.currentTarget.selectedItem;
            if (selectedObject is DataFeedDescriptor) {
                var descriptor:DataFeedDescriptor = selectedObject as DataFeedDescriptor;
                analyzeSource = new DescriptorAnalyzeSource(descriptor.dataFeedID, descriptor.name);
                dispatchEvent(new AnalyzeEvent(analyzeSource));
            } else {
                analyzeSource = new ReportAnalyzeSource(InsightDescriptor(selectedObject));
                dispatchEvent(new AnalyzeEvent(analyzeSource));
            }
        }

        public function gotFocus():void {
            var fragmentObject:Object = new Object();
            fragmentObject.page = "myData";
            BrowserManager.getInstance().setTitle("Easy Insight - My Data");
            var fragmentString:String = URLUtil.objectToString(fragmentObject);
            BrowserManager.getInstance().setFragment(fragmentString);
            var user:User = User.getInstance();
            if (user == null) {
                currentState = "notLoggedIn";
            } else {
                if (currentState != "") {
                    currentState = "";
                }
                showLoading = true;
                uploadService.getFeedAnalysisTree.send(includeGroups, initialEntry);
            }
        }

        private function passThrough(event:Event):void {
            dispatchEvent(event);
        }

        private function createEmptyFeed():void {
            var userUpload:SimpleFeedWindow = SimpleFeedWindow(PopUpManager.createPopUp(this, SimpleFeedWindow, true));
            userUpload.addEventListener(UploadConfigEvent.UPLOAD_CONFIG_COMPLETE, uploadConfigComplete);
            PopUpUtil.centerPopUp(userUpload);
        }

        private function uploadConfigComplete(event:UploadConfigEvent):void {
            showLoading = true;
            uploadService.getFeedAnalysisTree.send(includeGroups, initialEntry);
            if (event.feedID > 0) {
                var descriptor:DataFeedDescriptor = new DataFeedDescriptor();
                descriptor.dataFeedID = event.feedID;
                descriptor.name = event.name;
                if (event.startInAdmin) {
                    dispatchEvent(new AnalyzeEvent(new FeedAdminAnalyzeSource(descriptor.dataFeedID)));
                } else {
                    dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(event.feedID, event.name)));
                }
            }
        }

        private function gotFeedAnalysisTree():void {

            var sort:Sort = ICollectionView(uploadGrid.dataProvider).sort;
            var myDataTree:MyDataTree = uploadService.getFeedAnalysisTree.lastResult as MyDataTree;
            if (initialEntry) {
                initialEntry = false;
                includeGroups = myDataTree.includeGroup;
            }
            sourceTreeData = myDataTree.objects;
            displayTreeData = new ArrayCollection(sourceTreeData.toArray());
            var hierarchicalData:HierarchicalData = new HierarchicalData();
            hierarchicalData.source = displayTreeData;
            uploadGrid.dataProvider = hierarchicalData;
            IHierarchicalCollectionView(uploadGrid.dataProvider).filterFunction = filterFunction;
            displayTreeData.sort = sort;
            displayTreeData.refresh();
            showLoading = false;
        }

        private function filterFunction(item:Object):Boolean {
            if (keyword != null && keyword.length > 0) {
                return (item.name.toLowerCase().indexOf(keyword) > -1 ||
                        (item.genre != null && item.genre.toLowerCase().indexOf(keyword) > -1));
            }
            return true;
        }

        private function iconFunction(item:Object, depth:int):Class {
            if (item is DataFeedDescriptor) {
                return listIcon;
             } else if (item is InsightDescriptor) {
             return crosstabIcon;
             } else if (item is ReportPackageDescriptor) {
                return packageIcon;
            }
            return null;
        }

        private function simpleIconFunction(item:Object):Class {
            if (item is DataFeedDescriptor) {
                return listIcon;
             } else if (item is InsightDescriptor) {
             return crosstabIcon;
             } else if (item is ReportPackageDescriptor) {
                return packageIcon;
            }
            return null;
        }

        private function failure(event:FaultEvent):void {
            Alert.show(event.fault.message);
        }

        private function deleted():void {
            showLoading = true;
            uploadService.getFeedAnalysisTree.send(includeGroups, initialEntry);
        }

        private function createCompositeFeed():void {
            var descriptors:ArrayCollection = new ArrayCollection();
            if (uploadGrid.selectedItems != null) {

                for each (var obj:Object in uploadGrid.selectedItems) {
                    if (obj is DataFeedDescriptor) {
                        descriptors.addItem(obj);
                    }
                }

            }
            dispatchEvent(new AnalyzeEvent(new CompositeFeedCreationSource(descriptors)));
        }

        private function help():void {
            HelpWindow.createHelpWindow(MyDataHelp, this);
        }

        private function onItemOpen(event:AdvancedDataGridEvent):void {
            var item:Object = event.item;
            var children:ArrayCollection = item.children;

        }

        public function cleanup():void {
        }

        private var _includeGroups:Boolean;


        [Bindable(event="includeGroupsChanged")]
        public function get includeGroups():Boolean {
            return _includeGroups;
        }

        public function set includeGroups(value:Boolean):void {
            if (_includeGroups == value) return;
            _includeGroups = value;
            dispatchEvent(new Event("includeGroupsChanged"));
        }

        private function groupIncludeChanged():void {
            showLoading = true;
            uploadService.getFeedAnalysisTree.send(groupInclude.selected, initialEntry);
        }

        private function createPackage():void {
            var window:ReportPackageWindow = new ReportPackageWindow();
            window.addEventListener(PackageEvent.PACKAGE_ADDED, onPackageAdded);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onPackageAdded(event:PackageEvent):void {
            showLoading = true;
            uploadService.getFeedAnalysisTree.send(groupInclude.selected, initialEntry);
        }

        private function customSort(obj1:Object, obj2:Object):int {
            if (obj1 is DataFeedDescriptor && obj2 is DataFeedDescriptor) {
                var date1:Date = DataFeedDescriptor(obj1).lastDataTime;
                var date2:Date = DataFeedDescriptor(obj2).lastDataTime;
                return ObjectUtil.dateCompare(date1, date2);
            }
            if (dataColumn.sortDescending) {
                return -1;
            } else {
                return 1;
            }
        }

        [Bindable]
        private var showLoading:Boolean = false;
		]]>
</mx:Script>
	<mx:RemoteObject id="uploadService" destination="userUpload">
		<mx:method name="getFeedAnalysisTree" result="gotFeedAnalysisTree()"/>
		<mx:method name="deleteUserUpload" result="deleted()"/>		
	</mx:RemoteObject>
	<mx:RemoteObject id="analysisService" destination="analysisDefinition">
		<mx:method name="deleteAnalysisDefinition" result="deletedAnalysis()"/>
	</mx:RemoteObject>
    <mx:RemoteObject id="packageService" destination="reportPackageService">
		<mx:method name="deleteReportPackage" result="deletedPackage()"/>
	</mx:RemoteObject>

	<mx:VBox id="coreContent" height="100%" width="100%" paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10" verticalGap="10">
		<mx:HBox width="100%" id="uploadBox" verticalAlign="middle">
			<mx:Button toolTip="Create composite data source..." click="createCompositeFeed()" id="compositeFeedButton" icon="{joinIcon}"
				label="Combine Sources" labelPlacement="bottom"/>
            <mx:Button toolTip="Create package..." click="createPackage()" id="createPackageButton" icon="{packageAddIcon}"
				label="Create Package" labelPlacement="bottom"/>
			<mx:Button toolTip="Create empty data source..." click="createEmptyFeed()" id="emptyFeedButton" icon="{addIcon}"
				label="New Empty Source" labelPlacement="bottom"/>
            <!--<mx:Button icon="{helpIcon}" label="Help..." labelPlacement="bottom" click="help()"/>-->
			<mx:Button toolTip="Download desktop widget..." click="downloadAirWidget()" id="downloadAirButton" icon="{monitorIcon}"
               label="Desktop Widget" labelPlacement="bottom"/>
            <mx:ProgressBar indeterminate="true" visible="{showLoading}"/>
            <mx:Spacer width="100%"/>
            <mx:VBox horizontalAlign="center">
                <mx:HBox borderStyle="inset" borderThickness="1" paddingTop="5" paddingBottom="5" paddingLeft="5" paddingRight="5">
                    <mx:TextInput id="searchText" keyUp="search()"/>
                    <mx:Button toolTip="Search" icon="@Embed(source='../../../../assets/view.png')" id="searchButton"/>
                </mx:HBox>
                <mx:HBox>
                    <mx:CheckBox id="groupInclude" change="groupIncludeChanged()" selected="{includeGroups}"/>
                    <mx:Label text="Include Data in My Groups"/>
                </mx:HBox>
            </mx:VBox>
		</mx:HBox>

		<mx:AdvancedDataGrid width="100%" height="100%" id="uploadGrid" allowMultipleSelection="true" fontSize="12"
			itemDoubleClick="gridDoubleClick(event)" headerStyleName="BlueHeader" itemOpen="onItemOpen(event)"
                iconFunction="simpleIconFunction">
			<mx:dataProvider>
	            <mx:HierarchicalData source="{displayTreeData}"/>
	        </mx:dataProvider>
			<mx:columns>
				<mx:AdvancedDataGridColumn headerText="Name" dataField="name"/>
				<mx:AdvancedDataGridColumn headerText="Size" dataField="size" width="110" itemRenderer="com.easyinsight.listing.SizeRenderer"/>
				<mx:AdvancedDataGridColumn id="dataColumn" headerText="Last Data Time" dataField="lastDataTime" width="140" itemRenderer="com.easyinsight.listing.MyDataLastDataLabel"
                        sortCompareFunction="customSort"/>
				<mx:AdvancedDataGridColumn width="250" headerText="" dataField="name" sortable="false" itemRenderer="com.easyinsight.listing.MyDataIconControls"/>
			</mx:columns>
		</mx:AdvancedDataGrid>
	</mx:VBox>
</mx:VBox>
