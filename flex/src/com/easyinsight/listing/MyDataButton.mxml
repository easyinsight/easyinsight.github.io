<?xml version="1.0"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" height="100%" paddingLeft="10" paddingRight="10" paddingTop="10"
                         paddingBottom="10" horizontalAlign="center" verticalScrollPolicy="off">
    <mx:Metadata>
        [Event(name="uiChange", type="flash.events.Event")]
    </mx:Metadata>
    <mx:Script><![CDATA[
        import com.easyinsight.feedassembly.CompositeCreator;
        import com.easyinsight.framework.User;
        import com.easyinsight.genredata.AnalyzeEvent;
        import com.easyinsight.skin.ApplicationSkin;
        import com.easyinsight.skin.ApplicationSkinWindow;
        import com.easyinsight.skin.HomeConfigWindow;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.collections.ArrayCollection;

        import mx.managers.PopUpManager;
        import mx.states.RemoveChild;
        import mx.states.State;

        [Bindable]
        [Embed(source="../../../../assets/preferences.png")]
        private var configIcon:Class;


        [Bindable]
        [Embed(source="../../../../assets/data_table.png")]
        private var joinIcon:Class;

        public function refreshActions():void {
            adminService.getRecentActions.send()
        }

        private function createCompositeFeed():void {
            var descriptors:ArrayCollection = new ArrayCollection();
            var creator:CompositeCreator = new CompositeCreator(this);
            creator.dataSources = descriptors;
            creator.addEventListener(AnalyzeEvent.ANALYZE, onAnalyze, false, 0, true);
            creator.start();
        }

        [Bindable]
        private var recentActions:ArrayCollection;

        private function gotRecentActions():void {
            recentActions = adminService.getRecentActions.lastResult as ArrayCollection;
        }

        public function updateUI(event:Event = null):void {
            currentState = "";
            var showState:State = new State();
            showState.name = "buttonConfig";
            var ops:Array = [];
            if (!ApplicationSkin.instance().myDataCombine) {
                var removeComposite:RemoveChild = new RemoveChild();
                removeComposite.target = compositeFeedButton;
                ops.push(removeComposite);
            }
            if (!User.getInstance().analyst) {
                var removeConfig:RemoveChild = new RemoveChild();
                removeConfig.target = customizeButton;
                ops.push(removeConfig);
            }
            showState.overrides = ops;
            states = [ showState ];
            this.currentState = "buttonConfig";
            dispatchEvent(new Event('uiChange'));
        }

        private function onAnalyze(event:AnalyzeEvent):void {
            dispatchEvent(event);
        }

        private function customizeMyData():void {
            var applicationSkinWindow:HomeConfigWindow = new HomeConfigWindow();
            applicationSkinWindow.addEventListener(Event.CHANGE, updateUI, false, 0, true);
            PopUpManager.addPopUp(applicationSkinWindow, this, true);
            PopUpUtil.centerPopUp(applicationSkinWindow);
        }

        private function htmlView():void {
            ProgressAlert.alert(this, "Saving preferences...", null, userService.switchHtmlFlex);
            userService.switchHtmlFlex.send(true);
        }

        private function switched():void {
            navigateToURL(new URLRequest("/app/html"), "_self");
        }
        ]]></mx:Script>
    <mx:RemoteObject id="adminService" destination="admin">
        <mx:method name="getRecentActions" result="gotRecentActions()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="userService" destination="login">
        <mx:method name="switchHtmlFlex" result="switched()"/>
    </mx:RemoteObject>
    <mx:Button label="Switch to HTML View" click="htmlView()" labelPlacement="right" fontSize="12" styleName="grayButton" width="180"/>
    <mx:Button toolTip="Create composite data source..." click="createCompositeFeed()"
               id="compositeFeedButton" icon="{joinIcon}"
               label="Join Data Sources..." labelPlacement="right" fontSize="12" styleName="grayButton"
               width="180"/>
    <mx:Button toolTip="Customize Page..." click="customizeMyData()" icon="{configIcon}"
               label="Customize Page..." labelPlacement="right" fontSize="12" styleName="grayButton"
               width="180" id="customizeButton"/>
    <mx:HRule width="180"/>
    <mx:Label text="Recent Actions" fontSize="16" fontWeight="bold"/>
    <mx:List dataProvider="{recentActions}" backgroundAlpha="0" borderStyle="none" height="100%"
            itemRenderer="com.easyinsight.listing.LastActionLink" variableRowHeight="true" verticalScrollPolicy="off"/>
</mx:VBox>
