<?xml version="1.0" encoding="utf-8"?>
<mx:Box xmlns:listing="com.easyinsight.listing.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="100%"
                      height="100%"
                      xmlns:genredata="com.easyinsight.genredata.*"
                      backgroundColor="#DCE2F8" implements="com.easyinsight.listing.IPerspective" creationComplete="setupListeners()">
    <mx:Script>
		<![CDATA[

        import com.easyinsight.solutions.Solution;

        import mx.binding.utils.BindingUtils;
        import mx.utils.URLUtil;
        import mx.managers.BrowserManager;

        private function setupListeners():void {
            BindingUtils.bindProperty(stateBar, "selectedIndex", this, "viewMode");

            BindingUtils.bindProperty(displayTypeBar, "selectedIndex", this, "displayMode");

        }

        private function updateState():void {
            viewMode = stateBar.selectedIndex;
            listsBox.removeAllChildren();
            if (stateBar.selectedIndex == 0) {
                exchangeController = publicDataController;
            } else if (stateBar.selectedIndex == 1) {
                exchangeController = solutionDataController;
            }
            updateDisplayMode2();
            updateURL();
            listsBox.addChild(exchangeController.selectedPage);
            exchangeController.refreshData();
        }

        private function updateURL():void {
            var fragmentObject:Object = new Object();
            fragmentObject.page = "exchange";
            fragmentObject.view = viewMode;
            fragmentObject.display = displayMode;
            var fragmentString:String = URLUtil.objectToString(fragmentObject);
            fragmentString = stringReplaceAll(fragmentString, ";", "&");
            BrowserManager.getInstance().setFragment(fragmentString);
        }

        public static function stringReplaceAll(source:String, find:String, replacement:String) : String
        {
            return source.split(find).join(replacement);
        }

        private function search():void {
            exchangeController.keyword = searchText.text;
        }

        private function updateDisplayMode2():void {
            displayMode = displayTypeBar.selectedIndex;
            if (displayTypeBar.selectedIndex == 1) {
                exchangeController.displayMode = "grid";
            } else if (displayTypeBar.selectedIndex == 0) {
                exchangeController.displayMode = "summary";
            } else if (displayTypeBar.selectedIndex == 2) {
                exchangeController.displayMode = "detail";
            }
            updateURL();
        }

        /*private function updateState():void {
         }*/


        [Bindable]
        [Embed(source="../../../../assets/background2.JPG")]
        private var background2:Class;


        public function gotFocus():void {
            BrowserManager.getInstance().setTitle("Easy Insight - Exchange");
            updateURL();
            if (viewMode == 1) {
                exchangeController = solutionDataController;
            } else {
                exchangeController = publicDataController;
            }
            if (displayMode == 1) {
                exchangeController.displayMode = "grid";
            } else if (displayMode == 2) {
                exchangeController.displayMode = "detail";
            } else {
                exchangeController.displayMode = "summary";
            }
            listsBox.addChild(exchangeController.selectedPage);
            exchangeController.refreshData();
        }

        private function onChangeView(event:ExchangeControllerEvent):void {
            listsBox.removeAllChildren();
            listsBox.addChild(event.exchangePage);
        }

        private var _viewMode:int = 0;

        private var _displayMode:int = 0;


        [Bindable(event="viewModeChanged")]
        public function get viewMode():int {
            return _viewMode;
        }

        public function set viewMode(value:int):void {
            if (_viewMode == value) return;
            _viewMode = value;
            dispatchEvent(new Event("viewModeChanged"));
        }

        [Bindable(event="displayModeChanged")]
        public function get displayMode():int {
            return _displayMode;
        }

        public function set displayMode(value:int):void {
            if (_displayMode == value) return;
            _displayMode = value;
            dispatchEvent(new Event("displayModeChanged"));
        }

        private var exchangeController:ExchangeController;


        [Bindable(event="solutionChanged")]
        public function get solution():Solution {
            return _solution;
        }

        public function set solution(value:Solution):void {
            if (_solution == value) return;
            _solution = value;
            dispatchEvent(new Event("solutionChanged"));
        }

        private var _solution:Solution;

                ]]>
	</mx:Script>

    <genredata:PublicDataController id="publicDataController" changeView="onChangeView(event)"/>
    <genredata:SolutionExchangeController id="solutionDataController" changeView="onChangeView(event)" solution="{solution}"/>

    <mx:VBox width="100%" height="100%"  paddingLeft="0" paddingRight="0" paddingTop="0" paddingBottom="0"
                      verticalGap="0">

    <mx:HBox width="100%" horizontalAlign="center" height="100%" backgroundColor="#818285" paddingRight="0" paddingTop="0" paddingBottom="0">
    <mx:VBox id="centerBox" width="1000" height="100%" backgroundImage="{background2}" backgroundSize="100%" paddingBottom="10"
             paddingLeft="10" paddingRight="10" paddingTop="10">
        <mx:HBox width="100%" verticalAlign="middle">
            <mx:Spacer width="100%"/>
                <mx:ToggleButtonBar itemClick="updateState()" id="stateBar">
                    <mx:dataProvider>
                        <mx:Array>
                            <mx:String>Public Data</mx:String>
                            <mx:String>Solution Reports</mx:String>
                            <!--<mx:String>KPI Trees</mx:String>
                            <mx:String>Skins</mx:String>-->
                        </mx:Array>
                    </mx:dataProvider>
                </mx:ToggleButtonBar>
            <mx:Spacer width="100%"/>
                <mx:ToggleButtonBar itemClick="updateDisplayMode2()" id="displayTypeBar">
                    <mx:dataProvider>
                        <mx:Array>
                            <mx:String>Summary</mx:String>
                            <mx:String>Grid</mx:String>
                            <!--<mx:String>Detail</mx:String>-->                                
                        </mx:Array>
                    </mx:dataProvider>
                </mx:ToggleButtonBar>
            <mx:Spacer width="100%"/>
            <mx:VBox>
                <mx:HBox horizontalAlign="right">
                    <mx:TextInput id="searchText"/>
                    <mx:Button toolTip="Search" icon="@Embed(source='../../../../assets/view.png')" id="searchButton"
                            click="search()"/>
                </mx:HBox>
                <mx:Label fontSize="10" text="Enter data source or report name or tags"/>
            </mx:VBox>
        </mx:HBox>
        <mx:Spacer height="20" id="topSpacer"/>
        <mx:HBox width="100%" height="100%" id="listsBox"/>            
    </mx:VBox>
    </mx:HBox>
    </mx:VBox>
</mx:Box>
