<?xml version="1.0" encoding="utf-8"?>
<core:FullScreenPage xmlns:core="com.easyinsight.*" xmlns:mx="http://www.adobe.com/2006/mxml"
                     xmlns:analysis="com.easyinsight.analysis.*" xmlns:mxeffects="com.adobe.ac.mxeffects.*"
                     xmlns:datasources="com.easyinsight.datasources.*"
                     creationComplete="initializeForWorksheet()" xmlns:ns1="com.easyinsight.analysis.*" width="100%"
                     height="100%"
                     xmlns:transforms="com.easyinsight.transforms.*"
                     xmlns:util="com.easyinsight.util.*" xmlns:map="com.easyinsight.map.*"
                     xmlns:filtering="com.easyinsight.filtering.*"
                     xmlns:feed="com.easyinsight.administration.feed.*" doubleClickEnabled="true"
                     backgroundImage="{background2}" backgroundSize="100%"
                     paddingTop="10" paddingLeft="10" paddingRight="10" paddingBottom="10" backgroundColor="#DCE2F8"
        implements="com.easyinsight.framework.IFullScreenPage">
    <core:states>
        <mx:State name="NotLoggedIn">
            <mx:RemoveChild target="{subscribeToFeedButton}"/>
            <mx:RemoveChild target="{groupAddFeedButton}"/>
            <mx:RemoveChild target="{subscribeToInsightButton}"/>
            <mx:RemoveChild target="{groupAddInsightButton}"/>
            <mx:RemoveChild target="{saveButton}"/>
            <mx:RemoveChild target="{saveAsButton}"/>
            <!--<mx:RemoveChild target="{sharingSettingsBox}"/>
            <mx:RemoveChild target="{createFeedButton}"/>
            <mx:SetProperty target="{analysisNameInput}" name="editable" value="false"/>
            <mx:SetProperty target="{analysisTagsInput}" name="editable" value="false"/>-->
        </mx:State>
        <mx:State name="Professional">

        </mx:State>
        <mx:State name="Group" basedOn="Professional">

        </mx:State>
        <mx:State name="Individual" basedOn="Group">
            <!--<mx:RemoveChild target="{createFeedButton}"/>-->
        </mx:State>
        <mx:State name="Free" basedOn="Individual">

        </mx:State>
    </core:states>
    <mx:Script>
		<![CDATA[
        import com.easyinsight.account.Account;
        import com.easyinsight.administration.feed.DeleteAnalysisItemEvent;
        import com.easyinsight.analysis.AnalysisChangedEvent;        
        import com.easyinsight.analysis.AnalysisItemNode;
        import com.easyinsight.analysis.AnalysisItemTypeRenderer;
        import com.easyinsight.analysis.AnalysisUtil;
        import com.easyinsight.analysis.ControllerLookup;
        import com.easyinsight.analysis.DataServiceEvent;
        import com.easyinsight.analysis.FeedNode;
        import com.easyinsight.analysis.HierarchyDrilldownEvent;
        import com.easyinsight.analysis.AnalysisStateChangeEvent;
        import com.easyinsight.analysis.DataViewFactory;
        import com.easyinsight.analysis.HierarchyRollupEvent;
        import com.easyinsight.analysis.IReportController;
        import com.easyinsight.analysis.list.ListController;
        import com.easyinsight.analysis.AddFeedToGroupWindow;
        import com.easyinsight.analysis.SubscriptionEvent;
        import com.easyinsight.analysis.AddInsightToGroupWindow;
        import com.easyinsight.analysis.UserCapabilities;
        import com.easyinsight.datasources.DataSourceInfo;
        import com.easyinsight.detail.DataDetailEvent;
        import com.easyinsight.detail.DetailBook;
        import com.easyinsight.framework.InvalidFieldsEvent;
        import com.easyinsight.framework.LoginEvent;
        import com.easyinsight.genredata.ModuleAnalyzeEvent;
        import com.easyinsight.listing.DescriptorAnalyzeSource;
        import com.easyinsight.listing.FeedAdminAnalyzeSource;
        import com.easyinsight.report.MultiReportAnalyzeSource;
        import com.easyinsight.report.ReportAnalyzeSource;
        import com.easyinsight.solutions.InsightDescriptor;
        import com.easyinsight.util.PopUpUtil;
        import mx.collections.Sort;
        import mx.events.DragEvent;
        import mx.events.ListEvent;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.analysis.AnalysisCloseEvent;        
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.util.ProgressAlert;
        import com.easyinsight.commands.CommandEvent;
        import mx.utils.ObjectUtil;
        import mx.utils.URLUtil;
        import mx.managers.BrowserManager;
        import mx.controls.Alert;
        import mx.events.CloseEvent;
        import com.easyinsight.commands.CommandProcessor;
        import com.easyinsight.framework.DataServiceLoadingEvent;
        import com.easyinsight.framework.DataService;
        import com.easyinsight.analysis.SavedAnalysisEvent;
        import com.easyinsight.framework.User;
        import com.easyinsight.filtering.TransformsUpdatedEvent;
        import com.easyinsight.feedassembly.CompositeFeedCreatedEvent;
        import mx.managers.PopUpManager;
        import com.easyinsight.filtering.FilterDefinition;
        import com.easyinsight.filtering.FilterRawData;
        import com.easyinsight.analysis.FeedMetadata;
        import com.easyinsight.framework.CredentialsCache;
        import mx.collections.ArrayCollection;
        import com.easyinsight.analysis.AnalysisDefinition;

        [Bindable]
        [Embed(source="../../../assets/background2.JPG")]
        private var background2:Class;

        [Bindable]
        public var dataService:DataService;

        [Bindable]
        private var availableFields:ArrayCollection;

        [Bindable]
        private var dataSourceInfo:DataSourceInfo;

        [Bindable]
        private var selectedMode:int;

        private var _analysisDefinition:AnalysisDefinition;

        [Bindable]
        private var _previewMode:Boolean = false;

        [Bindable]
        private var admin:Boolean;

        public var filterOverrides:ArrayCollection = null;

        private var feedMetadata:FeedMetadata;

        [Bindable]
        private var dataView:DataViewFactory;

        private var commandProcessor:CommandProcessor;

        [Bindable]
        private var canSubscribeToInsight:Boolean;

        [Bindable]
        private var canAddInsightToGroup:Boolean;

        [Bindable]
        private var canSubscribeToFeed:Boolean;

        [Bindable]
        private var canAddFeedToGroup:Boolean;

        [Bindable]
        private var wrappers:ArrayCollection;

        [Bindable]
        private var modeText:String = "";

        private var analysisChanged:Boolean = false;

        [Bindable]
        private var canMultiViewAnalysis:Boolean;

        [Bindable]
        private var dataSourceName:String;

        [Bindable]
        private var _reportID:int;

        [Bindable]
        private var originSolution:int;

        private var loading:Boolean = false;

        public function set previewMode(previewMode:Boolean):void {
            this._previewMode = previewMode;
            this.dataService.previewMode = previewMode;
        }

        public function onInvalidFields(event:InvalidFieldsEvent):void {
            transformContainer.invalidateItems(event.invalidAnalysisItemIDs);
            dataView.invalidateItems(event.invalidAnalysisItemIDs);
            availableFields = new ArrayCollection();
            for each (var feedNode:FeedNode in event.feedMetadata.fieldHierarchy) {
                var analysisWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(feedNode);
                availableFields.addItem(analysisWrapper);
            }
            dataView.availableFields = availableFields;
            Alert.show("The underlying data to this data source has been changed, so some fields may have been removed from your filters or report.");
            dataView.retrieveData();
        }

        private function onAnalysisStateChange(event:AnalysisStateChangeEvent):void {
            var controller:IReportController = event.controller;
            loadController(controller);
        }

        private function onDataReturn(event:DataServiceEvent):void {
            this.dataSourceInfo = event.dataSource;
        }

        private function loadController(controller:IReportController):void {
            var dataViewFactory:DataViewFactory = controller.createDataView();
            dataViewFactory.availableFields = wrappers;
            if (dataView != null) {
                dataView.removeEventListener(DataServiceLoadingEvent.LOADING_STARTED, startLoading);
                dataView.removeEventListener(DataServiceLoadingEvent.LOADING_STOPPED, stopLoading);
                dataView.removeEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
                dataControlVBox.removeChild(dataView);
            }
            if (analysisDefinition == null) {
                this.analysisDefinition = dataViewFactory.createNewDefinition();
                analysisDefinition.canSaveDirectly = true;
                analysisDefinition.dataFeedID = dataService.dataFeedID;
            } else {
                analysisDefinition = dataViewFactory.fromExistingDefinition(analysisDefinition);
                dataViewFactory.analysisDefinition = analysisDefinition;
            }
            dataView = dataViewFactory;
            dataView.addEventListener(DataServiceLoadingEvent.LOADING_STARTED, startLoading);
            dataView.addEventListener(DataServiceLoadingEvent.LOADING_STOPPED, stopLoading);
            dataView.addEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
            dataView.addEventListener(DataServiceEvent.DATA_RETURNED, onDataReturn);
            dataControlVBox.addChild(dataViewFactory);
            dataViewFactory.retrieveData();
        }

        public function newAnalysis():void {
            var controller:IReportController = new ListController();
            loadController(controller);
        }


        [Bindable(event="analysisDefinitionChanged")]
        public function get analysisDefinition():AnalysisDefinition {
            return _analysisDefinition;
        }

        public function set analysisDefinition(value:AnalysisDefinition):void {
            if (_analysisDefinition == value) return;
            _analysisDefinition = value;
            dispatchEvent(new Event("analysisDefinitionChanged"));
        }

        public function set reportID(val:int):void {
            _reportID = val;
        }

        public function openAnalysis(analysisDefinition:AnalysisDefinition):void {
            BrowserManager.getInstance().setTitle("Easy Insight - " + analysisDefinition.name);
            this.analysisDefinition = analysisDefinition;
            
            //analysisPolicy = analysisDefinition.policy;
        }

        private function onLogin(event:LoginEvent):void {
            setState();
            User.getEventNotifier().removeEventListener(LoginEvent.LOGIN, onLogin);
        }

        private function setState():void {
            switch (User.getInstance().getAccountType()) {
                case Account.PROFESSIONAL:
                    currentState = "Professional";
                    break;
                case Account.GROUP:
                    currentState = "Group";
                    break;
                case Account.INDIVIDUAL:
                    currentState = "Individual";
                    break;
                case Account.FREE:
                    currentState = "Free";
                    break;
            }
        }

        private function goAdmin():void {
            if (analysisChanged) {
                Alert.show("You have unsaved changes. Are you sure you want to go to data source administration?", "Alert",
                        Alert.OK | Alert.CANCEL, null, adminAlertListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new ModuleAnalyzeEvent(new FeedAdminAnalyzeSource(dataService.dataFeedID)));
            }
        }

        private function adminAlertListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new ModuleAnalyzeEvent(new FeedAdminAnalyzeSource(dataService.dataFeedID)));
            }
        }

        private function onMetadataReceipt(feedMetadata:FeedMetadata):void {
            if (User.getInstance() == null) {
                currentState = "NotLoggedIn";
                User.getEventNotifier().addEventListener(LoginEvent.LOGIN, onLogin);
            } else {
                setState();
            }

            
            this.feedMetadata = feedMetadata;
            this.originSolution = feedMetadata.originSolution;
            this.admin = feedMetadata.dataSourceAdmin;
            dataSourceName = this.feedMetadata.dataSourceName;
            availableFields = new ArrayCollection();
            wrappers = new ArrayCollection();
            for each (var feedNode:FeedNode in feedMetadata.fieldHierarchy) {
                var analysisWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(feedNode);
                if (!analysisWrapper.hidden) {
                    availableFields.addItem(analysisWrapper);
                }
            }
            for each (var listItem:AnalysisItem in feedMetadata.fields) {
                var analysisNode:AnalysisItemNode = new AnalysisItemNode();
                analysisNode.analysisItem = listItem;
                var wrapper:AnalysisItemWrapper = new AnalysisItemWrapper(analysisNode);
                wrappers.addItem(wrapper);
            }

            if (analysisDefinition == null) {
                newAnalysis();
                for each (var filter:FilterDefinition in feedMetadata.intrinsicFilters) {
                    transformContainer.addFilterDefinition(filter);
                }
                var fragmentObject:Object = new Object();
                fragmentObject.feedID = String(dataService.dataFeedID);
                var fragmentString:String = URLUtil.objectToString(fragmentObject);
                BrowserManager.getInstance().setFragment(fragmentString);
            } else {
                //this.title = analysisDefinition.name;
                var controllerClass:Class = ControllerLookup.controllerForType(analysisDefinition.type);
                var controller:IReportController = new controllerClass();
                loadController(controller);

                analysisState.selectedLabel = analysisDefinition.type;

                var feedFragmentObject:Object = new Object();
                feedFragmentObject.analysisID = String(analysisDefinition.analysisID);
                var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
                BrowserManager.getInstance().setFragment(feedFragmentString);
                if (analysisDefinition.filterDefinitions == null) {
                    analysisDefinition.filterDefinitions = new ArrayCollection();
                }
                if (filterOverrides != null) {
                    analysisDefinition.filterDefinitions = filterOverrides;
                }
                createFilterBoxes(analysisDefinition.filterDefinitions.toArray());

                // add the fields which are specifically added in the context of the report, such as
                // calculations and hierarchies
                if (analysisDefinition.addedItems != null) {
                    for each (var item:AnalysisItem in analysisDefinition.addedItems) {
                        var analysisItemNode:AnalysisItemNode = new AnalysisItemNode();
                        analysisItemNode.analysisItem = item;
                        var addedWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(analysisItemNode);
                        availableFields.addItem(addedWrapper);
                        wrappers.addItem(addedWrapper);
                    }
                    sortFields();
                }
            }
            dataView.availableFields = wrappers;
            loading = false;
            stopLoading(null);
        }

        private function sortFields():void {
            var sort:Sort = new Sort();
            sort.compareFunction = customSort;
            availableFields.sort = sort;
            availableFields.refresh();
            wrappers.sort = sort;
            wrappers.refresh();
        }

        private function customSort(obj1:Object, obj2:Object, fields:Array = null):int {
            var wrapper1:AnalysisItemWrapper = obj1 as AnalysisItemWrapper;
            var wrapper2:AnalysisItemWrapper = obj2 as AnalysisItemWrapper;
            if (wrapper1.isAnalysisItem() && !wrapper2.isAnalysisItem()) {
                return 1;
            } else if (!wrapper1.isAnalysisItem() && wrapper2.isAnalysisItem()) {
                return -1;
            } else {
                return ObjectUtil.stringCompare(wrapper1.displayName, wrapper2.displayName);
            }
        }

        public function deleteItem(item:AnalysisItem):void {
            var index:int = 0;
            for each (var wrapper:AnalysisItemWrapper in availableFields) {
                if (wrapper.analysisItem.analysisItemID == item.analysisItemID) {
                    break;
                }
                index++;
            }
            availableFields.removeItemAt(index);
        }

        public function getAnalysisItems():ArrayCollection {
            var analysisItems:ArrayCollection = new ArrayCollection();
            for each (var analysisItemWrapper:AnalysisItemWrapper in wrappers) {
                analysisItems.addItem(analysisItemWrapper.analysisItem);
            }
            return analysisItems;
        }

        private function onCommand(event:CommandEvent):void {
            commandProcessor.addCommand(event.command);
        }

        private function onAnalysisChanged(event:AnalysisChangedEvent):void {
            this.analysisChanged = true;
        }

        public function initializeForWorksheet():void {
            loading = true;
            commandProcessor = new CommandProcessor();
            addEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
            addEventListener(DeleteAnalysisItemEvent.DELETE_ANALYSIS_ITEM, onFieldDelete);
            addEventListener(CommandEvent.COMMAND, onCommand);
            stage.addEventListener(KeyboardEvent.KEY_UP, onKey);
            addEventListener(HierarchyDrilldownEvent.DRILLDOWN, childFilterCreation);
            addEventListener(HierarchyRollupEvent.HIERARCHY_ROLLUP, onRollup);
            addEventListener(DataDetailEvent.DATA_DETAIL, toDetails);
            dataService.addEventListener(DataServiceLoadingEvent.LOADING_STARTED, startLoading);
            dataService.addEventListener(DataServiceLoadingEvent.LOADING_STOPPED, stopLoading);
            dataService.addEventListener(InvalidFieldsEvent.INVALID_FIELDS, onInvalidFields);
            transformContainer.addEventListener(TransformsUpdatedEvent.UPDATED_TRANSFORMS, transformsUpdated);
            startLoading(null);
            if (_reportID > 0) {
                analysisService.openAnalysisDefinition.send(_reportID);
            } else {
                initializeState();
            }
        }

        private function gotReport():void {
            this.analysisDefinition = analysisService.openAnalysisDefinition.lastResult as AnalysisDefinition;
            BrowserManager.getInstance().setTitle("Easy Insight - " + this.analysisDefinition.name);
            //analysisPolicy = this.analysisDefinition.policy;
            initializeState();
        }

        private function initializeState():void {
            if (analysisDefinition != null) {
                analysisService.getUserCapabilitiesForInsight.send(dataService.dataFeedID, analysisDefinition.analysisID);
            } else {
                analysisService.getUserCapabilitiesForFeed.send(dataService.dataFeedID);
            }
            // need to identify credential requirements at this point...

            //dataService.reloadInitialContent(this, onMetadataReceipt);
            detailService.getCredentialRequirements.send(dataService.dataFeedID);
        }

        private function onCredentials():void {
            dataService.reloadInitialContent(this, onMetadataReceipt);
        }

        private function gotCredentialRequirements():void {
            var requirements:ArrayCollection = detailService.getCredentialRequirements.lastResult as ArrayCollection;
            var canDirectly:Boolean = true;
            CredentialsCache.getCache().obtainCredentials(this, requirements, onCredentials);
        }

        private function onRollup(event:HierarchyRollupEvent):void {
            transformContainer.clearFilter(event.analysisItem);
        }

        private function childFilterCreation(event:HierarchyDrilldownEvent):void {
            transformContainer.processRawFilterData(event.filterRawData, event.type == HierarchyDrilldownEvent.DRILLDOWN);
        }

        private function toDetails(event:DataDetailEvent):void {
            var filter:FilterDefinition = transformContainer.createSimpleFilter(event.filterRawData);
            var filters:ArrayCollection = transformContainer.getFilterDefinitions();
            var allFilters:ArrayCollection = new ArrayCollection(filters.toArray());
            allFilters.addItem(filter);
            detailService.getAllData.send(dataService.dataFeedID, allFilters);
        }

        private function gotDetailData():void {
            var window:DetailBook = new DetailBook();
            window.details = detailService.getAllData.lastResult as ArrayCollection;
            PopUpManager.addPopUp(window, this);
            PopUpUtil.centerPopUp(window);
        }

        private function onKey(event:KeyboardEvent):void {            
            if (event.ctrlKey && event.shiftKey) {
                if (event.keyCode == 90) {
                    redo();
                }
            } else if (event.ctrlKey) {                
                if (event.keyCode == 90) {
                    undo();
                }
            } else if (event.keyCode == Keyboard.F1) {
                tabNavigator.selectedIndex = 0;
            } else if (event.keyCode == Keyboard.F2) {
                tabNavigator.selectedIndex = 1;
            } else if (event.keyCode == Keyboard.F3) {
                tabNavigator.selectedIndex = 2;
            } else if (event.keyCode == Keyboard.F4) {
                tabNavigator.selectedIndex = 3;
            } else if (event.keyCode == Keyboard.F5) {
                focusManager.setFocus(analysisState.p2);
                analysisState.openPopup();
            }
        }

        private function createFilterBoxes(filters:Array):void {
            if (filters != null) {
                for (var i:int = 0; i < filters.length; i++) {
                    var filterDefinition:FilterDefinition = filters[i];
                    transformContainer.addFilterDefinition(filterDefinition);
                }
            }
        }

        private function createFilter():void {
            var filterRawData:FilterRawData = dataView.createFilterRawData();
            dispatchEvent(new AnalysisChangedEvent());
            transformContainer.processRawFilterData(filterRawData, true);
        }

        private function getFilterDefinitions():Array {
            return transformContainer.getFilterDefinitions().toArray();
        }

        private function compositeFeedCreated(event:CompositeFeedCreatedEvent):void {
            dataService.dataFeedID = event.compositeFeedID;
            analysisDefinition.dataFeedID = event.compositeFeedID;
            dataService.reloadInitialContent(this, onMetadataReceipt);
        }

        private function exclude():void {
            var filterRawData:FilterRawData = dataView.createFilterRawData();
            transformContainer.processRawFilterData(filterRawData, false);
        }

        private function transformsUpdated(event:TransformsUpdatedEvent):void {
            analysisDefinition.filterDefinitions = event.filterDefinitions;
            dataView.retrieveData();
        }

        private function closeWindow():void {
            PopUpManager.removePopUp(this);
        }

        private function save():void {
            var user:User = User.getInstance();
            if (user == null) {
                var loginDialog:LoginDialog = LoginDialog(PopUpManager.createPopUp(this, LoginDialog, true));
                PopUpUtil.centerPopUp(loginDialog);
            } else {
                if (analysisDefinition.canSaveDirectly) {
                    var valid:Boolean = reportMetadataTab.updateReport().length == 0;
                    if (valid) {
                        dataView.updateExportMetadata();
                        ProgressAlert.alert(this, "Saving the report...", "Saved!", analysisService.saveAnalysisDefinition);
                        analysisService.saveAnalysisDefinition(analysisDefinition);
                    } else {
                        tabNavigator.selectedIndex = 0;
                        Alert.show("There are one or more errors which need to be corrected before this report can be saved.");
                    }
                } else {
                    Alert.show("You are not allowed to directly modify this report--use Save As instead.");
                }
            }
        }

        private function saved():void {
            var savedDef:AnalysisDefinition = analysisService.saveAnalysisDefinition.lastResult as AnalysisDefinition;
            AnalysisUtil.updateReport(analysisDefinition, savedDef);
            var feedFragmentObject:Object = new Object();
            feedFragmentObject.analysisID = String(analysisDefinition.analysisID);
            var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
            BrowserManager.getInstance().setFragment(feedFragmentString);
            this.analysisChanged = false;
        }

        private function saveAs():void {
            // if we're in admin mode, this should be disabled...
            var user:User = User.getInstance();
            if (user == null) {
                var loginContainer:LoginDialog = LoginDialog(PopUpManager.createPopUp(this, LoginDialog, true));
                PopUpUtil.centerPopUp(loginContainer);
            } else {
                var saveDialog:SaveDialog = SaveDialog(PopUpManager.createPopUp(this, SaveDialog, true));
                dataView.updateExportMetadata();
                saveDialog.analysisDefinition = analysisDefinition;
                saveDialog.addEventListener(SavedAnalysisEvent.SAVED_ANALYSIS, savedAnalysis);
                PopUpUtil.centerPopUp(saveDialog);
            }
        }

        private function savedAnalysis(event:SavedAnalysisEvent):void {
            //title = event.title;
            reportMetadataTab.updateName(event.title);
            var feedFragmentObject:Object = new Object();
            feedFragmentObject.analysisID = String(analysisDefinition.analysisID);
            var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
            BrowserManager.getInstance().setFragment(feedFragmentString);
            BrowserManager.getInstance().setTitle("Easy Insight - " + event.title);
            this.analysisChanged = false;
        }

        private function startLoading(event:DataServiceLoadingEvent):void {
            loadingBar.visible = true;
        }

        private function stopLoading(event:DataServiceLoadingEvent):void {
            loadingBar.visible = false;
        }

        protected function geticon(item:Object):Class {
            return item["icon"];
        }

        
        private function undo():void {
            commandProcessor.undo();
        }

        private function redo():void {
            commandProcessor.redo();
        }

        public function cleanup():void {
            removeEventListener(CommandEvent.COMMAND, onCommand);
            if(stage != null)
                stage.removeEventListener(KeyboardEvent.KEY_UP, onKey);
            removeEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
            removeEventListener(DeleteAnalysisItemEvent.DELETE_ANALYSIS_ITEM, onFieldDelete);
            removeEventListener(HierarchyDrilldownEvent.DRILLDOWN, childFilterCreation);
            removeEventListener(HierarchyRollupEvent.HIERARCHY_ROLLUP, onRollup);
            if(dataService != null) {
                dataService.removeEventListener(DataServiceLoadingEvent.LOADING_STARTED, startLoading);
                dataService.removeEventListener(DataServiceLoadingEvent.LOADING_STOPPED, stopLoading);
                dataService.removeEventListener(InvalidFieldsEvent.INVALID_FIELDS, onInvalidFields);
            }
            if(transformContainer != null) {
                transformContainer.removeEventListener(TransformsUpdatedEvent.UPDATED_TRANSFORMS, transformsUpdated);
            }
            if (dataView != null) {
                dataView.removeEventListener(DataServiceLoadingEvent.LOADING_STARTED, startLoading);
                dataView.removeEventListener(DataServiceLoadingEvent.LOADING_STOPPED, stopLoading);
                dataView.removeEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
                dataView.cleanup();
            }
        }

        private function close():void {
            if (analysisChanged) {
                Alert.show("You have unsaved changes. Are you sure you want to exit?", "Alert",
                        Alert.OK | Alert.CANCEL, null, alertListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new AnalysisCloseEvent(this));
            }
        }

        private function alertListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new AnalysisCloseEvent(this));
            }
        }


        private function subscribeToFeed():void {
            userUploadService.subscribe.send(dataService.dataFeedID);
        }

        private function subscribeToInsight():void {
            analysisService.subscribeToAnalysis.send(analysisDefinition.analysisID);
        }

        private function addInsightToGroup():void {
            var window:AddInsightToGroupWindow = AddInsightToGroupWindow(PopUpManager.createPopUp(this, AddInsightToGroupWindow));
            window.insightID = analysisDefinition.analysisID;
            window.addEventListener(SubscriptionEvent.GROUP_TO_INSIGHT, addedToGroup);
            PopUpUtil.centerPopUp(window);
        }

        private function addFeedToGroup():void {
            var window:AddFeedToGroupWindow = AddFeedToGroupWindow(PopUpManager.createPopUp(this, AddFeedToGroupWindow));
            window.feedID = dataService.dataFeedID;
            window.addEventListener(SubscriptionEvent.GROUP_TO_INSIGHT, addedToGroup);
            PopUpUtil.centerPopUp(window);
        }

        private function addedToGroup(event:SubscriptionEvent):void {

        }

        private function gotUserCapabilitiesForFeed():void {
            var userCapabilities:UserCapabilities = analysisService.getUserCapabilitiesForFeed.lastResult as UserCapabilities;
            canSubscribeToFeed = userCapabilities.feedRole > 3;
            canAddFeedToGroup = userCapabilities.groupMember;
            canMultiViewAnalysis = userCapabilities.reportingAvailable;
        }

        private function onFieldDelete(event:DeleteAnalysisItemEvent):void {
            for each (var analysisItemWrapper:AnalysisItemWrapper in availableFields) {
                if (analysisItemWrapper == event.analysisItemWrapper) {
                    availableFields.removeItemAt(availableFields.getItemIndex(analysisItemWrapper));
                    break;
                }
            }
        }

        private function gotUserCapabilitiesForInsight():void {
            var userCapabilities:UserCapabilities = analysisService.getUserCapabilitiesForInsight.lastResult as UserCapabilities;

            canSubscribeToFeed = userCapabilities.feedRole > 3;
            canSubscribeToInsight = userCapabilities.analysisRole > 3;
            canAddFeedToGroup = userCapabilities.groupMember;
            canAddInsightToGroup = userCapabilities.groupMember;
        }

        private function subscribedToFeed():void {
            canSubscribeToFeed = false;
            Alert.show("This data source will now show in your My Data page.");
        }

        private function subscribedToInsight():void {
            canSubscribeToInsight = false;
            Alert.show("This report will now show in your My Data page.");
        }

        

        

        private function onItemDoubleClick(event:ListEvent):void {
            var field:AnalysisItemWrapper = event.currentTarget.selectedItem as AnalysisItemWrapper;
            if (field.isAnalysisItem()) {
                dataView.addItem(field.analysisItem);
                dispatchEvent(new AnalysisChangedEvent(false));
            }
        }

        private function onDragComplete(event:DragEvent):void {
            event.preventDefault();
            transformContainer.closeDropMessage();
        }

        private function onDragStart():void {
            if (tabNavigator.selectedIndex == 2) {
                transformContainer.showDropMessage();
            }
        }

        private function onDragEnd():void {
            transformContainer.closeDropMessage();
        }

        private function iconFunction(item:Object):Class {
            var wrapper:AnalysisItemWrapper = item as AnalysisItemWrapper;
            return AnalysisItemTypeRenderer.getImageClass(wrapper);
        }

        private function toReports():void {
            if (analysisChanged) {
                Alert.show("You have unsaved changes. Are you sure you want to go to reports?", "Alert",
                        Alert.OK | Alert.CANCEL, null, adminAlertListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new ModuleAnalyzeEvent(new MultiReportAnalyzeSource(dataService.dataFeedID)));
            }
        }

        private function goReport():void {
            if (analysisDefinition.analysisID == 0) {
                Alert.show("You need to save this report first.");
            } else if (analysisChanged) {
                Alert.show("You have unsaved changes. Are you sure you want to go to reports?", "Alert",
                        Alert.OK | Alert.CANCEL, null, goReportListener, null, Alert.CANCEL);
            } else {
                var report:InsightDescriptor = new InsightDescriptor();
                report.dataFeedID = dataService.dataFeedID;
                report.id = analysisDefinition.analysisID;
                report.name = analysisDefinition.name;
                report.reportType = analysisDefinition.reportType;
                dispatchEvent(new ModuleAnalyzeEvent(new ReportAnalyzeSource(report)));
            }
        }

        private function goReportListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                var report:InsightDescriptor = new InsightDescriptor();
                report.dataFeedID = dataService.dataFeedID;
                report.id = analysisDefinition.analysisID;
                report.name = analysisDefinition.name;
                report.reportType = analysisDefinition.reportType;
                dispatchEvent(new ModuleAnalyzeEvent(new ReportAnalyzeSource(report)));
            }
        }

        

        private function newReport():void {
            if (analysisChanged) {
                Alert.show("You have unsaved changes. Are you sure you want to create a new report?", "Alert",
                        Alert.OK | Alert.CANCEL, null, newReportListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new ModuleAnalyzeEvent(new DescriptorAnalyzeSource(dataService.dataFeedID, dataSourceName)));
            }
        }

        private function newReportListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new ModuleAnalyzeEvent(new DescriptorAnalyzeSource(dataService.dataFeedID, dataSourceName)));
            }
        }
        ]]>
    </mx:Script>
    <mx:RemoteObject id="analysisService" destination="analysisDefinition">
        <mx:method name="saveAnalysisDefinition" result="saved()"/>
        <mx:method name="openAnalysisDefinition" result="gotReport()"/>
        <mx:method name="getUserCapabilitiesForFeed" result="gotUserCapabilitiesForFeed()"/>
        <mx:method name="getUserCapabilitiesForInsight" result="gotUserCapabilitiesForInsight()"/>
        <mx:method name="subscribeToAnalysis" result="subscribedToInsight()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="userUploadService" destination="userUpload">
        <mx:method name="subscribe" result="subscribedToFeed()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="detailService" destination="data">
        <mx:method name="getAllData" result="gotDetailData()"/>
        <mx:method name="getCredentialRequirements" result="gotCredentialRequirements()"/>
    </mx:RemoteObject>
    <mx:VBox height="100%" width="100%">
        <mx:HBox verticalAlign="middle">
            <mx:Button id="wrapButton" icon="@Embed(source='../../../assets/document_out.png')" click="close()"
               toolTip="Close"/>
                <!--<mx:VRule strokeColor="#DDDDDD" height="16"/>
                        <mx:Button id="undoButton" icon="@Embed(source='../../../assets/undo.png')" click="undo()" toolTip="Undo"/>
                        <mx:Button id="redoButton" icon="@Embed(source='../../../assets/redo.png')" click="redo()" toolTip="Redo"/>-->
            <mx:VRule strokeColor="#DDDDDD" height="16"/>
            <mx:Button id="saveButton" icon="@Embed(source='../../../assets/floppy_disk.png')" click="save()"
                       toolTip="Save" enabled="{!_previewMode}"/>
            <mx:Button id="saveAsButton" icon="@Embed(source='../../../assets/save_as.png')" click="saveAs()"
                       toolTip="Save As" enabled="{!_previewMode}"/>
            <mx:VRule strokeColor="#DDDDDD" height="16"/>
            <mx:Button id="dataSourceButton" toolTip="Create a New Report from Data Source"
                       icon="@Embed(source='../../../assets/table.png')" click="newReport()"/>
            <mx:Button id="reportsButton" icon="@Embed(source='../../../assets/tables_x16.png')" click="toReports()"
                       toolTip="View All Reports for Data Source" enabled="{!_previewMode &amp;&amp; canMultiViewAnalysis}"/>
            <mx:Button id="reportButton" icon="@Embed(source='../../../assets/media_play_green.png')" click="goReport()"
                       toolTip="View Report"/>
            <mx:Button id="adminButton" icon="@Embed(source='../../../assets/businessman_edit.png')" click="goAdmin()"
                       toolTip="Adminster"
                       visible="{admin}"/>
            <mx:VRule strokeColor="#DDDDDD" height="16"/>
            <mx:Button id="subscribeToFeedButton" icon="@Embed(source='../../../assets/folder_into.png')"
                       click="subscribeToFeed()" toolTip="Add Data Source to My Data" enabled="{canSubscribeToFeed}"/>
            <mx:Button id="groupAddFeedButton" icon="@Embed(source='../../../assets/users_into.png')"
                       click="addFeedToGroup()" toolTip="Add Data Source to Group" enabled="{canAddFeedToGroup}"/>
            <mx:Button id="subscribeToInsightButton" icon="@Embed(source='../../../assets/folder_into.png')"
                       click="subscribeToInsight()" toolTip="Add Report to My Data" enabled="{canSubscribeToInsight}"/>
            <mx:Button id="groupAddInsightButton" icon="@Embed(source='../../../assets/users_into.png')"
                       click="addInsightToGroup()" toolTip="Add Report to Group" enabled="{canAddInsightToGroup}"/>
            <mx:VRule strokeColor="#DDDDDD" height="16"/>
            <mx:ProgressBar indeterminate="true" visible="false" id="loadingBar"/>
        </mx:HBox>
        <mx:TabNavigator id="tabNavigator" width="100%" creationPolicy="all" enabled="{!_previewMode}" paddingTop="0"
                         paddingBottom="0"
                         paddingLeft="0" paddingRight="0" resizeToContent="true">
            <analysis:ReportMetadataTab id="reportMetadataTab" analysisDefinition="{analysisDefinition}" originSolution="{originSolution}"/>
            <analysis:ReportDataTab id="reportDataTab" analysisDefinition="{analysisDefinition}" dataService="{dataService}"
                    wrappers="{wrappers}" availableFields="{availableFields}" dataSourceInfo="{dataSourceInfo}"
                    dataView="{dataView}" analysisItemCreated="sortFields()"/>
            <filtering:TransformContainer id="transformContainer" paddingLeft="10" paddingTop="10" paddingBottom="10"
                                          paddingRight="10" transformAdded="transformsUpdated(event)" label="Filters (F3)"
                                          icon="@Embed(source='../../../assets/funnel.png')"
                                          width="100%" height="100%" backgroundColor="#ffffff" backgroundAlpha=".1"
                                          feedID="{dataService.dataFeedID}"
                                          analysisItems="{wrappers}">
                <mx:Button id="filterButton" icon="@Embed(source='../../../assets/funnel_new.png')"
                           toolTip="Filter Data" click="createFilter()"/>
                <mx:Button id="excludeButton" icon="@Embed(source='../../../assets/funnel_delete.png')"
                           toolTip="Exclude Data" click="exclude()"/>
            </filtering:TransformContainer>
            <analysis:ExportTab analysisDefinition="{analysisDefinition}" dataView="{dataView}"/>
        </mx:TabNavigator>

        <mx:HDividedBox id="hbox1" height="100%" width="100%">
            <mx:VBox height="100%" width="240" id="leftHandBox">

                <analysis:AnalysisStatePopupButton analysisStateChange="onAnalysisStateChange(event)" id="analysisState"
                                                   mode="all"/>

                <mx:VBox height="100%" width="100%" verticalGap="0" borderThickness="1" borderColor="#DCE2F8"
                         borderStyle="solid" dropShadowEnabled="true">
                    <mx:Box width="100%" height="5" backgroundColor="#10319A" left="0" right="0"/>
                    <mx:Box width="100%" backgroundColor="#3B56AF" horizontalAlign="center">
                        <mx:Label text="{dataSourceName}" color="0xFFFFFF"/>
                    </mx:Box>
                    <mx:AdvancedDataGrid width="100%" height="100%" dragEnabled="true" iconFunction="iconFunction"
                                         id="Measure" backgroundColor="#FFFFFF"
                                         alternatingItemColors="#FFFFFF" fontSize="12" rowHeight="28"
                                         itemDoubleClick="onItemDoubleClick(event)"
                                         dragComplete="onDragComplete(event)" mouseDown="onDragStart()"
                                         mouseUp="onDragEnd()">
                        <mx:dataProvider>
                            <mx:HierarchicalData source="{availableFields}"/>
                        </mx:dataProvider>
                        <mx:columns>
                            <mx:AdvancedDataGridColumn dataField="displayName" headerText=""/>
                        </mx:columns>
                    </mx:AdvancedDataGrid>
                </mx:VBox>
            </mx:VBox>
            <mx:VBox width="100%" height="100%" id="dataControlVBox"/>
        </mx:HDividedBox>
    </mx:VBox>
</core:FullScreenPage>
