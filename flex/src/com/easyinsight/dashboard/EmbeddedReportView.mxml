<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" xmlns:filtering="com.easyinsight.filtering.*"
         width="100%" height="100%" creationComplete="initStuff()" paddingLeft="7" paddingBottom="7" paddingRight="7" paddingTop="7"
        borderStyle="solid" borderThickness="1" cornerRadius="9">
    <mx:states>
        <mx:State name="Loading">
            <mx:AddChild relativeTo="{coreVBox}">
                <mx:Canvas width="100%" height="100%" backgroundAlpha=".1" backgroundColor="0x444444"/>
            </mx:AddChild>
            <mx:AddChild relativeTo="{coreVBox}">
                <mx:Box width="100%" height="100%" horizontalAlign="center" verticalAlign="middle">
                    <mx:ProgressBar indeterminate="true"/>
                </mx:Box>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script>
		<![CDATA[
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.EmbeddedControllerLookup;
        import com.easyinsight.analysis.EmbeddedDataServiceEvent;
        import com.easyinsight.analysis.EmbeddedViewFactory;
        import com.easyinsight.analysis.HierarchyDrilldownEvent;
        import com.easyinsight.analysis.HierarchyRollupEvent;
        import com.easyinsight.analysis.IEmbeddedReportController;
        import com.easyinsight.commands.CommandEvent;
        import com.easyinsight.commands.CommandProcessor;
        import com.easyinsight.filtering.TransformsUpdatedEvent;
        import com.easyinsight.framework.DataServiceLoadingEvent;
        import com.easyinsight.framework.HierarchyOverride;

        import mx.collections.ArrayCollection;
        import mx.controls.Label;
        import mx.rpc.events.FaultEvent;

        [Bindable]
        [Embed(source="../../../../assets/funnel.png")]
        private var filterIcon:Class;

        private var commandProcessor:CommandProcessor;

        private function onCommand(event:CommandEvent):void {
            commandProcessor.addCommand(event.command);
        }

        private function initStuff():void {
            commandProcessor = new CommandProcessor();
            addEventListener(CommandEvent.COMMAND, onCommand);
            addEventListener(HierarchyDrilldownEvent.DRILLDOWN, childFilterCreation);
            addEventListener(HierarchyRollupEvent.HIERARCHY_ROLLUP, onRollup);
        }

        private function onRollup(event:HierarchyRollupEvent):void {
            var overrideObj:HierarchyOverride = new HierarchyOverride();
            overrideObj.analysisItemID = event.hierarchy.analysisItemID;
            overrideObj.position = event.position;
            dataViewFactory.addOverride(overrideObj);
            transformContainer.clearFilter(event.analysisItem);
        }

        private function childFilterCreation(event:HierarchyDrilldownEvent):void {
            dataViewFactory.noCache = true;
            var overrideObj:HierarchyOverride = new HierarchyOverride();
            overrideObj.analysisItemID = event.hierarchyItem.analysisItemID;
            overrideObj.position = event.position;
            dataViewFactory.addOverride(overrideObj);
            transformContainer.processRawFilterData(event.filterRawData, event.type == HierarchyDrilldownEvent.DRILLDOWN);
        }

        private function toggleFilters():void {
            transformBox.selectedIndex = transformBox.selectedIndex == 0 ? 1 : 0;
        }

        [Bindable]
        private var title:String;
        [Bindable]
        private var _analysisID:int;
        private var _reportType:int;
        [Bindable]
        private var _dataSourceID:int;
        [Bindable]
        private var _reportName:String;

        private var _prefix:String = "";

        private var _titleFunction:Function;
        private var _titleFunctionCaller:Object;

        private var dataViewFactory:EmbeddedViewFactory;


        private var _filterDefinitions:ArrayCollection;


        [Bindable(event="filterDefinitionsChanged")]
        public function get filterDefinitions():ArrayCollection {
            return _filterDefinitions;
        }

        public function set filterDefinitions(value:ArrayCollection):void {
            if (_filterDefinitions == value) return;
            _filterDefinitions = value;
            dispatchEvent(new Event("filterDefinitionsChanged"));
        }

        public function set dataSourceID(val:int):void {
            _dataSourceID = val;
            invalidateProperties();
        }

        public function set prefix(val:String):void {
            _prefix = val;
            invalidateProperties();
        }

        public function set reportType(val:int):void {
            _reportType = val;
            invalidateProperties();
        }

        public function set reportName(val:String):void {
            _reportName = val;
            invalidateProperties();
        }

        public function set titleClickFunction(functionName:Function):void {
            _titleFunction = functionName;
            invalidateProperties();
        }

        public function set titleFunctionCaller(val:Object):void {
            _titleFunctionCaller = val;
            invalidateProperties();
        }

        public function set analysisID(analysisID:int):void {
            this._analysisID = analysisID;
            invalidateProperties();
        }

        private function dataLoadingEvent(event:DataServiceLoadingEvent):void {
            if (event.type == DataServiceLoadingEvent.LOADING_STARTED) {
                currentState = "Loading";
                dataViewFactory.enabled = false;
            } else {
                currentState = "";
                dataViewFactory.enabled = true;
            }
        }

        override protected function commitProperties():void {
            super.commitProperties();
            if (_reportType != 0) {
                if (dataViewFactory != null)
                    coreVBox.removeChild(dataViewFactory);

                var controllerClass:Class = EmbeddedControllerLookup.controllerForType(_reportType);
                var controller:IEmbeddedReportController = new controllerClass();
                dataViewFactory = controller.createEmbeddedView();
                dataViewFactory.prefix = _prefix;
                dataViewFactory.reportID = _analysisID;
                dataViewFactory.dataSourceID = _dataSourceID;
                coreVBox.addChild(dataViewFactory);
                dataViewFactory.addEventListener(EmbeddedDataServiceEvent.DATA_RETURNED, gotData);
                dataViewFactory.addEventListener(DataServiceLoadingEvent.LOADING_STARTED, dataLoadingEvent);
                dataViewFactory.addEventListener(DataServiceLoadingEvent.LOADING_STOPPED, dataLoadingEvent);
                dataViewFactory.retrieveData(false);
            }
        }


        private function signup(event:MouseEvent):void {
            navigateToURL(new URLRequest("https://www.easy-insight.com/app/#page=account"))
        }

        protected override function createChildren():void {
            super.createChildren();
            var signUpLabel:Label = new Label();
            signUpLabel.text = "Want to publish your own data or save your own reports?";
            addReportBox.addChild(signUpLabel);
            var signUpButton:Button = new Button();
            signUpButton.label = "Free account signup";
            signUpButton.addEventListener(MouseEvent.CLICK, signup);
            addReportBox.addChild(signUpButton);
        }

        private function transformsUpdated(event:TransformsUpdatedEvent):void {
            dataViewFactory.filterDefinitions = transformContainer.getFilterDefinitions();
            dataViewFactory.retrieveData(false);
        }

        private function gotData(event:EmbeddedDataServiceEvent):void {
            var report:AnalysisDefinition = event.analysisDefinition;
            if (report.filterDefinitions != null && report.filterDefinitions.length > 0) {
                filterDefinitions = report.filterDefinitions;
                showFilters = true;
            }
        }

        private var showFilters:Boolean = false;

        private function failure(event:FaultEvent):void {
            var failureLabel:String = event.message.toString();
            var label:Label = new Label();
            label.text = failureLabel;
            coreVBox.addChild(label);
        }

        private function returnToList():void {
            dispatchEvent(new ReturnToListEvent());
        }

        public function refreshData():void {
            dataViewFactory.retrieveData(false);
        }
                ]]>
    </mx:Script>
    <mx:HBox width="100%">
        <!--<mx:Button toolTip="Edit the Report Yourself" click="editReport()" icon=""/>-->
        <mx:Button toolTip="Adjust Filters" click="toggleFilters()" icon="{filterIcon}"/>
        <mx:HBox id="addReportBox" width="100%" horizontalAlign="center"/>
    </mx:HBox>
    <mx:ViewStack width="100%" id="transformBox" resizeToContent="true" creationPolicy="all">
        <mx:Box/>
        <filtering:EmbeddedTransformContainer id="transformContainer" filterEditable="false"
                                      existingFilters="{filterDefinitions}"
                                      updatedTransforms="transformsUpdated(event)" width="100%" paddingLeft="10"
                                      paddingTop="10" paddingBottom="10"
                                      paddingRight="10" feedID="{_dataSourceID}"/>
    </mx:ViewStack>
    <mx:Canvas id="coreVBox" height="100%" width="100%">
    </mx:Canvas>
</mx:VBox>
