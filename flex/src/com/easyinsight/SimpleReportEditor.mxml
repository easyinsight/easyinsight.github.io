<?xml version="1.0" encoding="utf-8"?>
<skin:BackgroundImage xmlns:mx="http://www.adobe.com/2006/mxml"
                      xmlns:analysis="com.easyinsight.analysis.*"
                      xmlns:suggestion="com.easyinsight.suggestion.*"
                      xmlns:report="com.easyinsight.report.*" xmlns:easyinsight="com.easyinsight.*"
                      creationComplete="initializeForWorksheet()" width="100%"
                      height="100%"
                      xmlns:filtering="com.easyinsight.filtering.*"
                      doubleClickEnabled="true"
                      implements="com.easyinsight.listing.IPerspective" xmlns:skin="com.easyinsight.skin.*"
                      applyCenterScreenLogic="false" styleName="screenBackground" minWidth="1135"
                      horizontalScrollPolicy="auto">
    <skin:states>
        <mx:State name="showSuggestions">
            <mx:AddChild>
                <suggestion:SuggestionSummary width="250" borderStyle="solid" borderThickness="1"
                                              borderColor="#666688" cornerRadius="5" horizontalAlign="center"
                                              report="{analysisDefinition}"
                                              transformContainer="{transformContainer}" fields="{wrappers}"
                                              availableFields="{availableFields}"
                                              intentionTrigger="onIntentionEvent(event)"
                                              suggestions="{suggestions}" dataSource="{dataSourceInfo}"
                                              dataView="{dataView}" backgroundColor="#CFCFDF" backgroundAlpha="1"
                                              id="suggestionSummary" simpleReportEditor="{this}"/>
            </mx:AddChild>
        </mx:State>
    </skin:states>
    <mx:Script>
		<![CDATA[
        import com.easyinsight.analysis.AddedItemUpdateEvent;
        import com.easyinsight.analysis.AddonReport;
        import com.easyinsight.analysis.AddonReportEvent;
        import com.easyinsight.analysis.AddonReportWindow;
        import com.easyinsight.analysis.AnalysisChangedEvent;
        import com.easyinsight.analysis.AnalysisCloseEvent;
        import com.easyinsight.analysis.AnalysisDefinition;
        import com.easyinsight.analysis.AnalysisItem;
        import com.easyinsight.analysis.AnalysisItemCopyEvent;
        import com.easyinsight.analysis.AnalysisItemEditEvent;
        import com.easyinsight.analysis.AnalysisItemEditWindow;
        import com.easyinsight.analysis.AnalysisItemNode;
        import com.easyinsight.analysis.AnalysisItemTypeRenderer;
        import com.easyinsight.analysis.AnalysisItemWrapper;
        import com.easyinsight.analysis.AnalysisStateChangeEvent;
        import com.easyinsight.analysis.AnalysisUtil;
        import com.easyinsight.analysis.AvailableFieldRenderer;
        import com.easyinsight.analysis.ControllerLookup;
        import com.easyinsight.analysis.DataServiceEvent;
        import com.easyinsight.analysis.DataViewFactory;
        import com.easyinsight.analysis.FeedMetadata;
        import com.easyinsight.analysis.FeedNode;
        import com.easyinsight.analysis.FieldDoubleClickEvent;
        import com.easyinsight.analysis.FieldEditorEvent;
        import com.easyinsight.analysis.GenericDefinitionEditWindow;
        import com.easyinsight.analysis.HierarchyWindow;
        import com.easyinsight.analysis.IReportController;
        import com.easyinsight.analysis.ImprovedSaveWindow;
        import com.easyinsight.analysis.JoinOverrideEvent;
        import com.easyinsight.analysis.PromptEvent;
        import com.easyinsight.analysis.ReportAuditWindow;
        import com.easyinsight.analysis.ReportEditorFieldEvent;
        import com.easyinsight.analysis.ReportEditorHelp;
        import com.easyinsight.analysis.ReportJoinWindow;
        import com.easyinsight.analysis.ReportPreferencesEvent;
        import com.easyinsight.analysis.ReportPropertiesEvent;
        import com.easyinsight.analysis.SavePromptWindow;
        import com.easyinsight.analysis.SavedAnalysisEvent;
        import com.easyinsight.analysis.list.ListController;
        import com.easyinsight.analysis.list.ListDefinition;
        import com.easyinsight.commands.CommandEvent;
        import com.easyinsight.commands.CommandProcessor;
        import com.easyinsight.dashboard.DashboardDescriptor;
        import com.easyinsight.datasources.DataSourceInfo;
        import com.easyinsight.datasources.DataSourceType;
        import com.easyinsight.filtering.FilterDefinition;
        import com.easyinsight.filtering.TransformsUpdatedEvent;
        import com.easyinsight.framework.DataService;
        import com.easyinsight.framework.DataServiceLoadingEvent;
        import com.easyinsight.framework.InvalidFieldsEvent;
        import com.easyinsight.framework.PerspectiveInfo;
        import com.easyinsight.framework.User;
        import com.easyinsight.genredata.AnalyzeEvent;
        import com.easyinsight.listing.DescriptorAnalyzeSource;
        import com.easyinsight.listing.ReportEditorAnalyzeSource;
        import com.easyinsight.report.AdditionalPropsWindow;
        import com.easyinsight.report.CustomFieldWindow;

        import com.easyinsight.report.ReportAnalyzeSource;
        import com.easyinsight.report.ReportExportWindow;
        import com.easyinsight.report.ReportNavigationEvent;
        import com.easyinsight.solutions.InsightDescriptor;
        import com.easyinsight.suggestion.IntentionListWindow;
        import com.easyinsight.suggestion.IntentionSuggestion;
        import com.easyinsight.suggestion.IntentionTriggerEvent;
        import com.easyinsight.suggestion.MajorProblemBar;
        import com.easyinsight.util.ActionDataSourceLog;
        import com.easyinsight.util.ActionReportLog;
        import com.easyinsight.util.ErrorReportView;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;
        import com.easyinsight.util.UserAudit;

        import mx.binding.utils.BindingUtils;

        import mx.binding.utils.ChangeWatcher;

        import mx.collections.ArrayCollection;
        import mx.collections.HierarchicalData;
        import mx.collections.ICollectionView;
        import mx.collections.IHierarchicalCollectionView;
        import mx.collections.Sort;
        import mx.controls.AdvancedDataGrid;
        import mx.controls.Alert;
        import mx.events.DragEvent;
        import mx.managers.BrowserManager;
        import mx.managers.PopUpManager;
        import mx.utils.ObjectUtil;
        import mx.utils.URLUtil;

        public var urlKey:String;

        [Bindable]
        public var dataService:DataService;

        [Bindable]
        private var availableFields:ArrayCollection;

        [Bindable]
        public var selectedTabIndex:int;

        private var _dataSourceInfo:DataSourceInfo;


        [Bindable(event="dataSourceInfoChanged")]
        public function get dataSourceInfo():DataSourceInfo {
            return _dataSourceInfo;
        }

        public function set dataSourceInfo(value:DataSourceInfo):void {
            if (_dataSourceInfo == value) return;
            _dataSourceInfo = value;
            dispatchEvent(new Event("dataSourceInfoChanged"));
        }

        [Bindable]
        private var selectedMode:int;

        [Bindable]
        private var joinsCustomizable:Boolean;

        private var _analysisDefinition:AnalysisDefinition;

        [Bindable]
        private var _previewMode:Boolean = false;

        [Bindable]
        private var saveAsEnabled:Boolean;

        [Bindable]
        private var admin:Boolean;

        public var filterOverrides:ArrayCollection = null;

        public var feedMetadata:FeedMetadata;

        [Bindable]
        private var dataView:DataViewFactory;

        private var commandProcessor:CommandProcessor;

        [Bindable]
        private var wrappers:ArrayCollection;

        private var analysisChanged:Boolean = false;

        [Bindable]
        private var canMultiViewAnalysis:Boolean;

        [Bindable]
        private var dataSourceName:String;

        [Bindable]
        private var fieldsLabel:String;

        [Bindable]
        public var dataSourceID:int;

        [Bindable]
        private var _reportID:int;

        [Bindable]
        private var exchangeSave:Boolean;

        [Bindable]
        private var reportName:String;

        public var openState:Object;

        private var loading:Boolean = false;

        public function onInvalidFields(event:InvalidFieldsEvent):void {
            //transformContainer.invalidateItems(event.invalidAnalysisItemIDs);
            dataView.invalidateItems(event.invalidAnalysisItemIDs);
            availableFields = new ArrayCollection();
            for each (var feedNode:FeedNode in event.feedMetadata.fieldHierarchy) {
                var analysisWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(feedNode);
                availableFields.addItem(analysisWrapper);
            }
            dataView.availableFields = availableFields;
            Alert.show("The underlying data to this data source has been changed, so some fields may have been removed from your filters or report.");
            dataView.refresh();
        }

        private function onAnalysisStateChange(event:AnalysisStateChangeEvent):void {
            dispatchEvent(new AnalysisChangedEvent());
            var controller:IReportController = event.controller;
            loadController(controller);
            analysisService.generatePossibleIntentions.send(analysisDefinition);
        }

        private function onDataReturn(event:DataServiceEvent):void {
            if (event.dataSource != null) {
                this.dataSourceInfo = event.dataSource;
            }
            assignSuggestions(event.suggestions);
            audits = event.reportAudit;
        }

        private var majorProblemBar:MajorProblemBar;

        private function assignSuggestions(suggests:ArrayCollection):void {
            var suggestions:ArrayCollection = new ArrayCollection();
            var quickActions:ArrayCollection = new ArrayCollection();
            if (suggests != null) {

                for each (var intentionSuggestion:IntentionSuggestion in suggests) {
                    if (intentionSuggestion.priority == IntentionSuggestion.PROBLEM || intentionSuggestion.priority == IntentionSuggestion.WARNING) {
                        suggestions.addItem(intentionSuggestion);
                    } else {
                        quickActions.addItem(intentionSuggestion);
                    }
                }
            }
            if (suggestions.length == 1) {
                if (majorProblemBar == null) {
                    majorProblemBar = new MajorProblemBar();
                    majorProblemBar.simpleReportEditor = this;
                    majorProblemBar.intentionSuggestion = suggestions.getItemAt(0) as IntentionSuggestion;
                    majorProblemBar.fields = getWrappers();
                    majorProblemBar.dataSourceID = dataService.dataFeedID;
                    rightBox.addChildAt(majorProblemBar, 0);
                }
            } else if (suggestions.length == 0) {
                if (majorProblemBar) {
                    majorProblemBar.parent.removeChild(majorProblemBar);
                    majorProblemBar = null;
                }
            }
            //this.suggestions = suggestions;
            this.quickActions = quickActions;
            /*if (suggestions != null && suggestions.length > 0) {
                currentState = "showSuggestions";
            } else {
                currentState = "";
            }*/
        }

        private function gotIntentions():void {
            assignSuggestions(analysisService.generatePossibleIntentions.lastResult as ArrayCollection);
        }

        [Bindable]
        private var suggestions:ArrayCollection;

        private function additionalInitialConfig(analysisDefinition:AnalysisDefinition):void {
            if (analysisDefinition is ListDefinition) {
                analysisDefinition.generalSizeLimit = feedMetadata.defaultMaxRecords;
            }

        }

        [Bindable]
        private var quickActions:ArrayCollection;

        private function populateFromMetadata(analysisDefinition:AnalysisDefinition):void {
            if (analysisDefinition is ListDefinition) {
                analysisDefinition.generalSizeLimit = feedMetadata.defaultMaxRecords;
            }
        }

        private function loadController(controller:IReportController, shareDataSourceSettings:Boolean = false):void {
            var dataViewFactory:DataViewFactory = controller.createDataView();
            dataViewFactory.availableFields = wrappers;
            if (dataView != null) {
                dataView.removeEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
                dataView.removeEventListener(DataServiceEvent.DATA_RETURNED, onDataReturn);
                dataView.removeEventListener(DataServiceLoadingEvent.LOADING_STARTED, loadingEvent);
                dataView.removeEventListener(DataServiceLoadingEvent.LOADING_STOPPED, loadingEvent);
                rightBox.removeChild(dataView);
                //if (!defaultManualRun) {
                    viewWatcher.unwatch();
                //}
            }
            if (analysisDefinition == null) {
                this.analysisDefinition = dataViewFactory.createNewDefinition();
                analysisDefinition.initialConfig();
                additionalInitialConfig(analysisDefinition);
                analysisDefinition.visibleAtFeedLevel = shareDataSourceSettings;
                analysisDefinition.dataFeedID = dataSourceID;
            } else {
                analysisDefinition = dataViewFactory.fromExistingDefinition(analysisDefinition);
                dataViewFactory.analysisDefinition = analysisDefinition;
            }
            var toRemove:ArrayCollection = analysisDefinition.cleanupReport(transformContainer.getFilterDefinitions());
            for each (var toRemoveFilter:FilterDefinition in toRemove) {
                transformContainer.removeFilter(toRemoveFilter);
            }
            var newFilters:ArrayCollection = analysisDefinition.newFilters(transformContainer.getFilterDefinitions());
            for each (var filterDefinition:FilterDefinition in newFilters) {
                transformContainer.addFilterDefinition(filterDefinition);
            }
            dataView = dataViewFactory;
            //dataView.adHocMode = !defaultManualRun;
            dataView.adHocMode = true;
            dataView.dataSourceID = dataSourceID;
            dataView.addEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
            dataView.addEventListener(DataServiceEvent.DATA_RETURNED, onDataReturn);
            dataView.addEventListener(DataServiceLoadingEvent.LOADING_STARTED, loadingEvent);
            dataView.addEventListener(DataServiceLoadingEvent.LOADING_STOPPED, loadingEvent);
            dataView.feedMetadata = feedMetadata;
            //dataView.dataSourceFields = dataSourceFields;

            //if (!defaultManualRun) {
                viewWatcher = BindingUtils.bindProperty(dataViewFactory, "adHocMode", adHocButton, "adHoc");
            /*} else {
                dataView.overlayIndex = 5;
            }*/
            rightBox.addChildAt(dataViewFactory, 1);
            dataViewFactory.refresh();
        }

        private var viewWatcher:ChangeWatcher;

        public function newAnalysis(shareDataSourceSettings:Boolean):void {
            var controller:IReportController = new ListController();
            loadController(controller, shareDataSourceSettings);
        }

        private function onFieldEditorOpen(event:FieldEditorEvent):void {
            editWindow = event.editor;
        }

        private function onFieldEditorClose(event:FieldEditorEvent):void {
            editWindow = null;
        }

        private var editWindow:AnalysisItemEditWindow;


        [Bindable(event="analysisDefinitionChanged")]
        public function get analysisDefinition():AnalysisDefinition {
            return _analysisDefinition;
        }

        public function set analysisDefinition(value:AnalysisDefinition):void {
            if (_analysisDefinition == value) return;
            _analysisDefinition = value;
            dispatchEvent(new Event("analysisDefinitionChanged"));
        }

        public function set reportID(val:int):void {
            _reportID = val;
        }

        private function onIntentionEvent(event:IntentionTriggerEvent):void {
            if (event.newField) {
                dataView.addItem(event.newField);
            } else if (event.newHierarchy) {
                var window:AnalysisItemEditWindow = new AnalysisItemEditWindow();
                window.dataSourceID = dataService.dataFeedID;
                window.editorClass = HierarchyWindow;
                window.analysisItems = wrappers;
                window.addEventListener(AnalysisItemEditEvent.ANALYSIS_ITEM_EDIT, onNewItem, false, 0, true);
                PopUpManager.addPopUp(window, this);
                PopUpUtil.centerPopUp(window);
            } else if (event.joins) {

            }
        }

        public function joinWindow():void {
            var joinWindow:ReportJoinWindow = new ReportJoinWindow();
            joinWindow.addonReports = _analysisDefinition.addonReports;
            var items:ArrayCollection = new ArrayCollection();
            for each (var wrapper:AnalysisItemWrapper in getWrappers()) {
                items.addItem(wrapper.analysisItem);
            }
            joinWindow.fields = items;
            joinWindow.dataSourceID = dataService.dataFeedID;
            joinWindow.startJoins = _analysisDefinition.joinOverrides;
            joinWindow.addEventListener(JoinOverrideEvent.JOIN_OVERRIDE_SET, onJoinOverride, false, 0, true);
            PopUpManager.addPopUp(joinWindow, this, true);
            PopUpUtil.centerPopUp(joinWindow);
            PopUpManager.removePopUp(this);
        }

        private function onJoinOverride(event:JoinOverrideEvent):void {
            _analysisDefinition.joinOverrides = event.joins;
            dataView.refresh();
        }

        private function onNewItem(event:AnalysisItemEditEvent):void {
            dispatchEvent(new AnalysisChangedEvent());
            dispatchEvent(event);
            var node:AnalysisItemNode = new AnalysisItemNode();
            node.analysisItem = event.analysisItem;
            var wrapper:AnalysisItemWrapper = new AnalysisItemWrapper(node);
            availableFields.addItem(wrapper);
            wrappers.addItem(wrapper);
            if (_analysisDefinition.addedItems == null) {
                _analysisDefinition.addedItems = new ArrayCollection();
            }
            _analysisDefinition.addedItems.addItem(event.analysisItem);
            dataView.addItem(event.analysisItem);
        }

        public function addonReportStuff():void {
            var window:AddonReportWindow = new AddonReportWindow();
            window.addEventListener(AddonReportEvent.NEW_ADDONS, onNewAddons, false, 0, true);
            window.report = analysisDefinition;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private var addonCount:int = 0;
        private var neededAddonCount:int = 0;

        private var newAddonReports:ArrayCollection;
        private var removedAddonReports:ArrayCollection;

        private function onNewAddons(event:AddonReportEvent):void {
            neededAddonCount = event.addonReports.length;
            newAddonReports = event.addonReports;
            removedAddonReports = event.removedReports;
            for each (var addonReport:AddonReport in event.addonReports) {
                boltOnFields(addonReport);
            }
            // start phase, start date, who changed it, end phase, end date time, analyst
            // total contracts in process, number of contracts expiring next quarter, total 90 day outstanding ~100
            //
            for each (var toRemove:AddonReport in event.removedReports) {
                for each (var wrapper:AnalysisItemWrapper in availableFields) {
                    if (wrapper.addonReportID == toRemove.reportID) {
                        availableFields.removeItemAt(availableFields.getItemIndex(wrapper));
                        break;
                    }
                }
                for each (var w:AnalysisItemWrapper in wrappers) {
                    if (w.addonReportID == toRemove.reportID) {
                        wrappers.removeItemAt(wrappers.getItemIndex(w));
                    }
                }
            }
        }

        private function boltOnFields(addonReport:AddonReport):void {
            coreDataService.addonFields.send(addonReport);
        }

        private function multiBolt():void {
            var nodes:ArrayCollection = coreDataService.multiAddonFields.lastResult as ArrayCollection;
            for each (var node:FeedNode in nodes) {
                var wrapper:AnalysisItemWrapper = new AnalysisItemWrapper(node);
                wrapper.addonReportID = node.addonReportID;
                availableFields.addItem(wrapper);
                for each (var child:AnalysisItemNode in node.children) {
                    var fieldWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(child);
                    //fieldWrapper.analysisItem.fieldListField = true;
                    fieldWrapper.addonReportID = node.addonReportID;
                    wrappers.addItem(fieldWrapper);
                }
            }
        }

        private function onBolt():void {
            var node:FeedNode = coreDataService.addonFields.lastResult as FeedNode;
            var wrapper:AnalysisItemWrapper = new AnalysisItemWrapper(node);
            wrapper.addonReportID = node.addonReportID;
            availableFields.addItem(wrapper);
            for each (var child:AnalysisItemNode in node.children) {
                var fieldWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(child);
                //fieldWrapper.analysisItem.fieldListField = true;
                fieldWrapper.addonReportID = node.addonReportID;
                wrappers.addItem(fieldWrapper);
            }
            addonCount++;
            if (addonCount == neededAddonCount) {
                addonCount = 0;
                var items:ArrayCollection = new ArrayCollection();
                for each (var wrapper1:AnalysisItemWrapper in wrappers) {
                    items.addItem(wrapper1.analysisItem);
                }
                analysisService.generateForAddons.send(analysisDefinition.joinOverrides, analysisDefinition.dataFeedID,
                        items, newAddonReports, removedAddonReports);
                newAddonReports = null;
                removedAddonReports = null;
                neededAddonCount = 0;
            }
        }

        public function generatedAddonJoins():void {
            var joins:ArrayCollection = analysisService.generateForAddons.lastResult as ArrayCollection;
            analysisDefinition.joinOverrides = joins;
        }

        public var embedded:Boolean;

        /*[Bindable]
        private var defaultManualRun:Boolean;

        private var dataSourceFields:ArrayCollection;*/

        private function onMetadataReceipt(feedMetadata:FeedMetadata):void {
            try {
                dataSourceID = feedMetadata.dataFeedID;
                this.feedMetadata = feedMetadata;
                dataSourceInfo = feedMetadata.dataSourceInfo;
                this.exchangeSave = feedMetadata.exchangeSave;
                //this.dataSourceFields = feedMetadata.dataSourceFields;
                this.admin = feedMetadata.dataSourceAdmin;
                var cFactory:ClassFactory = new ClassFactory(AvailableFieldRenderer);
                cFactory.properties = { dataSourceID:dataSourceID, calcRefactor:feedMetadata.allowRefactor };
                rendererFactory = cFactory;
                dataSourceName = this.feedMetadata.dataSourceName;
                joinsCustomizable = this.feedMetadata.customJoinsAllowed;
                /*defaultManualRun = feedMetadata.defaultManualRun;
                if (defaultManualRun) {
                    adHocButton.parent.removeChild(adHocButton);
                } else {
                    runReportButton.parent.removeChild(runReportButton);
                    runReportSpacer.parent.removeChild(runReportSpacer);
                }*/

                fieldsLabel = this.feedMetadata.dataSourceName + " Fields";
                availableFields = new ArrayCollection();
                wrappers = new ArrayCollection();
                for each (var feedNode:FeedNode in feedMetadata.fieldHierarchy) {
                    var analysisWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(feedNode);
                    if (!analysisWrapper.hidden) {
                        availableFields.addItem(analysisWrapper);
                    }
                }
                for each (var listItem:AnalysisItem in feedMetadata.fields) {
                    var analysisNode:AnalysisItemNode = new AnalysisItemNode();
                    analysisNode.analysisItem = listItem;
                    //listItem.fieldListField = true;
                    var wrapper:AnalysisItemWrapper = new AnalysisItemWrapper(analysisNode);
                    wrappers.addItem(wrapper);
                }

                if (analysisDefinition == null || analysisDefinition.analysisID == 0) {
                    var fragmentObject:Object = new Object();
                    fragmentObject.feedID = String(feedMetadata.urlKey);
                    if (!embedded) {
                        var fragmentString:String = URLUtil.objectToString(fragmentObject);
                        BrowserManager.getInstance().setTitle("Easy Insight - " + dataSourceName);
                        BrowserManager.getInstance().setFragment(fragmentString);
                    }
                    UserAudit.instance().log(new ActionDataSourceLog(ActionDataSourceLog.NEW_REPORT, dataSourceID));
                } else {
                    var feedFragmentObject:Object = new Object();
                    feedFragmentObject.analysisID = String(analysisDefinition.urlKey);
                    if (!embedded) {
                        var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
                        BrowserManager.getInstance().setTitle("Easy Insight - " + analysisDefinition.name);
                        BrowserManager.getInstance().setFragment(feedFragmentString);
                    }
                    UserAudit.instance().log(new ActionReportLog(ActionReportLog.EDIT, analysisDefinition.analysisID));
                }

                if (analysisDefinition == null) {
                    saveAsEnabled = false;
                    newAnalysis(User.getInstance().defaultReportSharing);
                    if (feedMetadata.dataSourceType == DataSourceType.COMPOSITE) {
                        analysisDefinition.optimized = true;
                    }
                    for each (var filter:FilterDefinition in feedMetadata.intrinsicFilters) {
                        transformContainer.addFilterDefinition(filter);
                    }
                    assignSuggestions(feedMetadata.suggestions);

                } else {
                    //this.title = analysisDefinition.name;
                    if (analysisDefinition.addonReports != null && analysisDefinition.addonReports.length > 0) {
                        coreDataService.multiAddonFields.send(analysisDefinition);
                    }
                    reportName = analysisDefinition.name;
                    saveAsEnabled = !_previewMode;
                    var controllerClass:Class = ControllerLookup.controllerForType(analysisDefinition.type);
                    var controller:IReportController = new controllerClass();


                    analysisState.selectedLabel = analysisDefinition.type;


                    if (analysisDefinition.filterDefinitions == null) {
                        analysisDefinition.filterDefinitions = new ArrayCollection();
                    }
                    if (filterOverrides != null) {
                        var transformedFilters:ArrayCollection = new ArrayCollection();
                        for each (var overrideFilter:FilterDefinition in filterOverrides) {
                            for each (var reportFilter:FilterDefinition in analysisDefinition.filterDefinitions) {
                                if (reportFilter.filterID == overrideFilter.filterID) {
                                    overrideFilter.updateFromReportView(reportFilter);
                                    transformedFilters.addItem(overrideFilter);
                                }
                            }
                        }
                        analysisDefinition.filterDefinitions = transformedFilters;
                    }
                    createFilterBoxes(analysisDefinition.filterDefinitions.toArray());

                    loadController(controller);

                    // add the fields which are specifically added in the context of the report, such as
                    // calculations and hierarchies
                    if (analysisDefinition.addedItems != null) {
                        for each (var item:AnalysisItem in analysisDefinition.addedItems) {
                            var analysisItemNode:AnalysisItemNode = new AnalysisItemNode();
                            analysisItemNode.analysisItem = item;
                            var addedWrapper:AnalysisItemWrapper = new AnalysisItemWrapper(analysisItemNode);
                            availableFields.addItem(addedWrapper);
                            wrappers.addItem(addedWrapper);
                        }
                        sortFields();
                    }
                }
                dataView.availableFields = wrappers;
                var f:FixedHierarchicalCollectionView = new FixedHierarchicalCollectionView(new HierarchicalData(availableFields));
                f.filterFunction = filterFunction;
                fieldGrid.dataProvider = f;
                if (openState != null) {
                    for each (var availableWrapper:AnalysisItemWrapper in availableFields) {
                        var open:Boolean = openState[availableWrapper.displayName];
                        if (open) {
                            fieldGrid.expandItem(availableWrapper, true);
                        }
                    }
                }
                loading = false;
            } catch (e:Error) {
                var errorWindow:ErrorReportView = new ErrorReportView();
                errorWindow.error = e;
                PopUpManager.addPopUp(errorWindow, this, true);
                PopUpUtil.centerPopUp(errorWindow);
            }
        }

        private function preserveOpenState():Object {
            var openState:Object = new Object();
            for each (var obj:AnalysisItemWrapper in availableFields) {
                openState[obj.displayName] = fieldGrid.isItemOpen(obj);
            }
            return openState;
        }

        private function sortFields():void {
            var sort:Sort = new Sort();
            sort.compareFunction = customSort;
            availableFields.sort = sort;
            availableFields.refresh();
            wrappers.sort = sort;
            wrappers.refresh();
        }

        private static function customSort(obj1:Object, obj2:Object, fields:Array = null):int {
            var wrapper1:AnalysisItemWrapper = obj1 as AnalysisItemWrapper;
            var wrapper2:AnalysisItemWrapper = obj2 as AnalysisItemWrapper;
            if (wrapper1.isAnalysisItem() && !wrapper2.isAnalysisItem()) {
                return 1;
            } else if (!wrapper1.isAnalysisItem() && wrapper2.isAnalysisItem()) {
                return -1;
            } else {
                return ObjectUtil.stringCompare(wrapper1.displayName, wrapper2.displayName, true);
            }
        }

        public function getAnalysisItems():ArrayCollection {
            var analysisItems:ArrayCollection = new ArrayCollection();
            for each (var analysisItemWrapper:AnalysisItemWrapper in wrappers) {
                analysisItems.addItem(analysisItemWrapper.analysisItem);
            }
            return analysisItems;
        }

        private function onCommand(event:CommandEvent):void {
            commandProcessor.addCommand(event.command);
        }

        private function onAnalysisChanged(event:AnalysisChangedEvent):void {
            this.analysisChanged = true;
        }

        [Bindable]
        private var loadingReport:Boolean = false;

        private function loadingEvent(event:DataServiceLoadingEvent):void {
            loadingReport = event.type == DataServiceLoadingEvent.LOADING_STARTED;
        }

        private function refactorToDataSource(event:ReportEditorFieldEvent):void {

            var window:SavePromptWindow = new SavePromptWindow();
            window.prompt = "Do you want to promote this field to the data source level, making it available to all reports on this data source?";
            window.defineOption("Promote to Data Source Field", "promoteToDataSource");
            window.defineOption("Cancel", "leaveSettings");
            var parent:UIComponent = this;
            window.addEventListener("promoteToDataSource", function (promoteEvent:Event):void {
                ProgressAlert.alert(parent, "Updating reports...", null, feedService.fieldToDataSourceLevel);
                feedService.fieldToDataSourceLevel.send(analysisDefinition, event.item.analysisItem, dataService.dataFeedID);
            });
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function promoted():void {
            var report:AnalysisDefinition = feedService.fieldToDataSourceLevel.lastResult as AnalysisDefinition;
            dispatchEvent(new AnalyzeEvent(new ReportEditorAnalyzeSource(report, 0)));
        }

        public function initializeForWorksheet():void {
            try {
                if (User.getInstance().reportMode) {
                    reportMode = "all";
                } else {
                    reportMode = "simplified";
                }
                if (dataService == null) {
                    dataService = new DataService();
                    dataService.dataFeedID = dataSourceID;
                }
                loading = true;
                commandProcessor = new CommandProcessor();
                addEventListener("explainReport", explain);
                addEventListener(FieldEditorEvent.FIELD_EDITOR_OPENED, onFieldEditorOpen);
                addEventListener(FieldEditorEvent.FIELD_EDITOR_CLOSED, onFieldEditorClose);
                addEventListener(ReportNavigationEvent.TO_REPORT, reportNavigation);
                addEventListener(ReportPropertiesEvent.REPORT_PROPERTIES, onReportPropertiesEvent);
                addEventListener(ReportEditorFieldEvent.ITEM_ADD_TO_REPORT, addFieldToReport);
                addEventListener(ReportEditorFieldEvent.ITEM_REFACTOR_TO_DATA_SOURCE, refactorToDataSource);
                addEventListener(ReportEditorFieldEvent.ITEM_COPY, fieldEventHandler.copyField);
                addEventListener(ReportEditorFieldEvent.ITEM_DELETE, fieldEventHandler.deleteField);
                addEventListener(ReportEditorFieldEvent.ITEM_EDIT, fieldEventHandler.editField);
                addEventListener(ReportEditorFieldEvent.ITEM_FILTER, filterField);
                addEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
                addEventListener(AnalysisItemCopyEvent.ITEM_COPY, onCopy);

                addEventListener(AddedItemUpdateEvent.UPDATE, onFieldUpdate);
                addEventListener(AnalysisItemDeleteEvent.ANALYSIS_ITEM_DELETE, onAnalysisItemDelete);
                addEventListener(CommandEvent.COMMAND, onCommand);
                addEventListener(FieldDoubleClickEvent.FIELD_DOUBLE_CLICK, onItemDoubleClick);
                dataService.addEventListener(InvalidFieldsEvent.INVALID_FIELDS, onInvalidFields);
                transformContainer.addEventListener(TransformsUpdatedEvent.UPDATED_TRANSFORMS, transformsUpdated);

                try {
                    var hints:String = User.getSharedObject().data["reportHints"];
                    if (hints == null || hints == "y") {
                        fieldIndex = 1;
                    } else {
                        fieldIndex = 0;
                    }
                } catch (e:Error) {
                }
                if (_reportID > 0) {
                    ProgressAlert.alert(this, "Retrieving the report...", null, analysisService.openAnalysisDefinition);
                    analysisService.openAnalysisDefinition.send(_reportID);
                } else if (analysisDefinition != null) {
                    _reportID = analysisDefinition.analysisID;
                    dataService.dataFeedID = analysisDefinition.dataFeedID;
                    BrowserManager.getInstance().setTitle("Easy Insight - " + this.analysisDefinition.name);
                    if (feedMetadata == null) {
                        initializeState();
                    } else {
                        onMetadataReceipt(feedMetadata);
                    }
                } else {
                    initializeState();
                }
            } catch (e:Error) {
                var errorWindow:ErrorReportView = new ErrorReportView();
                errorWindow.error = e;
                PopUpManager.addPopUp(errorWindow, this, true);
                PopUpUtil.centerPopUp(errorWindow);
            }
        }

        public function onCopy(event:AnalysisItemCopyEvent):void {
            dispatchEvent(new AnalysisChangedEvent());
            var wrapper:AnalysisItemWrapper;
            if (event.wrapper == null) {
                var node:AnalysisItemNode = new AnalysisItemNode();
                node.analysisItem = event.analysisItem;
                wrapper = new AnalysisItemWrapper(node);
            } else {
                wrapper = event.wrapper;
            }

            availableFields.addItem(wrapper);
            wrappers.addItem(wrapper);
            if (analysisDefinition.addedItems == null) {
                analysisDefinition.addedItems = new ArrayCollection();
            }
            analysisDefinition.addedItems.addItem(event.analysisItem);
        }

        private function addFieldToReport(event:ReportEditorFieldEvent):void {
            dataView.addItem(event.item.analysisItem);
            dispatchEvent(new AnalysisChangedEvent(false));
        }

        private function filterField(event:ReportEditorFieldEvent):void {
            transformContainer.createNewFilter(event.item.analysisItem, event.x, event.y);

        }

        private function gotReport():void {
            this.analysisDefinition = analysisService.openAnalysisDefinition.lastResult as AnalysisDefinition;
            BrowserManager.getInstance().setTitle("Easy Insight - " + this.analysisDefinition.name);
            initializeState();
        }

        private function initializeState():void {
            dataService.reloadInitialContent(this, onMetadataReceipt);
        }

        private function createFilterBoxes(filters:Array):void {
            if (filters != null) {
                transformContainer.loadingFromReport = true;
                for (var i:int = 0; i < filters.length; i++) {
                    var filterDefinition:FilterDefinition = filters[i];
                    transformContainer.addFilterDefinition(filterDefinition);
                }
                transformContainer.loadingFromReport = false;
            }
        }

        private function getFilterDefinitions():Array {
            return transformContainer.getFilterDefinitions().toArray();
        }

        private function transformsUpdated(event:TransformsUpdatedEvent):void {
            if (analysisDefinition != null) {
                analysisDefinition.filterDefinitions = event.filterDefinitions;
            }
            if (dataView != null) {
                dataView.refresh();
            }
        }

        private function save(closeAfterSave:Boolean = false, saveHandler:Function = null):void {
            dataView.updateExportMetadata();
            var window:ImprovedSaveWindow = new ImprovedSaveWindow();
            if (saveHandler == null) {
                saveHandler = saved;
            }
            window.closeAfterSave = closeAfterSave;
            window.addEventListener(SavedAnalysisEvent.SAVED_ANALYSIS, saveHandler, false, 0, true);
            window.coreView = dataView.getCoreView();
            window.report = analysisDefinition;
            window.exchangeSave = exchangeSave;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }



        private function saved(event:SavedAnalysisEvent):void {
            AnalysisUtil.updateReport(analysisDefinition, event.report);
            if (!embedded) {
                var feedFragmentObject:Object = new Object();
                feedFragmentObject.analysisID = String(analysisDefinition.urlKey);
                var feedFragmentString:String = URLUtil.objectToString(feedFragmentObject);
                BrowserManager.getInstance().setFragment(feedFragmentString);
                BrowserManager.getInstance().setTitle("Easy Insight - " + event.report.name);
            }
            reportName = event.report.name;


            this.analysisChanged = false;
            saveAsEnabled = true;
            invalidateDisplayList();
        }

        public var reportURLKey:String;

        private function undo():void {
            commandProcessor.undo();
        }

        private function redo():void {
            commandProcessor.redo();
        }

        public function cleanup():void {
            removeEventListener(CommandEvent.COMMAND, onCommand);
            removeEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
            if (dataService != null) {
                dataService.removeEventListener(InvalidFieldsEvent.INVALID_FIELDS, onInvalidFields);
            }
            if (transformContainer != null) {
                transformContainer.removeEventListener(TransformsUpdatedEvent.UPDATED_TRANSFORMS, transformsUpdated);
            }
            if (dataView != null) {
                dataView.removeEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged);
                dataView.removeEventListener(DataServiceEvent.DATA_RETURNED, onDataReturn);
                dataView.cleanup();
            }
        }

        private function close():void {
            if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    dispatchEvent(new AnalysisCloseEvent(feedMetadata.urlKey));
                });
            } else {
                dispatchEvent(new AnalysisCloseEvent(feedMetadata.urlKey));
            }
        }

        private function createNoDiscardPromptWindow(prompt:String, doneHandler:Function):void {
            var window:SavePromptWindow = new SavePromptWindow();
            window.defineOption("Save", PromptEvent.PROMPT_SAVE);
            window.defineOption("Cancel", PromptEvent.PROMPT_CANCEL);
            window.prompt = prompt;
            window.addEventListener(PromptEvent.PROMPT_SAVE, function (event:PromptEvent):void {
                save(true, function (event:SavedAnalysisEvent):void {
                    AnalysisUtil.updateReport(analysisDefinition, event.report);
                    doneHandler.call();
                });
            }, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function createPromptWindow(prompt:String, doneHandler:Function):void {
            var window:SavePromptWindow = new SavePromptWindow();
            window.prompt = prompt;
            window.addEventListener(PromptEvent.PROMPT_SAVE, function (event:PromptEvent):void {
                save(true, function (event:SavedAnalysisEvent):void {
                    AnalysisUtil.updateReport(analysisDefinition, event.report);
                    doneHandler.call();
                });
            }, false, 0, true);
            window.addEventListener(PromptEvent.PROMPT_DISCARD, function (event:PromptEvent):void {
                doneHandler.call();
            }, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onFieldUpdate(event:AddedItemUpdateEvent):void {
            if (availableFields.getItemIndex(event.wrapper) != -1) {
                availableFields.removeItemAt(availableFields.getItemIndex(event.wrapper));
            }
            availableFields.addItem(event.wrapper);
            if (analysisDefinition.addedItems.getItemIndex(event.previousItem) != -1) {
                analysisDefinition.addedItems.removeItemAt(analysisDefinition.addedItems.getItemIndex(event.previousItem));
            }
            analysisDefinition.addedItems.addItem(event.newItem);
        }

        private function onItemDoubleClick(event:FieldDoubleClickEvent):void {
            var field:AnalysisItemWrapper = event.analysisItem;
            if (field.isAnalysisItem()) {
                dataView.addItem(field.analysisItem);
                dispatchEvent(new AnalysisChangedEvent(false));
            } else {
                var open:Boolean = fieldGrid.isItemOpen(field);
                fieldGrid.expandItem(field, !open);
            }
        }

        private function onDragComplete(event:DragEvent):void {
            event.preventDefault();
            dataView.revertDropAreas();
            if (editWindow != null) {
                editWindow.normal();
            }
        }

        private function onDragStart(event:DragEvent):void {
            var analysisItemLabel:AdvancedDataGrid = event.dragInitiator as AdvancedDataGrid;
            var wrapper:AnalysisItemWrapper = analysisItemLabel.selectedItem as AnalysisItemWrapper;
            if (wrapper.analysisItem) {
                if (editWindow == null) {
                    dataView.highlightDropAreas(wrapper.analysisItem);
                } else {
                    editWindow.highlight();
                }
            }
        }

        private function onDragEnd():void {
            dataView.revertDropAreas();
            if (editWindow != null) {
                editWindow.normal();
            }
        }

        private static function iconFunction(item:Object):Class {
            var wrapper:AnalysisItemWrapper = item as AnalysisItemWrapper;
            return AnalysisItemTypeRenderer.getImageClass(wrapper);
        }

        private function goReport():void {
            if (analysisDefinition.analysisID == 0) {
                createNoDiscardPromptWindow("You have unsaved changes to this report.", function ():void {
                    reportView();
                });
            } else if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    reportView();
                });
            } else {
                reportView();
            }
        }

        private function reportNavigation(event:ReportNavigationEvent):void {
            if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    if (event.descriptor is InsightDescriptor) {
                        dispatchEvent(new AnalyzeEvent(new ReportAnalyzeSource(InsightDescriptor(event.descriptor), event.filters)));
                    } else if (event.descriptor is DashboardDescriptor) {
                        dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:DashboardDescriptor(event.descriptor).id,
                            initialFilters:event.filters})));
                    }
                });
            } else {
                if (event.descriptor is InsightDescriptor) {
                    dispatchEvent(new AnalyzeEvent(new ReportAnalyzeSource(InsightDescriptor(event.descriptor), event.filters)));
                } else if (event.descriptor is DashboardDescriptor) {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_VIEW, {dashboardID:DashboardDescriptor(event.descriptor).id,
                        initialFilters:event.filters})));
                }
            }
        }

        private function reportView():void {
            var report:InsightDescriptor = new InsightDescriptor();
            report.dataFeedID = dataSourceID;
            report.id = analysisDefinition.analysisID;
            report.name = analysisDefinition.name;
            report.reportType = analysisDefinition.reportType;
            report.urlKey = analysisDefinition.urlKey;
            dispatchEvent(new AnalyzeEvent(new ReportAnalyzeSource(report)));
        }

        private function newReport():void {
            if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(dataSourceID, feedMetadata.urlKey)));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(dataSourceID, feedMetadata.urlKey)));
            }
        }

        private function newDashboard():void {
            if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_EDITOR, {startDataSourceID:dataSourceID, urlKey: feedMetadata.urlKey})));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DASHBOARD_EDITOR, {startDataSourceID:dataSourceID, urlKey: feedMetadata.urlKey})));
            }
        }

        private function onAnalysisItemDelete(event:AnalysisItemDeleteEvent):void {
            var wrapperIndex:int = wrappers.getItemIndex(event.analysisItem);
            if (wrapperIndex != -1) {
                wrappers.removeItemAt(wrapperIndex);
            }
            var availableFieldIndex:int = availableFields.getItemIndex(event.analysisItem);
            if (availableFieldIndex != -1) {
                availableFields.removeItemAt(availableFieldIndex);
            }
            var addedIndex:int = analysisDefinition.addedItems.getItemIndex(event.analysisItem.analysisItem);
            if (addedIndex != -1) {
                analysisDefinition.addedItems.removeItemAt(addedIndex);
            }
        }

        /*private function getWrappers():ArrayCollection {
            var ret:ArrayCollection = new ArrayCollection();
            for each (var w:AnalysisItemWrapper in wrappers) {
                var node:AnalysisItemNode = new AnalysisItemNode();
                node.analysisItem = w.analysisItem.copy();
                var copy:AnalysisItemWrapper = new AnalysisItemWrapper(node);
                ret.addItem(copy);
            }
            return ret;
        }*/

        private function getWrappers():ArrayCollection {
            return new ArrayCollection(wrappers.toArray());
        }


        private function editReportProperties(index:int = 0):void {
            var window:GenericDefinitionEditWindow = new GenericDefinitionEditWindow();
            window.definition = analysisDefinition;
            window.allFields = getWrappers();
            window.startIndex = index;
            window.addEventListener(ReportPreferencesEvent.REPORT_PREFERENCES, function (event:ReportPreferencesEvent):void {
                dispatchEvent(new AnalysisChangedEvent());
                if (event.refreshData) {
                    dataView.forceRetrieve();
                } else {
                    dataView.rerender();
                }
            }, false, 0, true);
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function onReportPropertiesEvent(event:ReportPropertiesEvent):void {
            editReportProperties(event.startIndex);
        }

        private function tutorial():void {
            var window:ReportEditorHelp = new ReportEditorHelp();
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        [Bindable]
        private var fieldsIndex:int = 0;

        [Bindable]
        private var fieldIndex:int = 0;

        [Bindable]
        private var rendererFactory:IFactory;

        public function gotFocus():void {
        }


        override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void {
            super.updateDisplayList(unscaledWidth, unscaledHeight);
            if (suggestionSummary != null && currentState == "showSuggestions") {
                var position:int = 8;
                suggestionSummary.x = unscaledWidth - suggestionSummary.width - 10;
                suggestionSummary.y = position;
                transformContainer.percentWidth = NaN;
                transformContainer.width = unscaledWidth - suggestionSummary.width - 10;
                reportButtonsBox.percentWidth = NaN;
                reportButtonsBox.width = rightBox.width - suggestionSummary.width - 15;
            } else {
                transformContainer.width = NaN;
                transformContainer.percentWidth = 100;
                //reportLabel.percentWidth = 100;
                reportButtonsBox.width = NaN;
                reportButtonsBox.percentWidth = 100;
            }
            if (shape2 == null && uiComp2 != null && uiComp2.width > 0 && uiComp2.height > 0) {
                shape2 = new Shape();
                shape2.graphics.beginFill(0x666666, 1);
                shape2.graphics.drawRect(0, 0, uiComp2.width, uiComp2.height);
                shape2.graphics.endFill();
                uiComp2.addChild(shape2);
            }
        }

        private var reportNameShown:Boolean = false;
        private var shape:Shape;
        private var shape2:Shape;
        private var shape3:Shape;

        private function saveAs():void {
            var saveDialog:SaveDialog = SaveDialog(PopUpManager.createPopUp(this, SaveDialog, true));
            dataView.updateExportMetadata();
            saveDialog.analysisDefinition = analysisDefinition;
            saveDialog.addEventListener(SavedAnalysisEvent.SAVED_ANALYSIS, saveAsReport);
            PopUpUtil.centerPopUp(saveDialog);
        }

        private function saveAsReport(event:SavedAnalysisEvent):void {
            dispatchEvent(new AnalyzeEvent(new ReportEditorAnalyzeSource(event.report, 0, feedMetadata, preserveOpenState())));
        }

        private function exportReport():void {
            dataView.updateExportMetadata();
            var window:ReportExportWindow = new ReportExportWindow();
            window.report = analysisDefinition;
            window.coreView = dataView.getCoreView();
            PopUpManager.addPopUp(window, this);
            window.x = exportButton.x - 160;
            window.y = exportButton.y + exportButton.height + 10;
        }

        private function additionalProps():void {
            var window:AdditionalPropsWindow = new AdditionalPropsWindow();
            window.reportEditor = this;
            window.dataSourceID = dataService.dataFeedID;
            window.analysisDefinition = analysisDefinition;
            window.dataView = dataView;
            window.wrappers = getWrappers();
            window.joinsCustomizable = joinsCustomizable;
            window.addEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged, false, 0, true);
            window.addEventListener(AnalyzeEvent.ANALYZE, onRevision, false, 0, true);
            PopUpManager.addPopUp(window, this);
            window.x = propsButton.x - 160;
            window.y = propsButton.y + customFieldButton.height + 10;
        }

        private function onRevision(event:AnalyzeEvent):void {
            dispatchEvent(event);
        }

        private function customField():void {
            var window:CustomFieldWindow = new CustomFieldWindow();
            window.dataSourceID = dataService.dataFeedID;
            window.analysisDefinition = analysisDefinition;
            window.wrappers = getWrappers();
            window.availableFields = availableFields;
            window.addEventListener(FieldEditorEvent.FIELD_EDITOR_OPENED, onFieldEditorOpen, false, 0, true);
            window.addEventListener(FieldEditorEvent.FIELD_EDITOR_CLOSED, onFieldEditorClose, false, 0, true);
            window.addEventListener(ReportEditorFieldEvent.ITEM_COPY, fieldEventHandler.copyField, false, 0, true);
            window.addEventListener(AnalysisChangedEvent.ANALYSIS_CHANGED, onAnalysisChanged, false, 0, true);
            window.addEventListener(AnalysisItemEditEvent.ANALYSIS_ITEM_EDIT, onAnalysisItemEdit, false, 0, true);
            PopUpManager.addPopUp(window, this);
            window.x = customFieldButton.x - 20;
            window.y = customFieldButton.y + customFieldButton.height + 10;
        }

        private function onAnalysisItemEdit(event:AnalysisItemEditEvent):void {
            var node:AnalysisItemNode = new AnalysisItemNode();
            node.analysisItem = event.analysisItem;
            var wrapper:AnalysisItemWrapper = new AnalysisItemWrapper(node);
            availableFields.addItem(wrapper);
            wrappers.addItem(wrapper);
            if (_analysisDefinition.addedItems == null) {
                _analysisDefinition.addedItems = new ArrayCollection();
            }
            _analysisDefinition.addedItems.addItem(event.analysisItem);
        }

        private function onViewInHTML():void {
            if (analysisDefinition.analysisID == 0) {
                createNoDiscardPromptWindow("You have unsaved changes to this report.", function ():void {
                    navigateToURL(new URLRequest("/app/html/report/" + analysisDefinition.urlKey), "_blank");
                });
            } else if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    navigateToURL(new URLRequest("/app/html/report/" + analysisDefinition.urlKey), "_blank");
                });
            } else {
                navigateToURL(new URLRequest("/app/html/report/" + analysisDefinition.urlKey), "_blank");
            }
        }

        private function goAdmin():void {
            if (analysisChanged) {
                createPromptWindow("You have unsaved changes to this report.", function ():void {
                    dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DATA_SOURCE_ADMIN, {feedID:dataSourceID , urlKey: feedMetadata.urlKey})));
                });
            } else {
                dispatchEvent(new AnalyzeEvent(new PerspectiveInfo(PerspectiveInfo.DATA_SOURCE_ADMIN, {feedID:dataSourceID , urlKey: feedMetadata.urlKey})));
            }
        }

        /*private function onChange():void {
         IHierarchicalCollectionView(fieldGrid.dataProvider).refresh();
         }

         private function onDataChange():void {
         IHierarchicalCollectionView(fieldGrid.dataProvider).filterFunction = filterFields;
         }*/

        /*private function filterFields(item:Object):Boolean {
         if (findFieldInput.text != null && findFieldInput.text.length > 0) {
         var wrapper:AnalysisItemWrapper = item as AnalysisItemWrapper;
         var children:ICollectionView = IHierarchicalCollectionView(fieldGrid.dataProvider).getChildren(wrapper);
         children.filterFunction = filterFields;
         children.refresh();
         if (children.length > 0 || wrapper.displayName.toLowerCase().indexOf(findFieldInput.text.toLowerCase()) != -1) {
         return true;
         } else {
         return false;
         }
         }
         return true;
         }*/

        [Bindable]
        private var reportMode:String = "all";

        private function filterFunction(item:Object):Boolean {
            var a:AnalysisItemWrapper = item as AnalysisItemWrapper;
            if (a == null) return false;
            if (a.displayName == null) return false;
            var view:IHierarchicalCollectionView = fieldGrid.dataProvider as IHierarchicalCollectionView;
            if (view == null) return false;
            var children:ICollectionView = view.getChildren(a);
            if (children == null) return false;
            if (a.analysisItem == null) {
                children.filterFunction = filterFunction;
                children.refresh();
            }
            return children.length > 0 || a.displayName.toLowerCase().indexOf(searchText.toLowerCase()) != -1;

        }


        private var searchText:String = "";

        private function onSearchTextChange(event:Event):void {
            searchText = searchBox.text;
            fieldGrid.dataProvider.refresh();
        }

        private function showQuickActions():void {
            var items:ArrayCollection = new ArrayCollection();
            for each (var wrapper:AnalysisItemWrapper in getWrappers()) {
                items.addItem(wrapper.analysisItem);
            }
            var window:IntentionListWindow = new IntentionListWindow();
            window.addEventListener(IntentionTriggerEvent.INTENTION_TRIGGER, onIntentionEvent);
            window.simpleReportEditor = this;
            window.report = analysisDefinition;
            window.fields = items;
            window.allFields = availableFields;
            window.dataSourceDisplay = dataSourceInfo;
            window.wrappers = getWrappers();
            window.transformContainer = transformContainer;
            window.dataView = dataView;
            window.suggestions = quickActions;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }

        private function runReport():void {
            dataView.forceRetrieve();
        }

        private var audits:ArrayCollection;

        private function explain(event:Event):void {
            var window:ReportAuditWindow = new ReportAuditWindow();
            window.audits = audits;
            PopUpManager.addPopUp(window, this, true);
            PopUpUtil.centerPopUp(window);
        }
        ]]>
    </mx:Script>
    <mx:RemoteObject id="analysisService" destination="analysisDefinition">
        <mx:method name="openAnalysisDefinition" result="gotReport()"/>
        <mx:method name="generatePossibleIntentions" result="gotIntentions()"/>
        <mx:method name="generateForAddons" result="generatedAddonJoins()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="feedService" destination="feeds">
        <mx:method name="fieldToDataSourceLevel" result="promoted()"/>
    </mx:RemoteObject>
    <mx:RemoteObject id="coreDataService" destination="data">
        <mx:method name="addonFields" result="onBolt()"/>
        <mx:method name="multiAddonFields" result="multiBolt()"/>
    </mx:RemoteObject>
    <analysis:AnalysisItemEventHandler id="fieldEventHandler" dataSourceID="{dataSourceID}" analysisItems="{wrappers}"
                                       report="{analysisDefinition}"/>
    <mx:HBox width="100%" styleName="topButtonBar">
        <mx:Button id="wrapButton" icon="@Embed(source='../../../assets/document_out.png')" click="close()"
                   toolTip="Close" labelPlacement="right" label="Close Editor" x="10" styleName="grayButton"/>
        <mx:Button id="saveButton" icon="@Embed(source='../../../assets/floppy_disk.png')" click="save()"
                   toolTip="Save" enabled="{!_previewMode &amp;&amp; !loadingReport}" label="Save"
                   labelPlacement="right" styleName="grayButton"/>
        <mx:Button id="saveAsButton" icon="@Embed(source='../../../assets/save_as.png')" click="saveAs()"
                   toolTip="Save As" enabled="{saveAsEnabled &amp;&amp; !loadingReport}" label="Save As"
                   labelPlacement="right" styleName="grayButton"/>
        <report:RefreshButton id="refreshButton" toolTip="Refresh Data"
                              icon="@Embed(source='../../../assets/refresh.png')"
                              label="Refresh" labelPlacement="right" styleName="grayButton"
                              dataSource="{dataSourceInfo}" viewFactory="{dataView}"/>
        <analysis:AdHocMode id="adHocButton" modeChange="dataView.forceRetrieve()" labelPlacement="right"
                            label="Ad Hoc Mode"
                            styleName="grayButton"/>
        <mx:Button icon="@Embed(source='../../../assets/table_x16.png')"
                   toolTip="Export Report" label="Export" labelPlacement="right" styleName="grayButton"
                   id="exportButton"
                   click="exportReport()" enabled="{!loadingReport}"/>
        <mx:Button icon="@Embed(source='../../../assets/table_edit.png')"
                   click="additionalProps()" id="propsButton"
                   toolTip="Edit Report Configuration" label="Additional Configuration" labelPlacement="right"
                   styleName="grayButton"/>
        <analysis:AdditionalActionButton newReportAction="newReport()" newDashboardAction="newDashboard()"
                                         reportViewAction="goReport()" viewInHTMLAction="onViewInHTML()"
                                         configureDataSourceAction="goAdmin()" toolTip="Actions"
                                         icon="@Embed(source='../../../assets/businessman_edit.png')"
                />
        <mx:Button icon="@Embed(source='../../../assets/table_edit.png')"
                   click="showQuickActions()" id="quickActionButton"
                   toolTip="Quick Actions" label="Quick Actions" labelPlacement="right"
                   styleName="grayButton" enabled="{!loadingReport}"/>
    </mx:HBox>
    <mx:VBox height="100%" width="100%" id="reportEditor" disabledOverlayAlpha=".275"
             paddingBottom="10" paddingTop="33">
        <mx:VBox width="100%" id="headerArea" verticalGap="0" backgroundColor="#FFFFFF">

            <filtering:TransformContainer id="transformContainer" paddingLeft="10"
                                          paddingBottom="10"
                                          paddingRight="10" label="Filters"
                                          icon="@Embed(source='../../../assets/funnel.png')"
                                          width="100%" height="100%"
                                          backgroundAlpha="1"
                                          feedID="{dataSourceID}"
                                          analysisItems="{wrappers}" minHeight="40" backgroundColor="#FFFFFF"
                                          report="{analysisDefinition}">
                <mx:Button icon="@Embed(source='../../../assets/funnel.png')"
                           toolTip="Create a New Filter..." click="transformContainer.addNewFilter()"
                           label="New Filter"/>
            </filtering:TransformContainer>
            <mx:UIComponent height="1" id="uiComp2" width="100%"/>
        </mx:VBox>


        <mx:HDividedBox id="hbox1" height="100%" width="100%" paddingLeft="10" paddingRight="10">

            <mx:Canvas height="100%" width="240" borderThickness="1" borderColor="#DCE2F8"
                       borderStyle="solid" dropShadowEnabled="true" x="0" y="0">
                <mx:Box width="100%" backgroundColor="#333333" horizontalAlign="center" verticalAlign="middle"
                        height="28" x="0" y="0">
                    <mx:Label text="{fieldsLabel}" color="0xFFFFFF" fontWeight="bold" maxWidth="220"/>
                </mx:Box>
                <mx:Box backgroundColor="#444444" y="28" width="100%" paddingTop="5" paddingBottom="5"
                        horizontalAlign="center">
                    <mx:Button label="Add a Custom Field" styleName="grayButton" click="customField()"
                               id="customFieldButton"
                               icon="@Embed(source='../../../assets/code.png')"/>
                </mx:Box>
                <mx:Image source="@Embed(source='../../../assets/icon-search-black.png')" x="2" y="62"/>

                <mx:TextInput id="searchBox" x="20" y="60" height="24" width="100%"
                              change="onSearchTextChange(event)"/>
                <easyinsight:AnotherHackDataGrid width="100%" height="100%" dragEnabled="true"
                                                 iconFunction="iconFunction"
                                                 id="fieldGrid" backgroundColor="#FFFFFF"
                                                 alternatingItemColors="#FFFFFF" fontSize="12" rowHeight="28"
                                                 dragStart="onDragStart(event)" dragExit="onDragEnd()"
                                                 dragComplete="onDragComplete(event)"
                                                 showHeaders="false"
                                                 groupItemRenderer="{rendererFactory}" x="0" y="84">
                    <easyinsight:columns>
                        <mx:AdvancedDataGridColumn headerText="" dataField="displayName"/>
                    </easyinsight:columns>
                </easyinsight:AnotherHackDataGrid>
            </mx:Canvas>
            <mx:VBox width="100%" height="100%" id="rightBox">
                <mx:HBox id="reportButtonsBox">
                    <mx:Spacer width="100%" includeInLayout="{reportName != null}"/>
                    <mx:HBox includeInLayout="{reportName != null}" visible="{reportName != null}">
                        <mx:Label text="Report Name:" includeInLayout="{reportName != null}" fontWeight="bold"/>
                        <mx:Label text="{reportName}" includeInLayout="{reportName != null}" maxWidth="275"/>
                    </mx:HBox>
                    <mx:Spacer width="100%"/>
                    <!--<util:SaveButton label="Run Report" click="runReport()" fontSize="12" id="runReportButton"/>
                    <mx:Spacer width="100%" id="runReportSpacer"/>-->

                    <mx:Label text="Report Type:" fontWeight="bold"/>
                    <analysis:AnalysisStatePopupButton analysisStateChange="onAnalysisStateChange(event)"
                                                       id="analysisState"
                                                       mode="{reportMode}" x="91" y="0"/>
                    <mx:Spacer width="100%"/>
                </mx:HBox>
            </mx:VBox>
        </mx:HDividedBox>
    </mx:VBox>
</skin:BackgroundImage>
