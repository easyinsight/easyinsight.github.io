<?xml version="1.0" encoding="utf-8"?>
<util:EITitleWindow xmlns:util="com.easyinsight.util.*" xmlns:mx="http://www.adobe.com/2006/mxml">
	<mx:Script>
		<![CDATA[
import mx.containers.HBox;
import mx.controls.Label;
import mx.controls.CheckBox;
			import mx.managers.PopUpManager;
			import mx.collections.ArrayCollection;
			
			private var compositeFeedConnections:ArrayCollection = new ArrayCollection();



            private function createGroupingMap(connections:ArrayCollection):Object {
                var groupingMap:Object = new Object();
                for each (var sortConnection:ConnectionWithInfo in connections) {
                    var groupingKey:String = sortConnection.sourceFeedID + "." + sortConnection.targetFeedID;
                    var groupingList:ArrayCollection = groupingMap[groupingKey];
                    if (groupingList == null) {
                        groupingList = new ArrayCollection();
                        groupingMap[groupingKey] = groupingList;
                    }
                    groupingList.addItem(sortConnection);
                }
                return groupingMap;
            }
			
			public function set connections(connections:ArrayCollection):void {
                var groupingMap:Object = createGroupingMap(connections);

                for each (var groupingList:ArrayCollection in groupingMap) {
                    var firstConnection:ConnectionWithInfo = groupingList.getItemAt(0) as ConnectionWithInfo;
                    var connectionString:String = "Potential joins between " + firstConnection.sourceFeedName + " and " +
                                                  firstConnection.targetFeedName;
                    var connectionLabel:Label = new Label();
                    connectionLabel.text = connectionString;
                    coreBox.addChild(connectionLabel);
                    for each (var join:ConnectionWithInfo in groupingList) {
                        var joinString:String = join.sourceJoin.createString() + " to " + join.targetJoin.createString();
                        var joinCheckbox:CheckBox = new CheckBox();
                        joinCheckbox.selected = join == firstConnection;
                        var joinHBox:HBox = new HBox();
                        joinHBox.setStyle("paddingLeft", 20);
                        joinHBox.addChild(joinCheckbox);
                        var joinLabel:Label = new Label();
                        joinLabel.text = joinString;
                        joinHBox.addChild(joinLabel);
                        coreBox.addChild(joinHBox);
                        var connectObject:Object = new Object();
                        connectObject["join"] = join;
                        connectObject["checkbox"] = joinCheckbox;
                        compositeFeedConnections.addItem(connectObject);
                    }
                }

				/*for each (var connection:ConnectionWithInfo in connections) {
					var joinDetail:JoinDetail = new JoinDetail();
					joinDetail.feedConnection = connection;
					coreBox.addChild(joinDetail);
				}*/
			}	
			
			private function closeWindow():void {
                var validConnections:ArrayCollection = new ArrayCollection();
                for each (var connectObject:Object in compositeFeedConnections) {
                    var joinCheckbox:CheckBox = connectObject["checkbox"];
                    if (joinCheckbox.selected) {
                        validConnections.addItem(connectObject["join"]);
                    }
                }
                dispatchEvent(new JoinSelectionEvent(validConnections));
				PopUpManager.removePopUp(this);
			}
		]]>
	</mx:Script>
	<mx:Box paddingBottom="10" paddingLeft="10" paddingTop="10" paddingRight="10" styleName="TitleWindowContents">
		<mx:VBox id="coreBox">
			<mx:Label text="The following new joins were discovered. Select or unselect the joins to create the relationships:"/>
			
		</mx:VBox>
		<mx:HBox width="100%" horizontalAlign="center">
			<mx:Button label="Close" click="closeWindow()"/>
		</mx:HBox>		
	</mx:Box>
	
</util:EITitleWindow>
