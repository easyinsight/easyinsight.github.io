<?xml version="1.0" encoding="utf-8"?>
<FullScreenPage xmlns="com.easyinsight.*" xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%"
                xmlns:feedassembly="com.easyinsight.feedassembly.*"
                paddingTop="10" paddingLeft="0" paddingRight="0" paddingBottom="0"
        implements="com.easyinsight.framework.IFullScreenPage">
	<mx:Script>
		<![CDATA[
        import com.easyinsight.administration.feed.FeedChangeEvent;
        import com.easyinsight.genredata.AnalyzeEvent;
        import com.easyinsight.listing.DescriptorAnalyzeSource;
        import com.easyinsight.util.PopUpUtil;
        import com.easyinsight.util.ProgressAlert;

        import mx.controls.Alert;
        import mx.events.CloseEvent;
        import mx.managers.PopUpManager;
        import mx.collections.ArrayCollection;

        import com.easyinsight.analysis.AnalysisCloseEvent;

        [Bindable]
        private var _feedList:ArrayCollection;

        private var saveNeeded:Boolean;

        [Bindable]
        private var feedDefinition:CompositeFeedDefinition;

        private function createdFeed(event:CompositeFeedSaveEvent):void {
            saveNeeded = false;
            feedDefinition = event.feedDefinition;
        }

        private function cancel():void {
            if (saveNeeded) {
                Alert.show("You have unsaved changes. Are you sure you want to exit?", "Alert",
                        Alert.OK | Alert.CANCEL, null, alertListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new AnalysisCloseEvent(this));
            }
        }

        private function alertListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new AnalysisCloseEvent(this));
            }
        }

        private function onFeedChange(event:FeedChangeEvent):void {
            saveNeeded = true;
        }

        private function save():void {
            var compositeFeedNodes:ArrayCollection = workspace.createNodes();
            var compositeFeedConnections:ArrayCollection = workspace.createEdges();
            var saveWindow:SaveCompositeFeedWindow = new SaveCompositeFeedWindow();
            if (workspace.compositeFeedID == 0) {
                saveWindow.compositeFeedNodes = compositeFeedNodes;
                saveWindow.compositeFeedConnections = compositeFeedConnections;
                saveWindow.addEventListener(CompositeFeedSaveEvent.COMPOSITE_FEED_SAVE, createdFeed, false, 0, true);
                PopUpManager.addPopUp(saveWindow, this, true);
                PopUpUtil.centerPopUp(saveWindow);
            } else {
                ProgressAlert.alert(this, "Updating...", "Updated data source.", feedService.updateFeedDefinition);
                feedService.updateFeedDefinition.send(feedDefinition, "");
            }
        }

        private function updated():void {
            saveNeeded = false;
        }

        public function addFeeds(feedList:ArrayCollection):void {
            this._feedList = feedList;
        }

        override public function cleanup():void {
        }

        private function analyze():void {
            if (saveNeeded) {
                Alert.show("You have unsaved changes. Are you sure you want to exit?", "Alert",
                        Alert.OK | Alert.CANCEL, null, analyzeListener, null, Alert.CANCEL);
            } else {
                dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(feedDefinition.dataFeedID, feedDefinition.feedName)));
            }
        }

        private function analyzeListener(event:CloseEvent):void {
            if (event.detail == Alert.OK) {
                dispatchEvent(new AnalyzeEvent(new DescriptorAnalyzeSource(feedDefinition.dataFeedID, feedDefinition.feedName)));
            }
        }
		]]>
	</mx:Script>
    <mx:RemoteObject destination="feeds" id="feedService">
		<mx:method name="updateFeedDefinition" result="updated()"/>
	</mx:RemoteObject>
	<mx:VBox width="100%" height="100%">
		<mx:HBox paddingLeft="20">
            <mx:Button icon="@Embed(source='../../../../assets/document_out.png')" click="cancel()"
                       toolTip="Close"/>
            <mx:Button icon="@Embed(source='../../../../assets/floppy_disk.png')" click="save()"
                           toolTip="Save"/>
            <mx:Button icon="@Embed(source='../../../../assets/media_play_green.png')" click="analyze()"
                           toolTip="Analyze" enabled="{feedDefinition != null}"/>
		</mx:HBox>
		<feedassembly:CompositeWorkspace id="workspace" width="100%" height="100%" feeds="{_feedList}" feedChange="onFeedChange(event)"/>
	</mx:VBox>
</FullScreenPage>
