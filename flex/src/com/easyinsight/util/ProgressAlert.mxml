<?xml version="1.0" ?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" backgroundColor="#D8DBEE" borderThickness="1" borderStyle="solid" borderColor="0x000000"
           cornerRadius="15" dropShadowEnabled="true" addedToStage="setupListeners()">
    <mx:states>
        <mx:State name="Done">
            <mx:RemoveChild target="{progressBox}"/>
            <mx:AddChild relativeTo="{coreBox}">
                <mx:VBox horizontalAlign="center">
                    <mx:Label text="{completeText}" fontWeight="bold" fontSize="14" fontFamily="Tahoma"/>
                    <mx:Button label="OK" click="complete()"/>
                </mx:VBox>
            </mx:AddChild>
        </mx:State>
        <mx:State name="Failure">
            <mx:RemoveChild target="{progressBox}"/>
            <mx:AddChild relativeTo="{coreBox}">
                <mx:VBox horizontalAlign="center">
                    <mx:Label fontWeight="bold"
                                 text="Sorry, but we encountered the following error in trying to complete the operation:" fontSize="14" fontFamily="Tahoma"/>
                    <mx:TextArea borderStyle="none" backgroundAlpha="0" text="{errorText}" width="400" height="200"/>
                    <mx:Button label="Cancel" click="cancel()"/>
                </mx:VBox>
            </mx:AddChild>
        </mx:State>
    </mx:states>
    <mx:Script><![CDATA[
        import mx.collections.ArrayCollection;
        import mx.events.CloseEvent;
        import mx.events.FlexEvent;
        import mx.managers.PopUpManager;
        import mx.rpc.events.FaultEvent;
        import mx.rpc.events.ResultEvent;
        import mx.rpc.remoting.Operation;

        private function onKey(event:KeyboardEvent):void {
            if (event.keyCode == Keyboard.ESCAPE) {
                cancel();
            } else if (event.keyCode == Keyboard.ENTER) {
                if (currentState == "Done") {
                    complete();
                }
            }
        }

        private function setupListeners():void {
            stage.addEventListener(KeyboardEvent.KEY_UP, onKey, false, 0, true);
        }

        private var _progressText:String;
        private var _completeText:String;
        [Bindable]
        private var errorText:String;

        //private var _operation:Operation;

        private var _operations:ArrayCollection;

        private var opCount:int;

        private var failed:Boolean;

        private function cancel():void {
            if (stage != null) {
                stage.removeEventListener(KeyboardEvent.KEY_UP, onKey);
            }
            for each (var operation:Operation in _operations) {
                operation.removeEventListener(ResultEvent.RESULT, onResult);
                operation.removeEventListener(FaultEvent.FAULT, onFault);
            }
            PopUpManager.removePopUp(this);
        }

        private function complete():void {
            stage.removeEventListener(KeyboardEvent.KEY_UP, onKey);
            for each (var operation:Operation in _operations) {
                operation.removeEventListener(ResultEvent.RESULT, onResult);
                operation.removeEventListener(FaultEvent.FAULT, onFault);
            }
            PopUpManager.removePopUp(this);
            dispatchEvent(new CloseEvent(CloseEvent.CLOSE));
        }

        public function set operations(val:ArrayCollection):void {
            for each (var operation:Operation in val) {
                operation.addEventListener(ResultEvent.RESULT, onResult, false, 0, true);
                operation.addEventListener(FaultEvent.FAULT, onFault, false, 0, true);
            }
            _operations = val;
        }

        [Bindable]
        public function get completeText():String {
            return _completeText;
        }

        public static function alert(parent:DisplayObject, progressText:String, completeText:String, ... operations):ProgressAlert {
            var alert:ProgressAlert = new ProgressAlert();
            alert.progressText = progressText;
            alert.completeText = completeText;
            alert.operations = new ArrayCollection(operations);
            PopUpManager.addPopUp(alert, parent, true);
            PopUpUtil.centerPopUp(alert);
            return alert;
        }

        private function onResult(event:ResultEvent):void {
            opCount++;
            if (!failed && opCount == _operations.length) {
                if (_completeText == null) {
                    PopUpManager.removePopUp(this);
                } else {
                    currentState = "Done";
                }
            }
        }

        private function onFault(event:FaultEvent):void {
            failed = true;
            errorText = event.fault.faultString;
            currentState = "Failure";
        }

        public function set completeText(val:String):void {
            _completeText = val;
            dispatchEvent(new FlexEvent(FlexEvent.DATA_CHANGE));
        }

        [Bindable]
        public function get progressText():String {
            return _progressText;
        }

        public function set progressText(val:String):void {
            _progressText = val;
            dispatchEvent(new FlexEvent(FlexEvent.DATA_CHANGE));
        }]]></mx:Script>
    <mx:VBox id="coreBox" width="100%" height="100%" horizontalAlign="center" verticalAlign="middle"
             paddingBottom="20" paddingLeft="20" paddingRight="20" paddingTop="20">
        <mx:VBox id="progressBox" horizontalAlign="center">
            <mx:Label text="{progressText}" fontWeight="bold" fontSize="14" fontFamily="Tahoma"/>
            <mx:ProgressBar indeterminate="true"/>
            <mx:Button label="Cancel" click="cancel()"/>
        </mx:VBox>
    </mx:VBox>
</mx:Canvas>