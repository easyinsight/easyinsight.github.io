<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="startWorkspace()" xmlns:local="*" 
	xmlns:listing="listing.*" xmlns:customupload="customupload.*" xmlns:genredata="genredata.*"
	xmlns:intro="intro.*" xmlns:listing1="com.easyinsight.listing.*" xmlns:intro1="com.easyinsight.intro.*" 
	historyManagementEnabled="false" paddingTop="0" top="0" borderThickness="0" left="0" paddingLeft="0" right="0" paddingRight="0"
	bottom="0" paddingBottom="0" preloader="com.easyinsight.preloader.EIProgressBar">
	<mx:states>
		<mx:State name="Analysis">
			<mx:RemoveChild target="{headerBar}"/>
			<mx:RemoveChild target="{coreSpace}"/>
			<mx:AddChild relativeTo="{coreVBox}" target="{dataAnalysis}"/>
		</mx:State>
	</mx:states>
	<mx:Script>
		<![CDATA[
import com.easyinsight.analysis.DelayedAPIKeyLink;
import com.easyinsight.framework.FullScreenModuleEvent;
import com.easyinsight.framework.ModuleAnalyzeSource;
import com.easyinsight.genredata.ModuleAnalyzeEvent;
import com.easyinsight.groups.DelayedGroupLink;
import com.easyinsight.intro.WelcomeScreen;
			import com.easyinsight.FullScreenPage;
			import com.easyinsight.analysis.DelayedFeedLink;
			import com.easyinsight.analysis.DelayedDeepLink;
			import com.easyinsight.listing.PageFactory;
import mx.messaging.ChannelSet;
			import mx.messaging.config.ServerConfig;
			import mx.rpc.events.FaultEvent;
			import com.easyinsight.LoginDialog;
			import com.easyinsight.framework.SessionExpirationEvent;
			import com.easyinsight.framework.UserServiceResponse;
			import com.easyinsight.analysis.AnalysisCloseEvent;
			import com.easyinsight.framework.NavigationEvent;
			import mx.controls.Alert;
			import mx.utils.URLUtil;
			import mx.managers.IBrowserManager;
			import mx.events.BrowserChangeEvent;
			import mx.managers.BrowserManager;
            import com.easyinsight.genredata.AnalyzeEvent;
            import com.easyinsight.listing.AnalyzeSource;
            import com.easyinsight.listing.IPerspective;
            import com.easyinsight.listing.KeywordSearchEvent;
            import com.easyinsight.listing.ListingChangeEvent;
            import com.easyinsight.commands.CommandProcessor;
            import com.easyinsight.framework.User;
            import com.easyinsight.framework.LoginEvent;
            import mx.managers.PopUpManager;

            private var perspective:IPerspective;
            
            [Bindable]
            private var dataAnalysis:DisplayObject;
            
            public var browserManager:IBrowserManager;

            private function startWorkspace():void {
                userService.isSessionLoggedIn.send();                
                new CommandProcessor();
                addEventListener(FaultEvent.FAULT, onFault, true);
                this.perspective = introPanel;
                browserManager = BrowserManager.getInstance();
                browserManager.addEventListener(BrowserChangeEvent.BROWSER_URL_CHANGE, parseURL);
                browserManager.init("", "Intro");
                introPanel.gotFocus();
                User.getEventNotifier().addEventListener(NavigationEvent.NAVIGATION, navigation);
            }
            
            private function onFault(event:FaultEvent):void {
            	var fatal:Boolean = false;
            	if (event.fault.faultString.indexOf("com.easyinsight.security.SecurityException") != -1) {
            		var channelSet:ChannelSet = ServerConfig.getChannelSet("login");
            		if (channelSet.authenticated) {
            			// session expired
            			fatal = true;
            			ExternalInterface.call("window.location.reload()");
                        trace("blah");
            		}            		
            	}
            	if (!fatal) {
            		Alert.show(event.fault.name, "A server error occurred.");
            	}
            }
            
            private function sessionExpired(event:SessionExpirationEvent):void {
            	var loginDialog:LoginDialog = LoginDialog(PopUpManager.createPopUp(this, LoginDialog, true));
            	PopUpManager.centerPopUp(loginDialog);
            }
            
            private function navigation(event:NavigationEvent):void {
                if (event.targetPerspective == null) {
                    headerBar.explicitChange(event.targetPage);
                } else {
                    changePerspective(new ListingChangeEvent(event.targetPerspective));
                }
            }

            private function activated():void {
                var loginDialog:LoginDialog = new LoginDialog();
                loginDialog.showActivation = true;
                PopUpManager.addPopUp(loginDialog, this, true);
                PopUpManager.centerPopUp(loginDialog);
                User.getEventNotifier().addEventListener(LoginEvent.LOGIN, onLogin);
                //navigation(new NavigationEvent(null, new WelcomeScreen()));
            }

            private function onLogin(event:LoginEvent):void {
                User.getEventNotifier().removeEventListener(LoginEvent.LOGIN, onLogin);
                navigation(new NavigationEvent("Intro"));
            }
            
            private function parseURL(event:Event):void {
            	var o:Object = URLUtil.stringToObject(browserManager.fragment);
            	var analysisID:String = o.analysisID;
            	if (analysisID != null) {
        			//createAnalysisSource(Number(analysisID));
        			var deepAnalysisLink:DelayedDeepLink = new DelayedDeepLink(Number(analysisID));
        			deepAnalysisLink.addEventListener(ModuleAnalyzeEvent.MODULE_ANALYZE, internalModuleAnalyze);
        			deepAnalysisLink.execute(); 
            	} else {
            		var feedID:String = o.feedID;
            		if (feedID != null) {
            			var deepFeedLink:DelayedFeedLink = new DelayedFeedLink(Number(feedID));
            			deepFeedLink.addEventListener(ModuleAnalyzeEvent.MODULE_ANALYZE, internalModuleAnalyze);
            			deepFeedLink.execute();            			
            		} else {
                        var feedKey:String = o.feedKey;
                        if (feedKey != null) {
                            var deepAPILink:DelayedAPIKeyLink = new DelayedAPIKeyLink(feedKey);
                            deepAPILink.addEventListener(ModuleAnalyzeEvent.MODULE_ANALYZE, internalModuleAnalyze);
                            deepAPILink.execute();
                        } else {
                            var groupID:String = o.groupID;
                            if (groupID != null) {
                                var groupLink:DelayedGroupLink = new DelayedGroupLink(Number(groupID));
                                groupLink.execute();
                            } else {
                                var activationString:String = o.activationID;
                                if (activationString != null) {
                                    userService.activateAccount.send(activationString);
                                } else {
                                    var page:String = o.page;
                                    if (page != null) {
                                        if (currentState == "Analysis") {
                                            currentState = "";
                                            dataAnalysis.removeEventListener(AnalysisCloseEvent.ANALYSIS_CLOSE, analysisClosed);
                                            dataAnalysis = null;
                                        }
                                        switch (page) {
                                            case "myData":
                                                navigation(new NavigationEvent("My Data"));
                                                break;
                                            case "myGoals":
                                                navigation(new NavigationEvent("Goals"));
                                                break;
                                            case "marketplace":
                                                navigation(new NavigationEvent("Marketplace"));
                                                break;
                                            case "groups":
                                                navigation(new NavigationEvent("Groups"));
                                                break;
                                            case "groupDetail":
                                                break;
                                            case "intro":
                                                navigation(new NavigationEvent("Intro"));
                                                break;
                                            case "solutions":
                                                navigation(new NavigationEvent("Solutions"));
                                                break;
                                            case "account":
                                                navigation(new NavigationEvent("My Account"));
                                                break;
                                            case "welcome":
                                                navigation(new NavigationEvent(null, new WelcomeScreen()));
                                        }
                                    }
                                }
                            }
                        }
            		}
            	}
            }
            
            private function doAnalyze(analyzeSource:AnalyzeSource):void {
            	if (analyzeSource != null) {
            		var analysisWindow:FullScreenPage = analyzeSource.createAnalysisPopup();
            		analysisWindow.addEventListener(AnalysisCloseEvent.ANALYSIS_CLOSE, analysisClosed);
            		dataAnalysis = analysisWindow;
            		currentState = "Analysis";
            	}
            }

            private function analysisClosed(event:AnalysisCloseEvent):void {
            	currentState = "";
            	event.dataAnalysisContainer.removeEventListener(AnalysisCloseEvent.ANALYSIS_CLOSE, analysisClosed);
            	dataAnalysis = null;
            	this.perspective.gotFocus();
            }

            private function changePerspective(event:ListingChangeEvent):void {
            	if (event.perspective != null) {
                    if (this.perspective != null) {
                        var existingDispatcher:EventDispatcher = this.perspective as EventDispatcher;
                        existingDispatcher.removeEventListener(AnalyzeEvent.ANALYZE, internalAnalyze);
                        existingDispatcher.removeEventListener(ModuleAnalyzeEvent.MODULE_ANALYZE, internalModuleAnalyze);
                    }
                    coreSpace.removeChildAt(0);
                    this.perspective = event.perspective;
                    coreSpace.addChildAt(perspective as DisplayObject, 0);
                    perspective.gotFocus();
                    var dispatcher:EventDispatcher = perspective as EventDispatcher;
                    dispatcher.addEventListener(AnalyzeEvent.ANALYZE, internalAnalyze);
                    dispatcher.addEventListener(ModuleAnalyzeEvent.MODULE_ANALYZE, internalModuleAnalyze);
                }
            }

            private function search(event:KeywordSearchEvent):void {
                //getCurrentPerspective().search(event.keyword);
            }

            private function getCurrentPerspective():IPerspective {
                return this.perspective;
            }

            private function internalAnalyze(event:AnalyzeEvent):void {
                var analyzeSource:AnalyzeSource = event.analyzeSource;
                doAnalyze(analyzeSource);
            }

            private function internalModuleAnalyze(event:ModuleAnalyzeEvent):void {
                var moduleAnalyzeSource:ModuleAnalyzeSource = event.analyzeSource;
                var analysisWindow:DisplayObject = moduleAnalyzeSource.createDirect();
                analysisWindow.addEventListener(AnalysisCloseEvent.ANALYSIS_CLOSE, analysisClosed);
                dataAnalysis = analysisWindow;
                currentState = "Analysis";
                /*moduleAnalyzeSource.addEventListener(FullScreenModuleEvent.MODULE_LOADED, moduleLoaded);
                moduleAnalyzeSource.startModuleLoading();*/
            }

            private function moduleLoaded(event:FullScreenModuleEvent):void {
                dataAnalysis = event.val;
                dataAnalysis.addEventListener(AnalysisCloseEvent.ANALYSIS_CLOSE, analysisClosed);
                currentState = "Analysis";
            }
            
            private function sessionCheck():void {
            	var authResult:UserServiceResponse = userService.isSessionLoggedIn.lastResult as UserServiceResponse;
            	if (authResult != null) {
            		User.initializeUser(authResult.name, authResult.email,
                            	authResult.accountType, authResult.spaceAllowed, authResult.accountAdmin,
                            authResult.dataSourceCreator, authResult.insightCreator, authResult.userID);
                	User.getInstance().password = authResult.encryptedPassword;
                	User.getInstance().userName = authResult.userName;
                    User.getEventNotifier().dispatchEvent(new LoginEvent(LoginEvent.LOGIN));
            	}
            }                       
        ]]>
	</mx:Script>
	<mx:Style source="com/easyinsight/skin/osx/OSX.css"/>

	<mx:RemoteObject id="userService" destination="login">
		<mx:method name="isSessionLoggedIn" result="sessionCheck()"/>
		<mx:method name="activateAccount" result="activated()"/>
	</mx:RemoteObject>
	
	<mx:VBox width="100%" height="100%" id="coreVBox" paddingLeft="0" verticalGap="0" paddingRight="0">
		<listing1:HeaderBar keywordSearch="search(event)" width="100%" id="headerBar"
			paddingTop="5" paddingBottom="5" paddingLeft="10" paddingRight="10" bottom="0" top="0" 
			listingChange="changePerspective(event)"/>			
		<mx:HBox width="100%" height="100%" id="coreSpace" paddingLeft="0" paddingTop="0" top="0" paddingRight="0">
									
				<intro1:IntroPanel id="introPanel" width="100%" height="100%" paddingTop="0" navigation="navigation(event)"/>
						
		</mx:HBox>
	</mx:VBox>
	
</mx:Application>
